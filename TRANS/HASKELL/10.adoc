include::header.adoc[]

== Функціональне вирішення проблем

В цій главі ми поглянемо на деякі цікаві проблеми, і як мислити функціонально, щоб вирішувати їх настільки елегантно, наскільки це можливо. Можливо ми не будемо вводити нові концепції, ми будемо тільки розминати наші тільки що отримані м'язи Haskell і попрактикуємось в навичках кодування. Кожний розділ буде представляти іншу проблему. Спочатку ми опишемо проблему, потім ми спробуємо знайти, який є кращий (або найменьш поганий) шлях для його вирішення.

=== Калькулятор зворотньої польської нотації

Звичайно, коли ви пишете математичний вираз в школі, ми пишемо його в інфіксній манері. Наприклад, ми пишемо `10 - (4 + 3) * 2`. `+`, `*` та `-` є інфіксні оператори, так само як інфіксні функції, які ми зустрічали в Haskell (`+`, `\`elem`, тощо.). Це робить все зручним, оскількі ми, як люди, можемо це легко розібрати подумки, дивлячись на такий різновид виразів. Недолік цього в тому, що нам треба використовувати дужки для позначення преоритетів.

Зворотня польська нотація є іншим способом записувати математичні вирази. Спочатку вона виглядає дещо схибленою, але насправді її досить легко зрозуміти і використовувати, бо немає потреби в дужках, і її дуже легко вбити в калькулятор. Хоча більшість сучасних калькуляторів використовують інфіксну нотацію, деякі користувачі досі віддані RPN калькуляторам. Ось як виглядає попередній інфіксний вираз в RPN: `10 4 3 + 2 * -`. Як ми обчислюємо результат, що буде отриманий? Гаразд, уявіть собі стек. Ви ідете по виразу зліва направо. Кожного разу коли трапляється число, заштовхуємо його в стек. Коли нам трапляється оператор, беремо два числа, що знаходяться нагорі стеку (ми кажемо, що також виштовхуємой їх), застосовуємо оператор для ціх двох, та потім заштовхуємо результуюче число назад в стек. Коли ви дійдете до кінця виразу, ви повинні залишитись з одним числом, якщо вираз був гарно сформований, і це число представляє результат.

image:images/rpn.png[]
Давайте пройдемо по виразу `10 4 3 + 2 * -` разом! Спочатку ми заштовхуємо `10` в стек, і тепер стек `10`. Наступний елемент є `4`, так що ми заштовхуємо його в стек також. Тепер стек `10, 4`. Ми робимо те саме з `3`, і тепер стек `10, 4, 3`. І тепер ми натрапили на оператор, а саме `+`! Ми виштовхуємо два числа зі стеку (так що в стеку лише `10`), додаємо ці числа разом і заштовхуємо цей результат в стек. Тепер стек `10, 7`. Ми заштовхуємо `2` до стеку, стек тепер стає `10, 7, 2`. Ми знову натрапляємо на оператор, так що ми виштовхуємо `7` та `2` зі стеку, перемножуємо їх і заштовхуємо результат в стек. Множення `7` та `2` продукує `14`, так що тепер ми маємо в стеку `10, 14`. Нарешті маємо `-`. Ми виштовхуємо `10` та `14` зі стеку, віднімаємо `14` з `10`, і заштовхуємо його назад. Число в стеку тепер `-4`, і оскільки більше немає чисел або операторів в нашому виразі, це і є результат!

Тепер, коли ми знаємо як обчислити любий RPN вираз вручну, давайте подумаємо, як ми можемо створити функцію Haskell, що приймає в якості свого параметру рядок, що містить RPN вираз, як `"10 4 3 + 2 * -"` і видає назад його результат.

Який буде результат цієї функції? Ми хотіли б, що би вона брала рядок як параметр, і продукувала число в якості результата. Так що буде щось подібне до `solveRPN ::(Num a) => String -> a`.

Підказка: це дійсно допомагає спочатку подумати, який має бути тип декларації функції, перед тим як обтяжувати себе реалізацією, та потім записати її. В Haskell декларація типу функції каже нам повну інформацію про функцію через дуже жорстку систему типів.

image:images/calculator.png[]
Круто. Коли реалізується рішення проблеми в Haskell, також є гарним подумати, як це робиться вручну, і моживо спробувати подивитись, чи можливо отримати з цього якесь прозріння. Тут ми бачимо, що ми розглядаємо кожне число або оператор, що були розділені проміжками, як окремий елемент. Так що нам може стати в нагоді, якщо ми почнемо з розбиття рядка як `"10 4 3 + 2 * -"` в список елементів `["10","4","3","+","2","*","-"]`.

Далі що ми робили з цім списком елементів в нашій голові? Ми проходили по ньому зліва направо, і по ходу цього підтримували стек. Чи попереднє речення не нагадує вам щось? Пам'ятаєте, в розділі про згортки ми казали, що майже кожну функцію, що проходить по списку зліва направо або зправа наліво елемент за елементом, і будує (акумулює) деякий результат (чи число, чи список, стек або будь-що) може бути реалізована як згортка.

В цьому випадку ми збираємось використовувати ліву згортку, оскільки ми йдемо по списку зліва направо. Значення аккумулятора буде наш стек, і такм чином результат згортки також буде стек, який, як ми бачили, буде мати лише один елемент.

Одна річ, про яку нам треба подумати, гаразд, як ми збираємось представляти стек? Я пропоную використовувати список. Також я пропоную, щоб ми тримали верхівку нашого стеку в голові нашого списку. Це тому що додавання в голову (початок) списку значно швидше, ніж додавання в його кінець. Так що якщо ми маємо стек, скажімо, `10, 4, 3`, ми представлятимо його як список `[3,4,10]`.

Тепер ми маємо досить інформації щоб грубо накидати нашу функцію. Вона буде приймати рядок, як `"10 4 3 + 2 * -"` та розбивати його на список елементів за допомогою `words`, щоб отримати `["10","4","3","+","2","*","-"]`. Далі ми робитимемо ліву згортку та скінчимо зі стеком, що має один елемент, `[-4]`. Ми отримуємо цей поодинокий елмент зі списку, і це є наш фінальний результат!

Так що ось ескіз цієї функції:

[source,scala]
----
import Data.List  
  
solveRPN :: (Num a) => String -> a  
solveRPN expression = head (foldl foldingFunction [] (words expression))  
    where   foldingFunction stack item = ...  
----

Ми беремо вираз і перетворюємо його на список елементів. Потім ми робимо згортання по цьому списку елементів за допомогою функції згортки. Подумайте про `[]`, що представляє початковий аккумулятор. Аккумулятором є наш стек, так що `[]` представляє порожній стек, що є те з чого ми починаємо. Після отримання фінального стеку з одним елементом ми викликаємо `head` на цьому списку, щоб отримати елемент і потім застосовуємо `read`.

Так що все, що тепер залишилось, це реалізувати функцію згортки, що буде приймати стек, як `[4,10]` та елемент, як `"3"`, та повертає новмй стек `[3,4,10]`. Якщо стек був `[4,10]` і елемент `"*"`, тоді пона має повертати `[40]`. Але перед цім давайте перетворимо нашу функцію в крапковому стилі, оскільки вона має багато дужок, що ніби дещо лякають мене:

[source,scala]
----
import Data.List  
  
solveRPN :: (Num a) => String -> a  
solveRPN = head . foldl foldingFunction [] . words  
    where   foldingFunction stack item = ...  
----

Отак. Значно краще. Так що функція згортки буде приймати стек і елемент і повертати новий стек. Ми будемо використовувати співпадіння шаблонів для отримання верхніх елементів стеку та співпадіння шаблонів для операторів як `"*"` та `"-"`.

[source,scala]
----
solveRPN ::(Num a, Read a) => String -> a  
solveRPN = head . foldl foldingFunction [] . words  
    where   foldingFunction (x:y:ys) "*" = (x * y):ys  
            foldingFunction (x:y:ys) "+" = (x + y):ys  
            foldingFunction (x:y:ys) "-" = (y - x):ys  
            foldingFunction xs numberString = read numberString:xs  
----

Ми розклали це як чотири шаблони. Шаблони будуть випробовуватись згори донизу. Спочатку функція згортки буде дивитись, чи поточний елемент є `"* "`. Якщо так, тоді вона братиме список як `[3,4,9,3]` та позначатиме перші два елементи `x` та `y` відповідно. Так що в цьому випадку `x` буде `3` і `y` буде `4`. `ys` буде `[9,3]`. Вона повертатиме список, що подібний до `ys`, тільки він матиме перемножені `x` та `y` в якості голови. Так що маючи це ми виштовхуємо два верхні числа зі списку, множимо їх і заштовхуємо результат назад в стек. Якщо елемент не `"*"`, співпадіння шаблону провалиться далі і буде перевірятись `"+"`, і так далі.

Якщо елемент не один з операторів, тоді ми вважаємо що це рядок, що представляє число. Якщо це число ми просто викликаємо `read` на цьому рядку, щоб отримати з нього число, і повертаємо попередній стек, але з цім числом, покладеним зверху.

І це все! Також зауважте, що ми додали додаткове обмеження класу `Read a` до декларації функції, бо ми викликаємо `read` на нашому рядку для отримання числа. Так що ця декларація означає, що результат може бути любого типу, що є частиною типокласів `Num` та `Read` (як `Int`, `Float`, etc.).

Для списку елементів `["2","3","+"]` наша функція почне згортати зліва. Початковий стек буде `[]`. Вона буде викликати функцію згортки з `[]` в якості стека (аккумулятора) і `"2"` в якості елемента. Оскільки цей елемент не оператор, він буде прочитаний `read` і доданий в початок `[]`. Так що новий стек тепер `[2]` і функція згортання буде викликана з `[2]` та `"3"` в якості елемента, продукуючи новий стек `[3,2]`. Тоді вона буде викликана третій раз з  `[3,2]` як стек і `"+"` як елемент. Це призеде до того що ці два числа будуть виштовхнуті зі стеку, додані разом і заштовхнуті назад. Фінальний стек є `[5]`, що є числом яке ми повертаємо.

Давайте пограємось з нашею функцією:

----
ghci> solveRPN "10 4 3 + 2 * -"  
-4  
ghci> solveRPN "2 3 +"  
5  
ghci> solveRPN "90 34 12 33 55 66 + * - +"  
-3947  
ghci> solveRPN "90 34 12 33 55 66 + * - + -"  
4037  
ghci> solveRPN "90 34 12 33 55 66 + * - + -"  
4037  
ghci> solveRPN "90 3 -"  
87  
----

Круто, вона робить! Одна гарна річ щодо цієї функції, що вона може бути просто модифікована для підтримки різних інших операторів. Вони навіть не мають бути двомісними операціями. Наприклад ми можемо створити оператор `"log"` що просто бере одне число зі стеку, та заштовхує назад його логарифм. Ми також можемо створити тримісну операцію, що виштовхує три числа зі стеку і заштовхує назад результат, або оператори як `"sum"`, що виштовхують всі числа і повертають назад їх суму.

Давайте модифікуємо нашу функцію, щоб вона сприймала ще декілька операторів. Для спрощення ми змінимо її декларацію типу, щоб вона повертала число типу `Float`.

[source,scala]
----
import Data.List  
  
solveRPN :: String -> Float  
solveRPN = head . foldl foldingFunction [] . words  
    where   foldingFunction (x:y:ys) "*" = (x * y):ys  
            foldingFunction (x:y:ys) "+" = (x + y):ys  
            foldingFunction (x:y:ys) "-" = (y - x):ys  
            foldingFunction (x:y:ys) "/" = (y / x):ys  
            foldingFunction (x:y:ys) "^" = (y ** x):ys  
            foldingFunction (x:xs) "ln" = log x:xs  
            foldingFunction xs "sum" = [sum xs]  
            foldingFunction xs numberString = read numberString:xs  
----

Це йо, це чудово! `/` це звичайно ділення, та `**` є експонента з плаваючою крапкою. З оператором логарифму ми робимо співпадіння шаблону тільки для одного елементу і решти списку, бо нам треба лише один елемент для обчислення натурального логарифму. З оператором `sum` ми повертаємо стек, що містить всього один елемент, що є сумою всіх елементів, які були в стеку до цього.

----
ghci> solveRPN "2.7 ln"  
0.9932518  
ghci> solveRPN "10 10 10 10 sum 4 /"  
10.0  
ghci> solveRPN "10 10 10 10 10 sum 4 /"  
12.5  
ghci> solveRPN "10 2 ^"  
100.0  
----

Зуважте, що ми можемо включати в наш вираз числа з плаваючою крапкою, оскільки `read` знає як їх читати.

----
ghci> solveRPN "43.2425 0.5 ^"  
6.575903  
----

Я думаю що створити функцію, яка може обчислювати довільні RPN вирази з плаваючою крапкою, і що може бути просто розширеня в десяти рядках, є повністю неймовірним.

Одна річ, що треба відмітити, це те, що ця функція не є насправді стійкою до збоїв. Коли надається ввод що не має сенсу, це просто все зруйнує. Ми створимо захищену версію з декларацією типу `solveRPN ::String -> Maybe Float` як тільки дізнаємось про монади (вони не страшні, вірте мені!). Ми можемо зробити це прямо зараз, але це буде трохи обтяжливо, оскільки це включатиме багато перевірок на `Nothing` на кожному кроку. Однак якщо ви відчуваєте себе готовим до виклику, ви можете спробувати зробити це! Підказка: ви можете використовувати `reads` щоб побачити, чи `read` був вдалим чи ні.

=== З Хітроу до Лондона

Наступна наша проблема така: ваш літак щойно приземлився в Англії, і ви орендуєте машину. У вас незабаром зустріч, і ви повинні дістатися з аеропорту Хітроу до Лондона якнайшвидше (але безпечно!).

Є дві головні дороги, що йдуть від Хітроу до Лондона, і їх перетинає ряд регіональних доріг. Щоб подорожувати з одного перехрестя на інший, вам знадобиться певний час. Вам належить знайти оптимальний шлях, щоб пройти до Лондона якнайшвидше! Ви починаєте з лівого боку і можете переходити на іншу головну дорогу або їхати вперед.

image:images/roads.png[]
Як ви бачите на малюнку, найкоротший шлях від Хітроу до Лондона в цьому випадку - починати на головній дорозі B, переходити, їхати вперед по А, переходити знову і потім двічі йти по Б. Якщо ми підемо цим шляхом, це займає у нас 75 хвилин. Якби ми обрали будь-який інший шлях, це зайняло б більше.

Our job is to make a program that takes input that represents a road system and print out what the shortest path across it is. Here's what the input would look like for this case:
Наша робота полягає в тому, щоб зробити програму, яка бере вхід, що представляє дорожню систему, і роздрукувує найкоротший шлях. Ось як буде виглядати вхід для цього випадку:

----
50  
10  
30  
5  
90  
20  
40  
2  
25  
10  
8  
0  
----

Щоб подумки розібрати вхідний файл, прочитайте його по трійках, і подумки розділіть дорожню систему на ділянки. Кожна ділянка складається з дороги А, дороги Б та перехресної дороги. Щоб воно було акуратно вписане в трійки, ми говоримо, що є остання секція перетину, на яку потрібно їхати 0 хвилин. Це тому, що нам байдуже, куди ми приїдемо в Лондон, якщо ми вже в Лондоні.

Так само, як ми вирішували проблему з калькулятором RPN, ми вирішимо цю проблему в три етапи:

* Забудьте на хвилину Haskell і подумайте, як би ми вирішили проблему вручну
* Подумайте, як ми будемо представляти наші дані в Haskell
* З’ясуйте, як оперувати цими даними в Haskell, щоб ми виробляли рішення

У розділі калькулятора RPN ми спершу з’ясували, що, обчислюючи вираз вручну, ми зберігаємо свого роду уявний стек, а потім проходимо по виразу по одному кроку за раз. Ми вирішили використати список рядків для представлення нашого виразу. Нарешті, ми використали ліву згортку, щоб перейти до списку рядків, зберігаючи стек для рішення.

Okay, so how would we figure out the shortest path from Heathrow to London by hand? Well, we can just sort of look at the whole picture and try to guess what the shortest path is and hopefully we'll make a guess that's right. That solution works for very small inputs, but what if we have a road that has 10,000 sections? Yikes! We also won't be able to say for certain that our solution is the optimal one, we can just sort of say that we're pretty sure.
Гаразд, то як би ми вручну визначили найкоротший шлях з Хітроу до Лондона? Що ж, ми можемо просто поглянути на всю картину і спробувати вгадати, який шлях найкоротший, сподіваючись, що наша здогадка вірна. Це рішення працює для дуже малих входів, але що робити, якщо у нас є дорога, яка має 10 000 ділянок? Джізус! Ми також не зможемо точно сказати, що наше рішення є оптимальним, ми можемо просто сказати, що ми досить впевнені.

Тоді це не гарне рішення. Ось спрощена картина нашої дорожньої системи:

image:images/roads_simple.png[]
Добре, чи можете ви зрозуміти, який найкоротший шлях до першого перехрестя (перша синя крапка на A, позначена A1) на дорозі A? Це досить банально. Ми просто бачимо, чи коротше йти прямо вперед по А, чи коротше йти вперед по В, а потім повертати. Очевидно, що дешевше їхати вперед через B, а потім повертати: на це потрібно 40 хвилин, тоді як на шлях через A потрібно 50 хвилин. Що з перехрестям B1? Те ж саме. Ми бачимо, що набагато дешевше просто поїхати прямо через B (затрати 10 хвилин), тому що переїзд через A і потім переправлення зайняв би нам цілих 80 хвилин!

Тепер ми знаємо, що найдешевший шлях до А1 (піти через В, а потім перетнути, тому ми скажімо, що це B, C із вартістю 40), і ми знаємо, що найдешевший шлях до B1 (йти безпосередньо по B, так що це просто B, стане в 10). Чи допомагають нам ці знання взагалі, якщо ми хочемо знати найдешевший шлях до наступного перехрестя на обох головних дорогах? Боже, це саме так!

Подивимось, яким був би найкоротший шлях до A2. Щоб дістатися до A2, ми або перейдемо безпосередньо до A2 від A1, або підемо вперед від B1, а потім перетнемось (пам’ятайте, ми можемо рухатись лише вперед або переходити на інший бік). А оскільки ми знаємо вартість A1 та B1, ми можемо легко зрозуміти, який найкращий шлях до A2. Щоб дістатися до А1, коштує 40, а потім - від А1 до А2, тож B, C, A коштує 45. Щоб дістатися до B1, коштує лише 10, але тоді знадобиться додаткові 110 хвилин до В2, а потім перетнути! Очевидно, найдешевший шлях до A2 - це B, C, A. Таким же чином, найдешевший шлях до B2 - це йти вперед від А1, а потім перетнутись.

Можливо, ви запитуєте себе: а як же дістатися до A2, спочатку перейшовши на B1, а потім продовжуючи вперед? Ну, ми вже охопили перетин від B1 до A1, коли шукали найкращий шлях до A1, тому нам не доведеться також враховувати це на наступному кроці.
Тепер, коли у нас найкращий шлях до A2 та B2, ми можемо повторювати це нескінченно, поки не досягнемо кінця. Після того, як ми знайшли найкращі шляхи для A4 та B4, той, що дешевший - оптимальний шлях!

Отже, по суті, для другого розділу ми просто повторюємо крок, який ми зробили спочатку, тільки ми враховуємо, які попередні найкращі шляхи на А і В. Ми могли б сказати, що ми також врахували найкращі шляхи на А і на B на першому кроці, тільки вони обидва були порожніми шляхами вартістю 0.

Ось підсумок. Для того, щоб пройти найменший шлях від Хітроу до Лондона, ми робимо таке: спочатку ми бачимо, який найкращий шлях до наступного перехрестя на головній дорозі А. Два варіанти - йти прямо вперед або почати з протилежної дороги, вперед і потім перетнутись. Ми пам’ятаємо вартість і шлях. Ми використовуємо той самий метод, щоб побачити, який найкращий шлях до наступного перехрестя на головній дорозі B, і пам’ятаємо про це. Тоді ми бачимо, чи шлях до наступного перехрестя на А дешевший, якщо ми переходимо з попереднього перехрестя А або переходимо від попереднього перехрестя В, а потім переходимо. Ми пам’ятаємо про дешевший шлях, а потім робимо те ж саме для перехрестя, навпроти нього. Ми робимо це для кожного розділу, поки не дістанемось кінця. Як тільки ми дійшли до кінця, найдешевший з двох шляхів - це наш оптимальний шлях!

Отже, по суті, ми тримаємо один найкоротший шлях на дорозі А і один найкоротший шлях на дорозі В. І коли ми досягнемо кінця, то коротший з цих двох - наш шлях. Тепер ми знаємо, як вручну визначити найкоротший шлях. Якщо у вас було достатньо часу, паперу та олівців, ви могли б визначити найкоротший шлях через дорожню систему з будь-якою кількістю ділянок.

Next step! How do we represent this road system with Haskell's data types? One way is to think of the starting points and crossroads as nodes of a graph that point to other crossroads. If we imagine that the starting points actually point to each other with a road that has a length of one, we see that every crossroads (or node) points to the node on the other side and also to the next one on its side. Except for the last nodes, they just point to the other side.
Наступний крок! Як ми представляємо цю дорожню систему в типах даних Haskell? Один із способів - мислити вихідні точки та перехрестя як вузли графа, які вказують на інші перехрестя. Якщо ми уявляємо, що вихідні точки насправді вказують один на одного дорогою, яка має довжину одиниці, ми бачимо, що кожне перехрестя (або вузол) вказує на вузол з іншого боку, а також на наступне з його боку. За винятком останніх вузлів, вони просто вказують на іншу сторону.

[source,haskell]
----
data Node = Node Road Road | EndNode Road  
data Road = Road Int Node  
----

Вузол є або звичайним вузлом і має інформацію про дорогу, яка веде до іншої головної дороги, і дорогу, яка веде до наступного вузла, або кінцевий вузол, який містить лише інформацію про дорогу до іншої головної дороги. Дорога зберігає інформацію про те, як довго вона знаходиться і на який вузол вона вказує. Наприклад, першою частиною дороги на головній дорозі A буде `Road 50 a1`, де `a1` буде вузлом `Node x y`, де `x` і `y` - дороги, які вказують на B1 і A2.

Іншим способом було б використання `Maybe` для частин дороги, які спрямовані вперед. Кожен вузол має частину дороги, яка вказує на протилежну дорогу, але лише ті вузли, які не є кінцевими, мають частини, які спрямовані вперед.

[source,haskell]
----
data Node = Node Road (Maybe Road)  
data Road = Road Int Node  
----

Це добрий спосіб представити дорожню систему в Haskell, і ми, звичайно, могли б вирішити цю проблему з нею, але, можливо, ми могли б придумати щось простіше? Якщо ми повертаємося до нашого рішення вручну, ми завжди просто перевіряли довжину трьох дорожніх частин одразу: дорожню частину на дорозі А, її протилежну частину на дорозі Б та частину С, яка торкається цих двох частин і з'єднує їх . Коли ми шукали найкоротший шлях до А1 та В1, нам залишалося лише розібратися з довжинами перших трьох частин, що мають довжини 50, 10 та 30. Ми назвемо цей один розділ. Тож дорожню систему, яку ми використовуємо для цього прикладу, можна легко представити у вигляді чотирьох секцій: (50, 10, 30), (5, 90, 20), (40, 2, 25) та (10, 8, 0).

Завжди добре зберігати наші типи даних максимально простими, хоча не простіше!

[source,haskell]
----
data Section = Section { getA :: Int, getB :: Int, getC :: Int } deriving (Show)  
type RoadSystem = [Section]  
----

Це майже ідеально! Це так просто, як на мене, і я маю відчуття, що це буде ідеально працювати для втілення нашого рішення. `Section` - простий алгебраїчний тип даних, який містить три цілі числа на довжину трьох його дорожніх частин. Ми також вводимо синонім типу, кажучи, що `RoadSystem` - це список розділів.

Ми також могли б використати трійку `(Int, Int, Int)`, щоб представити ділянку дороги. Використання кортежів замість створення власних алгебраїчних типів даних добре для деяких невеликих локалізованих речей, але зазвичай краще  цього створити новий тип. Це дає типовій системі більше інформації про те, що робити. Ми можемо використовувати `(Int, Int, Int)` для відображення ділянки дороги або вектора в тривимірному просторі, і ми можемо оперувати цими двома, але це дозволяє нам їх змішати. Якщо ми використовуємо типи даних `Section` та `Vector`, ми не можемо випадково додати вектор до ділянки дорожньої системи.

Нашу дорожню систему від Хітроу до Лондона тепер можна представити так:

[source,haskell]
----
heathrowToLondon :: RoadSystem  
heathrowToLondon = [Section 50 10 30, Section 5 90 20, Section 40 2 25, Section 10 8 0]  
----

Все, що нам зараз потрібно зробити - це реалізувати рішення, яке ми придумали раніше в Haskell. Яким має бути оголошення типу для функції, яка обчислює найкоротший шлях для будь-якої дорожньої системи? Він повинен приймати дорожню систему як параметр і повертати шлях. Ми також будемо представляти шлях як список. Давайте введемо тип `Label`, який є лише перерахуванням або `A`, `B` або `C`. Ми також зробимо синонім типу: `Path`.

[source,haskell]
----
data Label = A | B | C deriving (Show)  
type Path = [(Label, Int)]  
----

Таким чином, наша функція, яку ми називаємо `optimalPath`, повинна мати типове оголошення `optimalPath ::RoadSystem -> Path`. Якщо викликається з дорожньою системою `heathrowToLondon`, вона повинна повернути такий шлях:

[source,haskell]
----
[(B,10),(C,30),(A,5),(C,20),(B,2),(B,8)]  
----

Нам доведеться переглядати список з розділами зліва направо і дотримуватись оптимального шляху на A та оптимального шляху на B під час руху. Ми накопичуємо найкращий шлях, коли ми будемо проходити по списку, зліва направо. Як це звучить? Дінь, дінь, дінь! Правильно, ЛІВА ЗГОРТКА!

Роблячи рішення вручну, був крок, який ми повторювали знову і знову. Він включав перевірку поточних оптимальних шляхів на A і B, і поточний розділ для створення нових оптимальних шляхів на A і B. Наприклад, на початку оптимальними шляхами були `[]` і `[]` для A і B відповідно. Ми розглянули розділ `Section 50 10 30` і дійшли висновку, що новий оптимальний шлях до А1 є `[(B, 10), (C, 30)]`, а оптимальний шлях до B1 - `[(B, 10)]`. Якщо ви дивитесь на цей крок як на функцію, вона приймає пару шляхів та ділянку, та створює нову пару шляхів. Тип - `(Path, Path) -> Section -> (Path, Path)`. Давайте підемо далі та реалізовуватимемо цю функцію, бо вона обов'язково буде корисною.

Підказка: це буде корисно, оскільки `(Path, Path) -> Section -> (Path, Path)` можна використовувати як бінарну функцію для лівої згортки, яка повинна мати тип `a -> b -> a`

[source,haskell]
----
roadStep :: (Path, Path) -> Section -> (Path, Path)  
roadStep (pathA, pathB) (Section a b c) =   
    let priceA = sum $ map snd pathA  
        priceB = sum $ map snd pathB  
        forwardPriceToA = priceA + a  
        crossPriceToA = priceB + b + c  
        forwardPriceToB = priceB + b  
        crossPriceToB = priceA + a + c  
        newPathToA = if forwardPriceToA <= crossPriceToA  
                        then (A,a):pathA  
                        else (C,c):(B,b):pathB  
        newPathToB = if forwardPriceToB <= crossPriceToB  
                        then (B,b):pathB  
                        else (C,c):(A,a):pathA  
    in  (newPathToA, newPathToB)  
----

image:images/guycar.png[]
Що тут відбувається? Спочатку обчислюємо оптимальну ціну на дорозі A на основі найкращої на даний момент на A і зробимо те саме для B. Ми зробимо `sum $ map snd pathA`, тож якщо `pathA` є чимось на зразок `[(A, 100), (C, 20 )]`, `priceA` стає `120`. `forwardPriceToA` - це ціна, яку ми заплатимо, якби ми перейшли до наступного перехрестя на A, якби ми їхали туди безпосередньо з попереднього перехрестя на A. Це дорівнює найкращій ціні нашої попередньої A плюс довжина поточного розділу. `crossPriceToA` - це ціна, яку ми заплатимо, якби ми перейшли до наступного A, рухаючись вперед від попереднього B, а потім перетинаючи. Це найкраща ціна попереднього B досі плюс довжина B секції плюс C довжина секції. Ми визначаємо `forwardPriceToB` та `crossPriceToB` подібним чином.

Тепер, коли ми знаємо, що найкращий шлях до A і B, нам просто потрібно зробити нові шляхи до A і B на основі цього. Якщо дешевше перейти до A, просто йдучи вперед, ми встановимо `newPathToA`, щоб бути `(A, a): pathA`. В основному ми ставимо мітку `Label A` і довжину відрізка `a` до оптимального шляху шляху A на даний момент. В основному, ми говоримо, що найкращий шлях до наступного перехрестя A - це шлях до попереднього перехрестя A, а потім один відрізок вперед по A. Пам'ятайте, A - це лише мітка, тоді як `a` має тип `Int`. Чому ми ставимо наперед замість того, щоб робити `pathA ++ [(A, a)]`? Добре, додавання елемента до початку списку (також відомого як _консинг_) набагато швидше, ніж додавання його в кінець. Це означає, що шлях буде невірним, коли ми складемо список із цією функцією, але просто обернути список пізніше. Якщо дешевше дістатися до наступного перехрестя А, рухаючись вперед від дороги В і потім перетинаючи, то `newPathToA` - це стара дорога до В, яка потім йде вперед і переходить на A. Ми робимо те ж саме для `newPathToB`, тільки все дзеркально відображено.

Нарешті, ми повертаємо `newPathToA` та `newPathToB` в парі.

Давайте запустимо цю функцію на першому розділі `heathrowToLondon`. Оскільки це перший розділ, найкращими шляхами для параметра A і B буде пара порожніх списків.

[source,haskell]
----
ghci> roadStep ([], []) (head heathrowToLondon)  
([(C,30),(B,10)],[(B,10)])  
----

Пам’ятайте, шляхи зворотні, тому читайте їх справа наліво. З цього ми можемо прочитати, що найкращий шлях до наступного A - це починати на B, а потім переходити на A і що найкращий шлях до наступного B - це просто йти прямо вперед від початкової точки на B.

Порада з оптимізації: коли ми робимо `priceA = sum $ map snd pathA`, ми обчислюємо ціну з шляху на кожному кроці. Нам би цього не довелося робити, якби ми реалізували `roadStep` як функцію `(Path, Path, Int, Int) -> Section -> (Path, Path, Int, Int)`, де цілі числа представляють найкращу ціну на A і B.
Тепер, коли у нас є функція, яка приймає пару шляхів і розділ і створює новий оптимальний шлях, ми можемо просто і легко виконати ліву згортку над списком розділів. `roadStep` викликається з `([], [])` і першим розділом і повертає пару оптимальних шляхів до цього розділу. Потім він визивається з цією парою шляхів і наступним розділом тощо. Коли ми пройшли всі розділи, нам залишається пара оптимальних шляхів, і коротший з них - наша відповідь. Маючи це на увазі, ми можемо реалізувати `optimalPath`.

[source,haskell]
----
optimalPath :: RoadSystem -> Path  
optimalPath roadSystem = 
    let (bestAPath, bestBPath) = foldl roadStep ([],[]) roadSystem  
    in  if sum (map snd bestAPath) <= sum (map snd bestBPath)  
            then reverse bestAPath  
            else reverse bestBPath  
----

We left fold over roadSystem (remember, it's a list of sections) with the starting accumulator being a pair of empty paths. The result of that fold is a pair of paths, so we pattern match on the pair to get the paths themselves. Then, we check which one of these was cheaper and return it. Before returning it, we also reverse it, because the optimal paths so far were reversed due to us choosing consing over appending.
Ми виконали згортку по `roadSystem` (пам'ятайте, це список розділів), при цьому початковим акумулятором є пара порожніх шляхів. Результатом цієї згортки є пара шляхів, тому ми робимо співпадіння шаблонів для отримання самих шляхів. Потім ми перевіряємо, який з них був дешевшим, і повертаємо його. Перш ніж повернути ми також обертаємо його, тому що оптимальні шляхи до цього часу були обернені через те, що ми обирали `cons`-інг над додаванням.

Давайте перевіримо це!

[source,haskell]
----
ghci> optimalPath heathrowToLondon  
[(B,10),(C,30),(A,5),(C,20),(B,2),(B,8),(C,0)]  
----

Це результат, який ми мали отримати! Дивовижно! Він трохи відрізняється від нашого очікуваного результату тим, що в кінці є крок `(C, 0)`, що означає, що ми переходимо на іншу дорогу, коли ми перебуваємо в Лондоні, але тому що цей переїзд нічого не коштує, це все-таки правильний результат.

У нас є функція, яка знаходить оптимальний шлях на основі, тепер нам просто потрібно прочитати текстове подання дорожньої системи зі стандартного вводу, перетворити її у тип `RoadSystem`, виконати її через нашу функцію `optimalPath` і надрукувати шлях.

Спочатку давайте зробимо функцію, яка бере список і розбиває його на групи однакового розміру. Ми назовемо її `groupsOf`. Для параметра `[1..10]` `groupsOf 3` має повернути `[[1,2,3], [4,5,6], [7,8,9], [10]]`.

[source,haskell]
----
groupsOf :: Int -> [a] -> [[a]]  
groupsOf 0 _ = undefined  
groupsOf _ [] = []  
groupsOf n xs = take n xs : groupsOf n (drop n xs)  
----

Стандартна рекурсивна функція. Для `xs [1..10]` та `n 3` це дорівнює `[1,2,3] : groupsOf 3 [4,5,6,7,8,9,10]`. Коли рекурсія виконана, ми отримуємо наш список у три групи. Ось наша основна функція, яка читає зі стандартного вводу, робить `RoadSystem` з нього і виводить найкоротший шлях:

[source,haskell]
----
import Data.List  
  
main = do  
    contents <- getContents  
    let threes = groupsOf 3 (map read $ lines contents)  
        roadSystem = map (\[a,b,c] -> Section a b c) threes  
        path = optimalPath roadSystem  
        pathString = concat $ map (show . fst) path  
        pathPrice = sum $ map snd path  
    putStrLn $ "The best path to take is: " ++ pathString  
    putStrLn $ "The price is: " ++ show pathPrice  
----

Спочатку ми отримуємо весь вміст зі стандартного вводу. Потім ми визиваємо `lines` з нашим вмістом, щоб перетворити щось на кшталт `"50\n10\n30\n` ... в `["50","10","30"..`, а потім ми перечитуємо прочитане, щоб перетворити його в список чисел. Ми визиваємо `groupsOf 3` на ньому, щоб перетворити його на список списків довжиною 3. Ми відображаємо лямбда `(\[a, b, c] -> Section a b c)` над цим списком списків. Як ви можете бачити, лямбда просто бере список довжиною 3 і перетворює його на розділ. Отже, `roadSystem` є нашою системою доріг і навіть має правильний тип, а саме `RoadSystem` (або `[Section]`). Ми визиваємо `OptimPath` з ним і потім отримуємо шлях і ціну в приємному текстовому вигляді та друкує його.

Ми зберігаємо наступний текст

----
50  
10  
30  
5  
90  
20  
40  
2  
25  
10  
8  
0  
----

у файлі під назвою _paths.txt_, а потім подаємо його до нашої програми.

----
$ cat paths.txt | runhaskell heathrow.hs  
The best path to take is: BCACBBC  
The price is: 75  
----

Працює чудово! Ви можете використовувати свої знання модуля `Data.Random`, щоб створити набагато довшу систему доріг, яку ви зможете подати до того, що ми написали. Якщо у вас є переповнення стека, спробуйте використовувати `foldl'` замість `foldl`, оскільки `foldl'` є суворим.