include::header.adoc[]

== Ввод та вивод

image:images/dognap.png[]
Ми вже казали, що Haskell є чисто функціональною мовою. В той час, коли в імперативних мовах ви зазвичай отримуєте речі надаючи копм'ютеру серії кроків для виконання, функціональне програмування більше щодо визначення того, чим є речі. В Haskell функція не може змінити деякий стан, як змінити вміст змінної (коли функція змінює стан ми кажемо, що функція має побічні ефекти). Єдина річ, яку може робити функція в Haskell, це дати нам деякий результат на основі параметрів, які ми надаємо їй. Якщо функція викликається два рази з тими самими параметрами, вона має повертати той самий результат. Хоча це може виглядати дещо обмежувальним, якщо ви прийшли з імперативного світу, ми бачили, що насправді це круто. В імперативній мові ви не маєте гарантії, що проста функція, що має лише пожонглювати деякими числами, не спалить вашу хату, не викраде вашого пса і не пошкрябає вашу машину помідором, при маніпуляції ціма числами. Наприклад, коли ви шукаєте в двійниковому дереві, ми не вставляли елемент в дерево, модифікуючи дерево на місці. Наша функція для вставки в двійникове дерево насправді повертає нове дерево, бо воно не може змінити старе дерево.

Хоча функції, що не можуть змінювати стан, є гарним, бо дозволяє нам промислювати наші програми, з цім є одна проблема. Якщо функція не може зробити дещо в світі, як вона зможе повідомити нам, що вона нарахувала? Щоб сказати нам, що обчислено, вона має змінити стан вихідного пристрою (звичайно стан екрану), що потім виділяє фотони, які подорожують до нашого мозку, та змінюють стан наших думок, чоловіче.

Не втрачайте надію, не все втрачено. З'ясовується, що насправді Haskell має дійсно розумну систему для справ з функціями, що мають побічні ефекти, яка мило відокремлює частину програми, що є чистою, та частину що є нечистою і робить всю брудну роботу, як розмови з клавіатурою та екраном. Маючи ці дві частини розділеними ми все ще можемо промислювати нашу чисту програму і отримувати переваги від всіх речей, що надає чистота, як лінивість, надійність та модульність, при цьому ефективно комунікуючи з зовнішним світом.

=== Hello, world!

image:images/helloworld.png[]
До цього часу ми завжди завантажували наші функції в GHCI для їх тестування та забавок з ними. Ми також дослідили функції стандартної бібліотеки, проміж справ. Але тепер, після восьми або десь так глав, ми нарешті збираємось написати нашу першу справжню програму Haskell! Вау! І будьте впевнені, що ми збираємось зробити стару добру штуку "hello, world".

Хей! Для цілей цієї глави я буду вважати, що ми використовуємо  unix-подібне середовище для вивчення Haskell. Якщо ви в Windows, я б порадив завантажити Cygwin, що є Linux-подібне оточення для Windows, тобто те що нам треба.

І так, для початку, забийте наступне в вашому улюбленому текстовому редакторі:

[source,scala]
----
main = putStrLn "hello, world"  
----

Ми тільки що визначили ім'я `main`, і в ньому ми викликаємо функцію з назвою `putStrLn` з параметром `"hello, world"`. Виглядає як робота млина, але ні, ми побачимо це через мить. Збережіть це в файлі `helloworld.hs`.

Але зараз ми збираємось зробити дещо, чого не робили досі. Насправді ми збираємось скомпілювати нашу програму! Я в захопленні! Відкрийте ваш термінал і перейдіть в каталог, де розміщений `helloworld.hs`, та виконайте таке:

[source,bash]
----
$ ghc --make helloworld  
[1 of 1] Compiling Main             ( helloworld.hs, helloworld.o )  
Linking helloworld ...  
----

Okay! With any luck, you got something like this and now you can run your program by doing ./helloworld.

[source,bash]
----
$ ./helloworld  
hello, world  
----

І ось де ми є, наша перша скомпільована програма, що друкує дещо на терміналі. Неймовірно нудно!

Давайте перевіримо, що ми написали. Спочатку давайте подивимось на тип функції `putStrLn`.

[source,scala]
----
ghci> :t putStrLn  
putStrLn :: String -> IO ()  
ghci> :t putStrLn "hello, world"  
putStrLn "hello, world" :: IO ()  
----

Ми можемо прочитати тип `putStrLn` таким чином: `putStrLn` бере рядок та повертає I/O дію, що має тип результата `()` (тобто порожній тапл, також відомий як юніт). I/O дія є дещо, що коли виконується буде нести дію з побічним ефектом (це зазвичай або читання з входу або друкування дечого на екрані), і буде також містити деякий різновид значення повернення в собі. Друкування рядка на термінал насправді не має жодного осмисленого типу повернення, так що використовується фіктивне значення `()`.

Порожній типл є значенням `()`, і він має також тип `()`.

То коли буде виконана I/O дія? Гаразд, ось де з'являється `main`. I/O дія буде виконана коли ми дамо їй ім'я `main`, і потім виконаємо програму.

Маючи всю вашу програму тільки в вигляді однієї I/O дії виглядає дещо обмежувальним. Ось чому ми можемо використовувати синтаксис `do` для зклеювання разом декількох I/O дій в одну. Погляньте на наступний приклад:

[source,scala]
----
main = do  
    putStrLn "Hello, what's your name?"  
    name <- getLine  
    putStrLn ("Hey " ++ name ++ ", you rock!")  
----

А, цікаво, новий синтаксис! І це читається майже так само як імперативна програма. Якщо ви скомпілюєте це і спробуєте в дії, це буде поводитись так само, як ми цього очікуємо. Зауважте, що ми тільки но сказали `do` і потім поклали серію кроків, як би ми зробили в імперативній програмі. Кожний з ціх кроків є I/O дією. Збираючи їх докупи за допомогою синтаксису `do`, ми зліпили їх в одну I/O дію. Дія, яку ми отримали, має тип `IO ()`, оскільки це тип останньої I/O дії всередині.

Через це `main` завжди має сигнатуру типу `main ::IO something`, де `something` є деякий конкретний тип. За домовленістю ми звичайно не вказуємо декларацію типу для `main`.

Цікава річ, з якою ми досі не зустрічались, це третій рядок, що вказує `name <- getLine`. Виглядає що воно читає рядок з входу, та зберігає його в змінній на ім'я `name`. Чи так насправді? Гаразд, давайте перевіримо тип `getLine`.

[source,scala]
----
ghci> :t getLine  
getLine :: IO String  
----

image:images/luggage.png[]
Так, це є. `getLine` є I/O дією, що містить тип результату `String`. Це має сенс, оскільки це буде очікувати користувача, докі він введе дещо в терміналі, і потім це дещо буде представлене як рядок. То що відбувається в `name <- getLine`? Ви можете читати цей шматок коду таким чином: виконати I/O дію `getLine`, та потім прив'язати значення результату до `name`. `getLine` має тип `IO String`, так що `name` буде мати тип `String`. Ви можете думати про I/O дію як бокс на маленьких ніжках, що виходитиме в реальний світ і робитиме там дещо (як малювати графіті на стінах), або приносити назад деякі дані. Коли ці дані були підтягрутя до вас, єдиним способом відкрити бокс і отримати дані всередині є використання конструкції `<-`. І коли ми беремо дані з I/O дії, ми можемо дістати їх лише коли ми всередині іншої I/O. Ось як діє Haskell для гарного поділу кода на чисту і нечисту частину. `getLine` в деякому сенсі є нечистим, бо його значення результату не буде гарантовано тим самим, якщо його виконати двічі. Ось чому він дещо заплямований конструктором типу IO, і ми можемо отримати ці дані лише від I/O коду. І оскільки I/O код також заплямований, любі обчислення, що залежать від заплямованих I/O даних, будуть мати заплямований результат.

Коли я кажу заплямований, я не маю на увазі це в такий спосіб, що ми ніколи не зможемо використовувати результат в I/O дії, будь-коли в чистому коді. Ні, ми тимчасово роз-плямовуємо дані в I/O дії, коли ми прикріплюємо її до `name`. Коли ми робимо `name <- getLine`, `name` є просто звичайний рядок, оскільки він представляє те, що знаходиться в боксі. Ми можемо мати дійсно складну функцію, що скажімо бере ваше ім'я (звичайний рядок) як параметр, і каже вам всю вашу вдачу і майбутнє всього вашого життя, базуючись на вашому імені. Ми можемо зробити це:

[source,scala]
----
main = do  
    putStrLn "Hello, what's your name?"  
    name <- getLine  
    putStrLn $ "Read this carefully, because this is your future: " ++ tellFortune name  
----

і `tellFortune` (або жодна функція, куди вона передає `name`) не має знати нічого про I/O, це просто нормальна функція `String -> String`!

Подивіться на цей шматок коду, чи він валідний?

[source,scala]
----
nameTag = "Hello, my name is " ++ getLine  
----

Якщо ви відповіли ні - пійдіть з'їжте періжок. Якщо ви сказали так - випийте ковшик розпеченої лави. Я пошуткував, не робіть цього! Причина, з якої це не робить, в тому, що `++` потребує обоє свої параметри як спискаи того самого типу. Лівий параметр має тип `String` (або `[Char]` якщо бажаєте), тоді як `getLine` має тип `IO String`. Ви не можете приєднати рядок та I/O дію. Спочатку ми маємо отримати результат від I/O дії, щоб отримати тип `String`, і це єдиний спосіб зробити це в тому, щоб сказати щось як `name <- getLine` всередині іншої I/O дії. Якщо ми бажаємо мати справу з нечистими даними, ми маємо робити це в нечистому оточенні. Так що прокляття нечистості розповсюджується навкруги як бич нечесті, так що в наших кращих інтересах утримувати I/O частини коду такими малими, як це можливо.

Кожна I/O дія, що виконується, має заточений в собі результат. Ось чому наш попередній приклад програми може бути записаний таким чином:

[source,scala]
----
main = do  
    foo <- putStrLn "Hello, what's your name?"  
    name <- getLine  
    putStrLn ("Hey " ++ name ++ ", you rock!")  
----

Однак `foo` буде мати лише значення `()`, так що робити таке буде лише  різновидом балачок. Зауважте, що ми не прив'язали останній `putStrLn` до будь-чого. Це через те, що в блоці `do` остання дія не може бути прив'язана до імені, як це було з першими двома. Ми побачимо чому це так трохи пізніше, коли ми поринемо в світ монад. Зараз ви можете думати про це в спосіб, що блок `do` автоматично виділяє значення з останньої дії, і прикріпляє його до свого власного результату.

За винятком цього останнього рядка, кожний рядок, що не прикріплений, може бути записаний як прикріплення. Так що `putStrLn "BLAH"` може бути записане як `_ <- putStrLn "BLAH"`. Але це не має користі, так що ми не будемо використовувати `<-` для I/O дій, що не мають корисних результатів, як `putStrLn something`.

Початківці іноді думають, що роблячи

[source,scala]
----
name = getLine  
----

буде читати з входу, та потім прикріпляти значення до `name`. Гаразд, так не буде, все що тут робиться це надання дії `getLine` іншого імені, а саме `name`. Пам'ятайте, щоб отримати значення від I/O дії, ми маємо виконати її в середині іншої I/O дії, через прикріплення її до імені за допомогою `<-`.

I/O дії будуть виконані лише тоді, якщо їм надано ім'я `main`, або коли вони всередині більшої I/O дії, що компонується за допомогою блоку `do`. Ми також можемо використати блок `do` для склеювання разом декількох I/O дій, і потім ми можемо використовувати цю I/O дію в іншому `do` блоці, і так далі. З іншого боку, всі вони будуть виконані тільки якщо вони з часом дійдуть до `main`.

О, гаразд, існує ще один випадок, коли будуть виконані I/O дії. Коли ми набираємо I/O дію в GHCI та натискаємо `return`, це також буде виконане.

[source,scala]
----
ghci> putStrLn "HEEY"  
HEEY  
----

Хоч коли ми вводимо число або викликаємо функцію в GHCI і натискаємо `return`, це буде обчислене (здебільшого як це нам і треба), і потім викликає `show` на цьому, і потім це друкує рядок на терміналі, неявно викликаючи `putStrLn`.

Пам'ятаєте прикріплення `let`? Якщо ні, оновіть вашу пам'ять щодо них в цьому розділі. Вони мають бути в формі `let` прикріплень в виразах, де прикріалення є іменами що будуть надані виразам, та вираз є вираз, що буде обчислений коли хтось бачить його. Ми також бачили його в осяжностях списків, де частина `in` непотрібна. Гаразд, ви можете використовувати їх в `do` блоках, майже так само, як ми використовуємо їх в осяжностях списків. Перевірте це:

[source,scala]
----
import Data.Char  
  
main = do  
    putStrLn "What's your first name?"  
    firstName <- getLine  
    putStrLn "What's your last name?"  
    lastName <- getLine  
    let bigFirstName = map toUpper firstName  
        bigLastName = map toUpper lastName  
    putStrLn $ "hey " ++ bigFirstName ++ " " ++ bigLastName ++ ", how are you?"  
----

Бачите, як підрівняні I/O дії в блоці `do`? Також зауважте, як `let` вирівняне разом з I/O діями і іменами `let`, вирівняними один з одним? Це гарна практика, бо відступи важливі в Haskell. Тепер: ми зробили `toUpper firstName`, що перетворює якесь `"John"` в значно крутіший рядок як `"JOHN"`. Ми прикріплюємо цей збільшений рядок до `name`, та потім використовуємо його в рядку нижче, коли друкуємо на термінал.

Ви можете поцікавитись, коли використавувати `<-`, та коли використовувати прикріплення `let`? Гаразд, пам'ятайте, що `<-` є (поки що) для виконання I/O дій та прикріплення їх результатів до імен. Однак `map toUpper firstName` не є I/O дією. Це чистий вираз в Haskell. Так що використовуйте `<-`, коли ви бажаєте прикріпити результати I/O дій до імен, і ви можете використовувати `let` для прикріалень чистих виразів до імен. Так що коли ми робимо дещо як `let firstName = getLine`, ми отримаємо лише нове ім'я для I/O дії `getLine`, і ми все ще маємо запустити її через `<-`, щоб вона виконалась.

Тепер ми збираємось зробити програму, що постійно читає рядок і друкує той самий рядок зі словами в зворотньому порядку. Виконання програми буде припинено, коли ми введемо порожній рядок. Ось програма:

[source,scala]
----
main = do   
    line <- getLine  
    if null line  
        then return ()  
        else do  
            putStrLn $ reverseWords line  
            main  
  
reverseWords :: String -> String  
reverseWords = unwords . map reverse . words  
----

Щоб отримати відчуття того, що вона робить, ви можете виконати її перед тим, як ми пройдемось по коду.

Protip: Щоб виконати програму ви можете або скомпілювати її, та потім виконати отримий виконавчий файл, роблячи `ghc --make helloworld` та потім `./helloworld`, або ви можете використовувати команду `runhaskell` ось так: `runhaskell helloworld.hs` і ваша програма буде виконана на льоту.

Спочатку давайте поглянемо на функцію `reverseWords`. Це тільки звичайна функція, що приймає рядок як `"hey there man"`, та потім викликає `words` для продукування списку слів як `["hey","there","man"]`. Потім ми `map reverse` на списку, отримуючи `["yeh","ereht","nam"]`, і потім ми знову покладаємо все це в один рядок, використовуючи `unwords`, і фінальний результат буде `"yeh ereht nam"`. Дивітья, як ми використовували композицію функцій. Без композиції функцій ми б мали написати щось як `reverseWords st = unwords (map reverse (words st))`.

Що відносно `main`? Спершу ми отримуємо рядок з термінала, виконуючи виклик `getLine` що повертається в `line`. І тепер ми маємо условний вираз. Пам'ятайте, що в Haskell кожний `if` повинен мати відповідний `else`, оскільки кожний вираз повинен мати деякий різновид значення. Ми робимо `if`, так що коли умова `true` (в нашому випадку введений нами рядок порожній), ми виконуємо одну I/O дію, та коли ні - буде виконана інша I/O дія для `else`. Ось чому в блоці `I/O do`, `if` повинні мати форму `if умова then I/O дія else I/O дія`.

Давайте спочатку поглянемо на те, що відбувається в частині `else`. Оскільки ми повинні мати саме одну I/O дію після `else`, ми використовуємо блок `do` для склеювання разом двох I/O дій в одну. Ви також можете записати цю частину таким чином:

[source,scala]
----
else (do  
    putStrLn $ reverseWords line  
    main)  
----

Це робить більш наочним, що блок `do` має розгядатись як одна I/O дія, але це огидніше. В любому разі в блоці `do` ми викликаємо  `reverseWords` на рядку, що ми отрумуємо від `getLine`, та потім друкуємо це на терміналі. Після цього ми просто виконуємо `main`. Вона виключається рекурсивно і це гаразд, оскільки `main` сама є I/O дією. Так що в деякому сенсі ми повертаємось на початок програми.

Тепер, що трапляється коли `null line` стає `true`? Це те, що в данному випадку виконується після `then`. Якщо ми подивимось, ми побачимо що виконується `then return ()`. Якщо ви візьмете імперативні мови, як C, Java або Python, ви можливо думаєте, що знаєте що робить цей `return`, так що є шанси, що ви перестрибнули цей доволі довгий параграф. Гаразд, ось як вам таке: `return` в Haskell насправді нічого подібного до `return` в більшості мов! Воно має те саме ім'я, що бентежить багато людей, але в реальності це досить інше. В імперативних мовах `return` звичайно закінчує виконання метода або процедури, і повідомляє про це деяким разновидом значення до викликаючої сторони. В Haskell (точніше в I/O діях), це виробляє I/O дію з чистого значення. Якщо ви подумаєте про аналогію бокса з попередніх розділів, це приймає значення і огортає його в бокс. Отримана I/O дія насправді нічого не робить, це просто це значення, оточене як його результат. Так що в контексті I/O `return "haha"` буде мати тип `IO String`. Який сенс в простому перетворенні чистого значення в I/O дію, що нічого не робить? Навіщо заплямовувати нашу програму в IO більше, ніж це мало би бути? Гаразд, нам треба деяка I/O дія, що треба видати в випадку порожнього вхідного рядка. Ось чому ми просто створили фіктивну I/O дію, дію що нічого не робить, написавши `return ()`.

Використання `return` не спричиняє закінчення блоку `I/O do` в плані виконання, або будь чого подібного. Наприклад, ця програма буде досить щасливо продовжуватись весь час до останнього рядка:

[source,scala]
----
main = do  
    return ()  
    return "HAHAHA"  
    line <- getLine  
    return "BLAH BLAH BLAH"  
    return 4  
    putStrLn line  
----

Всі ці `return` роблять те, що вони створюють I/O дії, що насправді нічого не роблять, крім інкапсуляції результату, і цей результат відкидається геть, оскільки він не прив'язаний до імені. Ми можемо використовувати `return` в комбінації з `<-` для прив'язки речей до імен.

[source,scala]
----
main = do  
    a <- return "hell"  
    b <- return "yeah!"  
    putStrLn $ a ++ " " ++ b  
----

Як ви бачите, `return` є різновидом протилежності до `<-`. Тоді як  `return` бере значення і огортає його в бокс, `<-` бере бокс (та виконує його), та отримує з нього значення, прикріплюючи його до імені. Але робити це є дещо надлишковим, особливо коли ви можете використовувати `let` прикріпленнях в `do` блоках до імен, десь так:

[source,scala]
----
main = do  
    let a = "hell"  
        b = "yeah"  
    putStrLn $ a ++ " " ++ b  
----

Коли маємо справу з блоками `I/O do`, ми в основному використовуємо `return` оба тому що нам треба створити I/O дію, що не робить нічого, або якщо ми не бажаємо, щоб I/O дія, створена з `do` блока, мала значення результата в своїй останній дії, але ми бажаємо, щоб він мав інше значення результата, так що ми використовуємо `return` для створення I/O дії, що завжди має заточеним наш бажаний результат, і ми покладаємо його в кінці.

Блок `do` також може мати лише одну I/O дію. В цьому випадку це те саме, що просто записати I/O дію. Деякі люди можуть обрати запис `then do return ()` в цьому випадку, оскільки `else` також має `do`.

Оскільки ми просуваємось до файлів, давайте поглянемо на деякі функції, що будуть корисні при справах з I/O.

`putStr` здебільшого як `putStrLn` в тому, що він бере рядок як параметр і повертає I/O дію, що буде друкувати цей рядок на терміналі, тільки `putStr` не перестрибує на новий рядок після друкування рядка, тоді як `putStrLn` робить це.

[source,scala]
----
main = do   putStr "Hey, "  
            putStr "I'm "  
            putStrLn "Andy!"   
$ runhaskell putstr_test.hs  
Hey, I'm Andy!  
----

Її сигнатура типу є `putStr :: String -> IO () `, так що результат заточений в результуючій I/O дії є юніт. Дурне значення, так що немає сенсу прикріплювати його.

`putChar` бере символ та повертає I/O дію, що буде роздруковувати його на термінал.

[source,scala]
----
main = do   putChar 't'  
            putChar 'e'  
            putChar 'h'  
$ runhaskell putchar_test.hs  
teh  
----

`putStr` насправді визначена рекурсивно за допомогою `putChar`. Гранична умова `putStr` є порожній рядок, так що якщо ми друкуємо порожній рядок, просто повертаємо I/O дію, що нічого не робить, використовуючи `return ()`. Якщо він не порожній, тоді друкуємо перший символ рядка, роблячи `putChar`, і потім друкуємо всі інші з використанням `putStr`.

[source,scala]
----
putStr :: String -> IO ()  
putStr [] = return ()  
putStr (x:xs) = do  
    putChar x  
    putStr xs  
----

Дивіться, як ми можемо використовувати рекурсію в I/O, так само як ми можемо робити це в чистому коді. Так само як в чистому коді, ми визначаємо граничний випадок, і потім думаємо, чим насправді є результат. Це дія, що спочатку виводить перший символ, і потім виводить залишок рядка.

`print` бере значення любого типу, що є примірником `Show` (що означає, що ми знаємо як представляти його як рядок), викликає `show` з цім значенням для перетворення на рядок, і потім виводить цей рядок на термінал. В основному це просто `putStrLn . show`. Це спочатку виконує `show` на значенні, і потім подає його до `putStrLn`, що повертає I/O дію, що буде роздруковувати наше значення.

[source,scala]
----
main = do   print True  
            print 2  
            print "haha"  
            print 3.2  
            print [3,4,3]  
$ runhaskell print_test.hs  
True  
2  
"haha"  
3.2  
[3,4,3]  
----

Як ви можете бачити, це дуже зручна функція. Згадайте, як ми казали про те, як I/O дії виконуються тільки коли вони підпадають до `main`, або коли ми намагаємось обчислити їх в підказці GHCI. Коли ми друкуємо значення (як `3` або `[1,2,3]`), та натискаємо клавішу `return`, GHCI насправді використовує `print` на цьому значенні для відображення його на терміналі!

[source,scala]
----
ghci> 3  
3  
ghci> print 3  
3  
ghci> map (++"!") ["hey","ho","woo"]  
["hey!","ho!","woo!"]  
ghci> print (map (++"!") ["hey","ho","woo"])  
["hey!","ho!","woo!"]  
----

Коли ми бажаємо роздрукувати рядки, зазвичай ми використовуємо `putStrLn`, оскільки ми не бажаємо лапки навкруги, але для друку наших значень інших типів на термінал здебільшого використовується `print`.

`getChar` є I/O дією, що читає символ зі входу. Таким чином її сигнатура типу є `getChar ::IO Char`, оскільки результат, що міститься в I/O дії є `Char`. Зауважте, що через буферізацію читання символів насправді не буде відбуватись, доки користувач не начавить на клавішу вводу.

[source,scala]
----
main = do     
    c <- getChar  
    if c /= ' '  
        then do  
            putChar c  
            main  
        else return ()  
----

Ця програма виглядає так, що вона повинна читати символ, та перевіряти чи це проміжок. Якщо це так, виконання припиняється, та якщо ні - друкуємо його на терміналі, і потім робимо те саме спочатку і так далі. Гаразд, це робить щось подібне, тільки не в спосіб як ви можете це очікувати. Перевірте це:

[source,scala]
----
$ runhaskell getchar_test.hs  
hello sir  
hello  
----

Другий рядок є ввод. Ми вводимо `hello sir` і потім натискаємо ввод. Через буферізацію виконання програми буде починатись тільки після того, як ми натиснемо `return`, і не після кожного введеного символу. Але коли ми натиснемо ввод, це діє на тому, що було введено до цього. Спробуйте погратись з цією програмою щоб отримати відчуття як вона робить!

Функцію `when` можна знайти в `Control.Monad` (щоб отримати до неї доступ зробіть `import Control.Monad`). Це цікаво, бо в блоці `do` це виглядає як твердження керування виконанням, але насправді це звичайна функція. Вона приймає логічне значення та I/O дію. Якщо це логічне значення є `True`, це повертає ту саму I/O дію, що ми надали туди. Однак якщо це `False`, вона повертає `return ()`, дію, так що  I/O дія нічого не робить. Ось як ми можемо переписати попередній шмат коду, де ми демонструємо `getChar` через використання `when`:

[source,scala]
----
import Control.Monad   
  
main = do  
    c <- getChar  
    when (c /= ' ') $ do  
        putChar c  
        main  
----

Так що, як ви можете бачити, це корисно для інкапсуляції шаблона `if something then do some I/O action else return ()`.

`sequence` бере список I/O дій і повертає I/O дії, що будуть виконувати ці дії, одна за одною. Результат, що міститься в цій I/O дії. буде списком результатів всіх I/O дій, що виконуються. Його сигнатура типу є `sequence ::[IO a] -> IO [a]`. Роблячи це:

[source,scala]
----
main = do  
    a <- getLine  
    b <- getLine  
    c <- getLine  
    print [a,b,c]  
----

є те саме, що і наступне:

[source,scala]
----
main = do  
    rs <- sequence [getLine, getLine, getLine]  
    print rs  
----

Так що `sequence [getLine, getLine, getLine]` створює I/O дію, що буде виконувати `getLine` три рази. Якщо ви прикріпите цю дію до імені, результатом є список з усіх результатів, так що в нашому випадку це список з трьох речей, які користувач вводить на підказку консолі.

Загальний шаблон з `sequence` це коли ми відображуємо функції як  `print` або `putStrLn` на списки. Виконання `map print [1,2,3,4]` не створить I/O дію. Це створить сипсок I/O дій, оскільки це як написати `[print 1, print 2, print 3, print 4]`. Якщо ми бажаємо трансформувати цей список I/O дій в одну I/O дію, ми маємо створити послідовність.

[source,scala]
----
ghci> sequence (map print [1,2,3,4,5])  
1  
2  
3  
4  
5  
[(),(),(),(),()]  
----

Що це за `[(),(),(),(),()]` в самому кінці? Гаразд, коли ми обчислюємо I/O дію в GHCI, вона виконується і потім друкується його результат, якщо результат не `()`, в якому випадку нічого не друкується. Ось чому обчислення `putStrLn "hehe"` в GHCI просто друкує `hehe` (оскільки результат що міститься в результаті `putStrLn "hehe"` в `()`). Але коли ми робимо `getLine` в GHCI, результат цієї I/O дії друкується, оскільки `getLine` має тип `IO String`.

Оскільки відображення функції, що повертає I/O дію до списку, та потім створення послідовності по цьому, є таким загальним, що були введені функції `mapM` та `mapM_`. `mapM` бере функцію та список, відображує функцію на списку, та потім створює послідовність. `mapM_` робить те саме, тільки пізніше він закидає результат. Ми звичайно використовуємо `mapM_`, коли нам байдуже, який результат має наша послідовність I/O дій.

[source,scala]
----
ghci> mapM print [1,2,3]  
1  
2  
3  
[(),(),()]  
ghci> mapM_ print [1,2,3]  
1  
2  
3  
----

`forever` бере I/O дію і повертає I/O дію, що просто повторює отриману I/O дію постійно. Вона розташована в `Control.Monad`. Ця невелика програма буде безкінечно запитувати користувача щодо деякого вводу, і віддавати його назад, ВЕЛИКИМИ ЛІТЕРАМИ:

[source,scala]
----
import Control.Monad  
import Data.Char  
  
main = forever $ do  
    putStr "Give me some input: "  
    l <- getLine  
    putStrLn $ map toUpper l  
----

`forM` (розташована в `Control.Monad`) як `mapM`, тільки з переміненими параметрами. Перший параметр є списком, та другий є функція, по якій треба відображувати цей список, що потім перетворюється на послідовність. Чому це корисне? Гаразд, з деяким креативним застосуванням лямбди та `do` нотації ми можемо робити такі речі:

[source,scala]
----
import Control.Monad  
  
main = do   
    colors <- forM [1,2,3,4] (\a -> do  
        putStrLn $ "Which color do you associate with the number " ++ show a ++ "?"  
        color <- getLine  
        return color)  
    putStrLn "The colors that you associate with 1, 2, 3 and 4 are: "  
    mapM putStrLn colors  
----

`(\a -> do ... )` є функцією, що бере число і повертає I/O дію. Ми маємо оточити її в дужки, інакше лямбда подумає, що останні дві I/O дії належать до неї. Зауважте, що ми робимо `return color` всередині `do` блока. Ми робимо так, щоб I/O дія, яку визначає `do` блок, мала б результат кольору, що міститься в ній. Ми насправді не маємо робити це, оскільки `getLine` вже має те, що міститься в ній. Роблячи `color <- getLine` та потім `return color` є проста розпаковка результату від `getLine`, та потім знову його розпаковка, так що це як просто зробити `getLine`. `forM` (викликана з її двома параметрами) продукує I/O дію, результат якої ми прикріплюємо до `colors`. `colors` є лише звичайний список, що містить рядки. В кінці ми друкуємо всі ці кольори, роблячи `mapM putStrLn colors`.

Ви можете думати про `forM` в такому сенсі: створити I/O для кожного елементу в цьому списку. Що кожна I/O дія буде робити залежить від елемента, що була використана для створення дії. Нарешті виконаємо ці дії та прикріпимо результат до чогось. Ми не маємо прикріплювати будь-що, так що ми можемо також відкинути це геть.

[source,scala]
----
$ runhaskell form_test.hs  
Which color do you associate with the number 1?  
white  
Which color do you associate with the number 2?  
blue  
Which color do you associate with the number 3?  
red  
Which color do you associate with the number 4?  
orange  
The colors that you associate with 1, 2, 3 and 4 are:  
white  
blue  
red  
orange  
----

Насправді ми можемо зробити це без `forM`, але з `forM` це більш читабельне. Звичайно ми пишемо `forM` коли ми бажаємо зробити `map` та `sequence` деяких дій, що ми визначили тут на місці за допомогою `do` нотації. В тому ж ключі ми можемо замінити останній рядок на `forM colors putStrLn`.

В цьому розділі ми навчились основам вводу та виводу. Ми також з'ясували що таке I/O дії, як вони дозволяють нам вводити та виводити рядки, та коли вони насправді виконуються. Для повтору: I/O дії є значення, здебільшого як любі інші значення в Haskell. Ми можемо передавати їх як параметри до функцій, та функції можуть повертати I/O дії як результати. Що особливе щодо них, це якщо вони підпадають в функцію `main` (або є результат в рядку GHCI), вони виконуються. І ось тоді вони починають писати дещо на вашому екрані, або грають Yakety Sax в ваших гучномовцях. Кожна I/O дія також може інкапсулювати результат з тим, що ви бажаєте отримати від зовнішнього світу.

Не думайте про функцію як `putStrLn` як про функцію, що бере рядок і друкує його на екрані. Думайте про це, як про функцію, що бере рядок та повертає I/O дію. Ця I/O дія буде, коли виконана, друкувати прекрасні вірші на вашому терміналі.

=== Файли та потоки

image:images/streams.png[]
`getChar` є I/O дія, що читає поодинокий символ з терміналу. `getLine` є I/O дія, що читає рядок з терміналу. Вони обоє досить прямолінійні, і більшість мов програмування мають деякі функції або твердження, що відповідають до них. Але тепер давайте познайомимось з `getContents`. `getContents` є I/O дія, що читає все зі стандартного вводу, доки не зустріне символ кінця файлу. Її тип `getContents :: IO String`. Що круте щодо `getContents`, це те що вона робить ліниве I/O. Коли ми робимо `foo <- getContents`, вона не читає все і одразу, зберігає в пам'яті і потім прикріплює до `foo`. Ні, вона лінива! Вона каже: "Так так, я прочитаю вхідні дані з терміналу пізніше як ми дійдемо до цього, коли нам це дійсно знадобиться!".

getContents is really useful when we're piping the output from one program into the input of our program. In case you don't know how piping works in unix-y systems, here's a quick primer. Let's make a text file that contains the following little haiku:

I'm a lil' teapot  
What's with that airplane food, huh?  
It's so small, tasteless   
Yeah, the haiku sucks, what of it? If anyone knows of any good haiku tutorials, let me know.

Now, recall the little program we wrote when we were introducing the forever function. It prompted the user for a line, returned it to him in CAPSLOCK and then did that all over again, indefinitely. Just so you don't have to scroll all the way back, here it is again:

import Control.Monad  
import Data.Char  
  
main = forever $ do  
    putStr "Give me some input: "  
    l <- getLine  
    putStrLn $ map toUpper l  
We'll save that program as capslocker.hs or something and compile it. And then, we're going to use a unix pipe to feed our text file directly to our little program. We're going to use the help of the GNU cat program, which prints out a file that's given to it as an argument. Check it out, booyaka!

$ ghc --make capslocker   
[1 of 1] Compiling Main             ( capslocker.hs, capslocker.o )  
Linking capslocker ...  
$ cat haiku.txt  
I'm a lil' teapot  
What's with that airplane food, huh?  
It's so small, tasteless  
$ cat haiku.txt | ./capslocker  
I'M A LIL' TEAPOT  
WHAT'S WITH THAT AIRPLANE FOOD, HUH?  
IT'S SO SMALL, TASTELESS  
capslocker <stdin>: hGetLine: end of file  
As you can see, piping the output of one program (in our case that was cat) to the input of another (capslocker) is done with the | character. What we've done is pretty much equivalent to just running capslocker, typing our haiku at the terminal and then issuing an end-of-file character (that's usually done by pressing Ctrl-D). It's like running cat haiku.txt and saying: “Wait, don't print this out to the terminal, tell it to capslocker instead!”.

So what we're essentially doing with that use of forever is taking the input and transforming it into some output. That's why we can use getContents to make our program even shorter and better:

import Data.Char  
  
main = do  
    contents <- getContents  
    putStr (map toUpper contents)  
We run the getContents I/O action and name the string it produces contents. Then, we map toUpper over that string and print that to the terminal. Keep in mind that because strings are basically lists, which are lazy, and getContents is I/O lazy, it won't try to read the whole content at once and store it into memory before printing out the capslocked version. Rather, it will print out the capslocked version as it reads it, because it will only read a line from the input when it really needs to.

$ cat haiku.txt | ./capslocker  
I'M A LIL' TEAPOT  
WHAT'S WITH THAT AIRPLANE FOOD, HUH?  
IT'S SO SMALL, TASTELESS  
Cool, it works. What if we just run capslocker and try to type in the lines ourselves?

$ ./capslocker  
hey ho  
HEY HO  
lets go  
LETS GO  
We got out of that by pressing Ctrl-D. Pretty nice! As you can see, it prints out our capslocked input back to us line by line. When the result of getContents is bound to contents, it's not represented in memory as a real string, but more like a promise that it will produce the string eventually. When we map toUpper over contents, that's also a promise to map that function over the eventual contents. And finally when putStr happens, it says to the previous promise: "Hey, I need a capslocked line!". It doesn't have any lines yet, so it says to contents: "Hey, how about actually getting a line from the terminal?". So that's when getContents actually reads from the terminal and gives a line to the code that asked it to produce something tangible. That code then maps toUpper over that line and gives it to putStr, which prints it. And then, putStr says: "Hey, I need the next line, come on!" and this repeats until there's no more input, which is signified by an end-of-file character.

Let's make program that takes some input and prints out only those lines that are shorter than 10 characters. Observe:

main = do  
    contents <- getContents  
    putStr (shortLinesOnly contents)  
  
shortLinesOnly :: String -> String  
shortLinesOnly input =   
    let allLines = lines input  
        shortLines = filter (\line -> length line < 10) allLines  
        result = unlines shortLines  
    in  result  
We've made our I/O part of the program as short as possible. Because our program is supposed to take some input and print out some output based on the input, we can implement it by reading the input contents, running a function on them and then printing out what the function gave back.

The shortLinesOnly function works like this: it takes a string, like "short\nlooooooooooooooong\nshort again". That string has three lines, two of them are short and the middle one is long. It runs the lines function on that string, which converts it to ["short", "looooooooooooooong", "short again"], which we then bind to the name allLines. That list of string is then filtered so that only those lines that are shorter than 10 characters remain in the list, producing ["short", "short again"]. And finally, unlines joins that list into a single newline delimited string, giving "short\nshort again". Let's give it a go.

i'm short  
so am i  
i am a loooooooooong line!!!  
yeah i'm long so what hahahaha!!!!!!  
short line  
loooooooooooooooooooooooooooong  
short  
$ ghc --make shortlinesonly  
[1 of 1] Compiling Main             ( shortlinesonly.hs, shortlinesonly.o )  
Linking shortlinesonly ...  
$ cat shortlines.txt | ./shortlinesonly  
i'm short  
so am i  
short  
We pipe the contents of shortlines.txt into the output of shortlinesonly and as the output, we only get the short lines.

This pattern of getting some string from the input, transforming it with a function and then outputting that is so common that there exists a function which makes that even easier, called interact. interact takes a function of type String -> String as a parameter and returns an I/O action that will take some input, run that function on it and then print out the function's result. Let's modify our program to use that.

main = interact shortLinesOnly  
  
shortLinesOnly :: String -> String  
shortLinesOnly input =   
    let allLines = lines input  
        shortLines = filter (\line -> length line < 10) allLines  
        result = unlines shortLines  
    in  result  
Just to show that this can be achieved in much less code (even though it will be less readable) and to demonstrate our function composition skill, we're going to rework that a bit further.

main = interact $ unlines . filter ((<10) . length) . lines  
Wow, we actually reduced that to just one line, which is pretty cool!

interact can be used to make programs that are piped some contents into them and then dump some result out or it can be used to make programs that appear to take a line of input from the user, give back some result based on that line and then take another line and so on. There isn't actually a real distinction between the two, it just depends on how the user is supposed to use them.

Let's make a program that continuously reads a line and then tells us if the line is a palindrome or not. We could just use getLine to read a line, tell the user if it's a palindrome and then run main all over again. But it's simpler if we use interact. When using interact, think about what you need to do to transform some input into the desired output. In our case, we have to replace each line of the input with either "palindrome" or "not a palindrome". So we have to write a function that transforms something like "elephant\nABCBA\nwhatever" into "not a palindrome\npalindrome\nnot a palindrome". Let's do this!

respondPalindromes contents = unlines (map (\xs -> if isPalindrome xs then "palindrome" else "not a palindrome") (lines contents))  
    where   isPalindrome xs = xs == reverse xs  
Let's write this in point-free.

respondPalindromes = unlines . map (\xs -> if isPalindrome xs then "palindrome" else "not a palindrome") . lines  
    where   isPalindrome xs = xs == reverse xs  
Pretty straightforward. First it turns something like "elephant\nABCBA\nwhatever" into ["elephant", "ABCBA", "whatever"] and then it maps that lambda over it, giving ["not a palindrome", "palindrome", "not a palindrome"] and then unlines joins that list into a single, newline delimited string. Now we can do

main = interact respondPalindromes  
Let's test this out:

$ runhaskell palindromes.hs  
hehe  
not a palindrome  
ABCBA  
palindrome  
cookie  
not a palindrome  
Even though we made a program that transforms one big string of input into another, it acts like we made a program that does it line by line. That's because Haskell is lazy and it wants to print the first line of the result string, but it can't because it doesn't have the first line of the input yet. So as soon as we give it the first line of input, it prints the first line of the output. We get out of the program by issuing an end-of-line character.

We can also use this program by just piping a file into it. Let's say we have this file:

dogaroo  
radar  
rotor  
madam  
and we save it as words.txt. This is what we get by piping it into our program:

$ cat words.txt | runhaskell palindromes.hs  
not a palindrome  
palindrome  
palindrome  
palindrome  
Again, we get the same output as if we had run our program and put in the words ourselves at the standard input. We just don't see the input that palindromes.hs because the input came from the file and not from us typing the words in.

So now you probably see how lazy I/O works and how we can use it to our advantage. You can just think in terms of what the output is supposed to be for some given input and write a function to do that transformation. In lazy I/O, nothing is eaten from the input until it absolutely has to be because what we want to print right now depends on that input.

So far, we've worked with I/O by printing out stuff to the terminal and reading from it. But what about reading and writing files? Well, in a way, we've already been doing that. One way to think about reading from the terminal is to imagine that it's like reading from a (somewhat special) file. Same goes for writing to the terminal, it's kind of like writing to a file. We can call these two files stdout and stdin, meaning standard output and standard input, respectively. Keeping that in mind, we'll see that writing to and reading from files is very much like writing to the standard output and reading from the standard input.

We'll start off with a really simple program that opens a file called girlfriend.txt, which contains a verse from Avril Lavigne's #1 hit Girlfriend, and just prints out out to the terminal. Here's girlfriend.txt:

Hey! Hey! You! You!   
I don't like your girlfriend!   
No way! No way!   
I think you need a new one!  
And here's our program:

import System.IO  
  
main = do  
    handle <- openFile "girlfriend.txt" ReadMode  
    contents <- hGetContents handle  
    putStr contents  
    hClose handle  
Running it, we get the expected result:

$ runhaskell girlfriend.hs  
Hey! Hey! You! You!  
I don't like your girlfriend!  
No way! No way!  
I think you need a new one!  
Let's go over this line by line. The first line is just four exclamations, to get our attention. In the second line, Avril tells us that she doesn't like our current romantic partner. The third line serves to emphasize that disapproval, whereas the fourth line suggests we should seek out a new girlfriend.

Let's also go over the program line by line! Our program is several I/O actions glued together with a do block. In the first line of the do block, we notice a new function called openFile. This is its type signature: openFile :: FilePath -> IOMode -> IO Handle. If you read that out loud, it states: openFile takes a file path and an IOMode and returns an I/O action that will open a file and have the file's associated handle encapsulated as its result.

FilePath is just a type synonym for String, simply defined as:

type FilePath = String  
IOMode is a type that's defined like this:

data IOMode = ReadMode | WriteMode | AppendMode | ReadWriteMode  
A FILE IN A CAKE!!!
Just like our type that represents the seven possible values for the days of the week, this type is an enumeration that represents what we want to do with our opened file. Very simple. Just note that this type is IOMode and not IO Mode. IO Mode would be the type of an I/O action that has a value of some type Mode as its result, but IOMode is just a simple enumeration.

Finally, it returns an I/O action that will open the specified file in the specified mode. If we bind that action to something we get a Handle. A value of type Handle represents where our file is. We'll use that handle so we know which file to read from. It would be stupid to read a file but not bind that read to a handle because we wouldn't be able to do anything with the file. So in our case, we bound the handle to handle.

In the next line, we see a function called hGetContents. It takes a Handle, so it knows which file to get the contents from and returns an IO String — an I/O action that holds as its result the contents of the file. This function is pretty much like getContents. The only difference is that getContents will automatically read from the standard input (that is from the terminal), whereas hGetContents takes a file handle which tells it which file to read from. In all other respects, they work the same. And just like getContents, hGetContents won't attempt to read the file at once and store it in memory, but it will read it as needed. That's really cool because we can treat contents as the whole contents of the file, but it's not really loaded in memory. So if this were a really huge file, doing hGetContents wouldn't choke up our memory, but it would read only what it needed to from the file, when it needed to.

Note the difference between the handle used to identify a file and the contents of the file, bound in our program to handle and contents. The handle is just something by which we know what our file is. If you imagine your whole file system to be a really big book and each file is a chapter in the book, the handle is a bookmark that shows where you're currently reading (or writing) a chapter, whereas the contents are the actual chapter.

With putStr contents we just print the contents out to the standard output and then we do hClose, which takes a handle and returns an I/O action that closes the file. You have to close the file yourself after opening it with openFile!

Another way of doing what we just did is to use the withFile function, which has a type signature of withFile :: FilePath -> IOMode -> (Handle -> IO a) -> IO a. It takes a path to a file, an IOMode and then it takes a function that takes a handle and returns some I/O action. What it returns is an I/O action that will open that file, do something we want with the file and then close it. The result encapsulated in the final I/O action that's returned is the same as the result of the I/O action that the function we give it returns. This might sound a bit complicated, but it's really simple, especially with lambdas, here's our previous example rewritten to use withFile:

import System.IO     
    
main = do     
    withFile "girlfriend.txt" ReadMode (\handle -> do  
        contents <- hGetContents handle     
        putStr contents)  
As you can see, it's very similar to the previous piece of code. (\handle -> ... ) is the function that takes a handle and returns an I/O action and it's usually done like this, with a lambda. The reason it has to take a function that returns an I/O action instead of just taking an I/O action to do and then close the file is because the I/O action that we'd pass to it wouldn't know on which file to operate. This way, withFile opens the file and then passes the handle to the function we gave it. It gets an I/O action back from that function and then makes an I/O action that's just like it, only it closes the file afterwards. Here's how we can make our own withFile function:

withFile' :: FilePath -> IOMode -> (Handle -> IO a) -> IO a  
withFile' path mode f = do  
    handle <- openFile path mode   
    result <- f handle  
    hClose handle  
    return result  
butter toast
We know the result will be an I/O action so we can just start off with a do. First we open the file and get a handle from it. Then, we apply handle to our function to get back the I/O action that does all the work. We bind that action to result, close the handle and then do return result. By returning the result encapsulated in the I/O action that we got from f, we make it so that our I/O action encapsulates the same result as the one we got from f handle. So if f handle returns an action that will read a number of lines from the standard input and write them to a file and have as its result encapsulated the number of lines it read, if we used that with withFile', the resulting I/O action would also have as its result the number of lines read.

Just like we have hGetContents that works like getContents but for a specific file, there's also hGetLine, hPutStr, hPutStrLn, hGetChar, etc. They work just like their counterparts without the h, only they take a handle as a parameter and operate on that specific file instead of operating on standard input or standard output. Example: putStrLn is a function that takes a string and returns an I/O action that will print out that string to the terminal and a newline after it. hPutStrLn takes a handle and a string and returns an I/O action that will write that string to the file associated with the handle and then put a newline after it. In the same vein, hGetLine takes a handle and returns an I/O action that reads a line from its file.

Loading files and then treating their contents as strings is so common that we have these three nice little functions to make our work even easier:

readFile has a type signature of readFile :: FilePath -> IO String. Remember, FilePath is just a fancy name for String. readFile takes a path to a file and returns an I/O action that will read that file (lazily, of course) and bind its contents to something as a string. It's usually more handy than doing openFile and binding it to a handle and then doing hGetContents. Here's how we could have written our previous example with readFile:

import System.IO  
  
main = do  
    contents <- readFile "girlfriend.txt"  
    putStr contents  
Because we don't get a handle with which to identify our file, we can't close it manually, so Haskell does that for us when we use readFile.

writeFile has a type of writeFile :: FilePath -> String -> IO (). It takes a path to a file and a string to write to that file and returns an I/O action that will do the writing. If such a file already exists, it will be stomped down to zero length before being written on. Here's how to turn girlfriend.txt into a CAPSLOCKED version and write it to girlfriendcaps.txt:

import System.IO     
import Data.Char  
    
main = do     
    contents <- readFile "girlfriend.txt"     
    writeFile "girlfriendcaps.txt" (map toUpper contents)  
$ runhaskell girlfriendtocaps.hs  
$ cat girlfriendcaps.txt  
HEY! HEY! YOU! YOU!  
I DON'T LIKE YOUR GIRLFRIEND!  
NO WAY! NO WAY!  
I THINK YOU NEED A NEW ONE!  
appendFile has a type signature that's just like writeFile, only appendFile doesn't truncate the file to zero length if it already exists but it appends stuff to it.

Let's say we have a file todo.txt that has one task per line that we have to do. Now let's make a program that takes a line from the standard input and adds that to our to-do list.

import System.IO     
    
main = do     
    todoItem <- getLine  
    appendFile "todo.txt" (todoItem ++ "\n")  
$ runhaskell appendtodo.hs  
Iron the dishes  
$ runhaskell appendtodo.hs  
Dust the dog  
$ runhaskell appendtodo.hs  
Take salad out of the oven  
$ cat todo.txt  
Iron the dishes  
Dust the dog  
Take salad out of the oven  
We needed to add the "\n" to the end of each line because getLine doesn't give us a newline character at the end.

Ooh, one more thing. We talked about how doing contents <- hGetContents handle doesn't cause the whole file to be read at once and stored in-memory. It's I/O lazy, so doing this:

main = do   
    withFile "something.txt" ReadMode (\handle -> do  
        contents <- hGetContents handle  
        putStr contents)  
is actually like connecting a pipe from the file to the output. Just like you can think of lists as streams, you can also think of files as streams. This will read one line at a time and print it out to the terminal as it goes along. So you may be asking, how wide is this pipe then? How often will the disk be accessed? Well, for text files, the default buffering is line-buffering usually. That means that the smallest part of the file to be read at once is one line. That's why in this case it actually reads a line, prints it to the output, reads the next line, prints it, etc. For binary files, the default buffering is usually block-buffering. That means that it will read the file chunk by chunk. The chunk size is some size that your operating system thinks is cool.

You can control how exactly buffering is done by using the hSetBuffering function. It takes a handle and a BufferMode and returns an I/O action that sets the buffering. BufferMode is a simple enumeration data type and the possible values it can hold are: NoBuffering, LineBuffering or BlockBuffering (Maybe Int). The Maybe Int is for how big the chunk should be, in bytes. If it's Nothing, then the operating system determines the chunk size. NoBuffering means that it will be read one character at a time. NoBuffering usually sucks as a buffering mode because it has to access the disk so much.

Here's our previous piece of code, only it doesn't read it line by line but reads the whole file in chunks of 2048 bytes.

main = do   
    withFile "something.txt" ReadMode (\handle -> do  
        hSetBuffering handle $ BlockBuffering (Just 2048)  
        contents <- hGetContents handle  
        putStr contents)  
Reading files in bigger chunks can help if we want to minimize disk access or when our file is actually a slow network resource.

We can also use hFlush, which is a function that takes a handle and returns an I/O action that will flush the buffer of the file associated with the handle. When we're doing line-buffering, the buffer is flushed after every line. When we're doing block-buffering, it's after we've read a chunk. It's also flushed after closing a handle. That means that when we've reached a newline character, the reading (or writing) mechanism reports all the data so far. But we can use hFlush to force that reporting of data that has been read so far. After flushing, the data is available to other programs that are running at the same time.

Think of reading a block-buffered file like this: your toilet bowl is set to flush itself after it has one gallon of water inside it. So you start pouring in water and once the gallon mark is reached, that water is automatically flushed and the data in the water that you've poured in so far is read. But you can flush the toilet manually too by pressing the button on the toilet. This makes the toilet flush and all the water (data) inside the toilet is read. In case you haven't noticed, flushing the toilet manually is a metaphor for hFlush. This is not a very great analogy by programming analogy standards, but I wanted a real world object that can be flushed for the punchline.

We already made a program to add a new item to our to-do list in todo.txt, now let's make a program to remove an item. I'll just paste the code and then we'll go over the program together so you see that it's really easy. We'll be using a few new functions from System.Directory and one new function from System.IO, but they'll all be explained.

Anyway, here's the program for removing an item from todo.txt:

import System.IO  
import System.Directory  
import Data.List  
  
main = do        
    handle <- openFile "todo.txt" ReadMode  
    (tempName, tempHandle) <- openTempFile "." "temp"  
    contents <- hGetContents handle  
    let todoTasks = lines contents     
        numberedTasks = zipWith (\n line -> show n ++ " - " ++ line) [0..] todoTasks     
    putStrLn "These are your TO-DO items:"  
    putStr $ unlines numberedTasks  
    putStrLn "Which one do you want to delete?"     
    numberString <- getLine     
    let number = read numberString     
        newTodoItems = delete (todoTasks !! number) todoTasks     
    hPutStr tempHandle $ unlines newTodoItems  
    hClose handle  
    hClose tempHandle  
    removeFile "todo.txt"  
    renameFile tempName "todo.txt"  
At first, we just open todo.txt in read mode and bind its handle to handle.

Next up, we use a function that we haven't met before which is from System.IO — openTempFile. Its name is pretty self-explanatory. It takes a path to a temporary directory and a template name for a file and opens a temporary file. We used "." for the temporary directory, because . denotes the current directory on just about any OS. We used "temp" as the template name for the temporary file, which means that the temporary file will be named temp plus some random characters. It returns an I/O action that makes the temporary file and the result in that I/O action is a pair of values: the name of the temporary file and a handle. We could just open a normal file called todo2.txt or something like that but it's better practice to use openTempFile so you know you're probably not overwriting anything.

The reason we didn't use getCurrentDirectory to get the current directory and then pass it to openTempFile but instead just passed "." to openTempFile is because . refers to the current directory on unix-like system and Windows

Next up, we bind the contents of todo.txt to contents. Then, split that string into a list of strings, each string one line. So todoTasks is now something like ["Iron the dishes", "Dust the dog", "Take salad out of the oven"]. We zip the numbers from 0 onwards and that list with a function that takes a number, like 3, and a string, like "hey" and returns "3 - hey", so numberedTasks is ["0 - Iron the dishes", "1 - Dust the dog" .... We join that list of strings into a single newline delimited string with unlines and print that string out to the terminal. Note that instead of doing that, we could have also done mapM putStrLn numberedTasks

We ask the user which one they want to delete and wait for them to enter a number. Let's say they want to delete number 1, which is Dust the dog, so they punch in 1. numberString is now "1" and because we want a number, not a string, we run read on that to get 1 and bind that to number.

Remember the delete and !! functions from Data.List. !! returns an element from a list with some index and delete deletes the first occurence of an element in a list and returns a new list without that occurence. (todoTasks !! number) (number is now 1) returns "Dust the dog". We bind todoTasks without the first occurence of "Dust the dog" to newTodoItems and then join that into a single string with unlines before writing it to the temporary file that we opened. The old file is now unchanged and the temporary file contains all the lines that the old one does, except the one we deleted.

After that we close both the original and the temporary files and then we remove the original one with removeFile, which, as you can see, takes a path to a file and deletes it. After deleting the old todo.txt, we use renameFile to rename the temporary file to todo.txt. Be careful, removeFile and renameFile (which are both in System.Directory by the way) take file paths as their parameters, not handles.

And that's that! We could have done this in even fewer lines, but we were very careful not to overwrite any existing files and politely asked the operating system to tell us where we can put our temporary file. Let's give this a go!

$ runhaskell deletetodo.hs  
These are your TO-DO items:  
0 - Iron the dishes  
1 - Dust the dog  
2 - Take salad out of the oven  
Which one do you want to delete?  
1  
  
$ cat todo.txt  
Iron the dishes  
Take salad out of the oven  
  
$ runhaskell deletetodo.hs  
These are your TO-DO items:  
0 - Iron the dishes  
1 - Take salad out of the oven  
Which one do you want to delete?  
0  
  
$ cat todo.txt  
Take salad out of the oven  
Command line arguments
COMMAND LINE ARGUMENTS!!! ARGH
Dealing with command line arguments is pretty much a necessity if you want to make a script or application that runs on a terminal. Luckily, Haskell's standard library has a nice way of getting command line arguments of a program.

In the previous section, we made one program for adding a to-do item to our to-do list and one program for removing an item. There are two problems with the approach we took. The first one is that we just hardcoded the name of our to-do file in our code. We just decided that the file will be named todo.txt and that the user will never have a need for managing several to-do lists.

One way to solve that is to always ask the user which file they want to use as their to-do list. We used that approach when we wanted to know which item the user wants to delete. It works, but it's not so good, because it requires the user to run the program, wait for the program to ask something and then tell that to the program. That's called an interactive program and the difficult bit with interactive command line programs is this — what if you want to automate the execution of that program, like with a batch script? It's harder to make a batch script that interacts with a program than a batch script that just calls one program or several of them.

That's why it's sometimes better to have the user tell the program what they want when they run the program, instead of having the program ask the user once it's run. And what better way to have the user tell the program what they want it to do when they run it than via command line arguments!

The System.Environment module has two cool I/O actions. One is getArgs, which has a type of getArgs :: IO [String] and is an I/O action that will get the arguments that the program was run with and have as its contained result a list with the arguments. getProgName has a type of getProgName :: IO String and is an I/O action that contains the program name.

Here's a small program that demonstrates how these two work:

import System.Environment   
import Data.List  
  
main = do  
   args <- getArgs  
   progName <- getProgName  
   putStrLn "The arguments are:"  
   mapM putStrLn args  
   putStrLn "The program name is:"  
   putStrLn progName  
We bind getArgs and progName to args and progName. We say The arguments are: and then for every argument in args, we do putStrLn. Finally, we also print out the program name. Let's compile this as arg-test.

$ ./arg-test first second w00t "multi word arg"  
The arguments are:  
first  
second  
w00t  
multi word arg  
The program name is:  
arg-test  
Nice. Armed with this knowledge you could create some cool command line apps. In fact, let's go ahead and make one. In the previous section, we made a separate program for adding tasks and a separate program for deleting them. Now, we're going to join that into one program, what it does will depend on the command line arguments. We're also going to make it so it can operate on different files, not just todo.txt.

We'll call it simply todo and it'll be able to do (haha!) three different things:

View tasks
Add tasks
Delete tasks
We're not going to concern ourselves with possible bad input too much right now.

Our program will be made so that if we want to add the task Find the magic sword of power to the file todo.txt, we have to punch in todo add todo.txt "Find the magic sword of power" in our terminal. To view the tasks we'll just do todo view todo.txt and to remove the task with the index of 2, we'll do todo remove todo.txt 2.

We'll start by making a dispatch association list. It's going to be a simple association list that has command line arguments as keys and functions as their corresponding values. All these functions will be of type [String] -> IO (). They're going to take the argument list as a parameter and return an I/O action that does the viewing, adding, deleting, etc.

import System.Environment   
import System.Directory  
import System.IO  
import Data.List  
  
dispatch :: [(String, [String] -> IO ())]  
dispatch =  [ ("add", add)  
            , ("view", view)  
            , ("remove", remove)  
            ]  
We have yet to define main, add, view and remove, so let's start with main:

main = do  
    (command:args) <- getArgs  
    let (Just action) = lookup command dispatch  
    action args  
First, we get the arguments and bind them to (command:args). If you remember your pattern matching, this means that the first argument will get bound to command and the rest of them will get bound to args. If we call our program like todo add todo.txt "Spank the monkey", command will be "add" and args will be ["todo.xt", "Spank the monkey"].

In the next line, we look up our command in the dispatch list. Because "add" points to add, we get Just add as a result. We use pattern matching again to extract our function out of the Maybe. What happens if our command isn't in the dispatch list? Well then the lookup will return Nothing, but we said we won't concern ourselves with failing gracefully too much, so the pattern matching will fail and our program will throw a fit.

Finally, we call our action function with the rest of the argument list. That will return an I/O action that either adds an item, displays a list of items or deletes an item and because that action is part of the main do block, it will get performed. If we follow our concrete example so far and our action function is add, it will get called with args (so ["todo.txt", "Spank the monkey"]) and return an I/O action that adds Spank the monkey to todo.txt.

Great! All that's left now is to implement add, view and remove. Let's start with add:

add :: [String] -> IO ()  
add [fileName, todoItem] = appendFile fileName (todoItem ++ "\n")  
If we call our program like todo add todo.txt "Spank the monkey", the "add" will get bound to command in the first pattern match in the main block, whereas ["todo.txt", "Spank the monkey"] will get passed to the function that we get from the dispatch list. So, because we're not dealing with bad input right now, we just pattern match against a list with those two elements right away and return an I/O action that appends that line to the end of the file, along with a newline character.

Next, let's implement the list viewing functionality. If we want to view the items in a file, we do todo view todo.txt. So in the first pattern match, command will be "view" and args will be ["todo.txt"].

view :: [String] -> IO ()  
view [fileName] = do  
    contents <- readFile fileName  
    let todoTasks = lines contents  
        numberedTasks = zipWith (\n line -> show n ++ " - " ++ line) [0..] todoTasks  
    putStr $ unlines numberedTasks  
We already did pretty much the same thing in the program that only deleted tasks when we were displaying the tasks so that the user can choose one for deletion, only here we just display the tasks.

And finally, we're going to implement remove. It's going to be very similar to the program that only deleted the tasks, so if you don't understand how deleting an item here works, check out the explanation under that program. The main difference is that we're not hardcoding todo.txt but getting it as an argument. We're also not prompting the user for the task number to delete, we're getting it as an argument.

remove :: [String] -> IO ()  
remove [fileName, numberString] = do  
    handle <- openFile fileName ReadMode  
    (tempName, tempHandle) <- openTempFile "." "temp"  
    contents <- hGetContents handle  
    let number = read numberString  
        todoTasks = lines contents  
        newTodoItems = delete (todoTasks !! number) todoTasks  
    hPutStr tempHandle $ unlines newTodoItems  
    hClose handle  
    hClose tempHandle  
    removeFile fileName  
    renameFile tempName fileName  
We opened up the file based on fileName and opened a temporary file, deleted the line with the index that the user wants to delete, wrote that to the temporary file, removed the original file and renamed the temporary file back to fileName.

Here's the whole program at once, in all its glory!

import System.Environment   
import System.Directory  
import System.IO  
import Data.List  
  
dispatch :: [(String, [String] -> IO ())]  
dispatch =  [ ("add", add)  
            , ("view", view)  
            , ("remove", remove)  
            ]  
   
main = do  
    (command:args) <- getArgs  
    let (Just action) = lookup command dispatch  
    action args  
  
add :: [String] -> IO ()  
add [fileName, todoItem] = appendFile fileName (todoItem ++ "\n")  
  
view :: [String] -> IO ()  
view [fileName] = do  
    contents <- readFile fileName  
    let todoTasks = lines contents  
        numberedTasks = zipWith (\n line -> show n ++ " - " ++ line) [0..] todoTasks  
    putStr $ unlines numberedTasks  
  
remove :: [String] -> IO ()  
remove [fileName, numberString] = do  
    handle <- openFile fileName ReadMode  
    (tempName, tempHandle) <- openTempFile "." "temp"  
    contents <- hGetContents handle  
    let number = read numberString  
        todoTasks = lines contents  
        newTodoItems = delete (todoTasks !! number) todoTasks  
    hPutStr tempHandle $ unlines newTodoItems  
    hClose handle  
    hClose tempHandle  
    removeFile fileName  
    renameFile tempName fileName  
fresh baked salad
To summarize our solution: we made a dispatch association that maps from commands to functions that take some command line arguments and return an I/O action. We see what the command is and based on that we get the appropriate function from the dispatch list. We call that function with the rest of the command line arguments to get back an I/O action that will do the appropriate thing and then just perform that action!

In other languages, we might have implemented this with a big switch case statement or whatever, but using higher order functions allows us to just tell the dispatch list to give us the appropriate function and then tell that function to give us an I/O action for some command line arguments.

Let's try our app out!

$ ./todo view todo.txt  
0 - Iron the dishes  
1 - Dust the dog  
2 - Take salad out of the oven  
  
$ ./todo add todo.txt "Pick up children from drycleaners"  
  
$ ./todo view todo.txt  
0 - Iron the dishes  
1 - Dust the dog  
2 - Take salad out of the oven  
3 - Pick up children from drycleaners  
  
$ ./todo remove todo.txt 2  
  
$ ./todo view todo.txt  
0 - Iron the dishes  
1 - Dust the dog  
2 - Pick up children from drycleaners  
Another cool thing about this is that it's easy to add extra functionality. Just add an entry in the dispatch association list and implement the corresponding function and you're laughing! As an exercise, you can try implementing a bump function that will take a file and a task number and return an I/O action that bumps that task to the top of the to-do list.

You could make this program fail a bit more gracefully in case of bad input (for example, if someone runs todo UP YOURS HAHAHAHA) by making an I/O action that just reports there has been an error (say, errorExit :: IO ()) and then check for possible erronous input and if there is erronous input, perform the error reporting I/O action. Another way is to use exceptions, which we will meet soon.

Randomness
this picture is the ultimate source of randomness and wackiness
Many times while programming, you need to get some random data. Maybe you're making a game where a die needs to be thrown or you need to generate some test data to test out your program. There are a lot of uses for random data when programming. Well, actually, pseudo-random, because we all know that the only true source of randomness is a monkey on a unicycle with a cheese in one hand and its butt in the other. In this section, we'll take a look at how to make Haskell generate seemingly random data.

In most other programming languages, you have functions that give you back some random number. Each time you call that function, you get back a (hopefully) different random number. How about Haskell? Well, remember, Haskell is a pure functional language. What that means is that it has referential transparency. What THAT means is that a function, if given the same parameters twice, must produce the same result twice. That's really cool because it allows us to reason differently about programs and it enables us to defer evaluation until we really need it. If I call a function, I can be sure that it won't do any funny stuff before giving me the results. All that matters are its results. However, this makes it a bit tricky for getting random numbers. If I have a function like this:

randomNumber :: (Num a) => a  
randomNumber = 4  
It's not very useful as a random number function because it will always return 4, even though I can assure you that the 4 is completely random, because I used a die to determine it.

How do other languages make seemingly random numbers? Well, they take various info from your computer, like the current time, how much and where you moved your mouse and what kind of noises you made behind your computer and based on that, give a number that looks really random. The combination of those factors (that randomness) is probably different in any given moment in time, so you get a different random number.

Ah. So in Haskell, we can make a random number then if we make a function that takes as its parameter that randomness and based on that returns some number (or other data type).

Enter the System.Random module. It has all the functions that satisfy our need for randomness. Let's just dive into one of the functions it exports then, namely random. Here's its type: random :: (RandomGen g, Random a) => g -> (a, g). Whoa! Some new typeclasses in this type declaration up in here! The RandomGen typeclass is for types that can act as sources of randomness. The Random typeclass is for things that can take on random values. A boolean value can take on a random value, namely True or False. A number can also take up a plethora of different random values. Can a function take on a random value? I don't think so, probably not! If we try to translate the type declaration of random to English, we get something like: it takes a random generator (that's our source of randomness) and returns a random value and a new random generator. Why does it also return a new generator as well as a random value? Well, we'll see in a moment.

To use our random function, we have to get our hands on one of those random generators. The System.Random module exports a cool type, namely StdGen that is an instance of the RandomGen typeclass. We can either make a StdGen manually or we can tell the system to give us one based on a multitude of sort of random stuff.

To manually make a random generator, use the mkStdGen function. It has a type of mkStdGen :: Int -> StdGen. It takes an integer and based on that, gives us a random generator. Okay then, let's try using random and mkStdGen in tandem to get a (hardly random) number.

ghci> random (mkStdGen 100)  
<interactive>:1:0:  
    Ambiguous type variable `a' in the constraint:  
      `Random a' arising from a use of `random' at <interactive>:1:0-20  
    Probable fix: add a type signature that fixes these type variable(s)  
What's this? Ah, right, the random function can return a value of any type that's part of the Random typeclass, so we have to inform Haskell what kind of type we want. Also let's not forget that it returns a random value and a random generator in a pair.

ghci> random (mkStdGen 100) :: (Int, StdGen)  
(-1352021624,651872571 1655838864)  
Finally! A number that looks kind of random! The first component of the tuple is our number whereas the second component is a textual representation of our new random generator. What happens if we call random with the same random generator again?

ghci> random (mkStdGen 100) :: (Int, StdGen)  
(-1352021624,651872571 1655838864)  
Of course. The same result for the same parameters. So let's try giving it a different random generator as a parameter.

ghci> random (mkStdGen 949494) :: (Int, StdGen)  
(539963926,466647808 1655838864)  
Alright, cool, great, a different number. We can use the type annotation to get different types back from that function.

ghci> random (mkStdGen 949488) :: (Float, StdGen)  
(0.8938442,1597344447 1655838864)  
ghci> random (mkStdGen 949488) :: (Bool, StdGen)  
(False,1485632275 40692)  
ghci> random (mkStdGen 949488) :: (Integer, StdGen)  
(1691547873,1597344447 1655838864)  
Let's make a function that simulates tossing a coin three times. If random didn't return a new generator along with a random value, we'd have to make this function take three random generators as a parameter and then return coin tosses for each of them. But that sounds wrong because if one generator can make a random value of type Int (which can take on a load of different values), it should be able to make three coin tosses (which can take on precisely eight combinations). So this is where random returning a new generator along with a value really comes in handy.

We'll represent a coin with a simple Bool. True is tails, False is heads.

threeCoins :: StdGen -> (Bool, Bool, Bool)  
threeCoins gen =   
    let (firstCoin, newGen) = random gen  
        (secondCoin, newGen') = random newGen  
        (thirdCoin, newGen'') = random newGen'  
    in  (firstCoin, secondCoin, thirdCoin)  
We call random with the generator we got as a parameter to get a coin and a new generator. Then we call it again, only this time with our new generator, to get the second coin. We do the same for the third coin. Had we called it with the same generator every time, all the coins would have had the same value and we'd only be able to get (False, False, False) or (True, True, True) as a result.

ghci> threeCoins (mkStdGen 21)  
(True,True,True)  
ghci> threeCoins (mkStdGen 22)  
(True,False,True)  
ghci> threeCoins (mkStdGen 943)  
(True,False,True)  
ghci> threeCoins (mkStdGen 944)  
(True,True,True)  
Notice that we didn't have to do random gen :: (Bool, StdGen). That's because we already specified that we want booleans in the type declaration of the function. That's why Haskell can infer that we want a boolean value in this case.

So what if we want to flip four coins? Or five? Well, there's a function called randoms that takes a generator and returns an infinite sequence of values based on that generator.

ghci> take 5 $ randoms (mkStdGen 11) :: [Int]  
[-1807975507,545074951,-1015194702,-1622477312,-502893664]  
ghci> take 5 $ randoms (mkStdGen 11) :: [Bool]  
[True,True,True,True,False]  
ghci> take 5 $ randoms (mkStdGen 11) :: [Float]  
[7.904789e-2,0.62691015,0.26363158,0.12223756,0.38291094]  
Why doesn't randoms return a new generator as well as a list? We could implement the randoms function very easily like this:

randoms' :: (RandomGen g, Random a) => g -> [a]  
randoms' gen = let (value, newGen) = random gen in value:randoms' newGen  
A recursive definition. We get a random value and a new generator from the current generator and then make a list that has the value as its head and random numbers based on the new generator as its tail. Because we have to be able to potentially generate an infinite amount of numbers, we can't give the new random generator back.

We could make a function that generates a finite stream of numbers and a new generator like this:

finiteRandoms :: (RandomGen g, Random a, Num n) => n -> g -> ([a], g)  
finiteRandoms 0 gen = ([], gen)  
finiteRandoms n gen =   
    let (value, newGen) = random gen  
        (restOfList, finalGen) = finiteRandoms (n-1) newGen  
    in  (value:restOfList, finalGen)  
Again, a recursive definition. We say that if we want 0 numbers, we just return an empty list and the generator that was given to us. For any other number of random values, we first get one random number and a new generator. That will be the head. Then we say that the tail will be n - 1 numbers generated with the new generator. Then we return the head and the rest of the list joined and the final generator that we got from getting the n - 1 random numbers.

What if we want a random value in some sort of range? All the random integers so far were outrageously big or small. What if we want to to throw a die? Well, we use randomR for that purpose. It has a type of randomR :: (RandomGen g, Random a) :: (a, a) -> g -> (a, g), meaning that it's kind of like random, only it takes as its first parameter a pair of values that set the lower and upper bounds and the final value produced will be within those bounds.

ghci> randomR (1,6) (mkStdGen 359353)  
(6,1494289578 40692)  
ghci> randomR (1,6) (mkStdGen 35935335)  
(3,1250031057 40692)  
There's also randomRs, which produces a stream of random values within our defined ranges. Check this out:

ghci> take 10 $ randomRs ('a','z') (mkStdGen 3) :: [Char]  
"ndkxbvmomg"  
Nice, looks like a super secret password or something.

You may be asking yourself, what does this section have to do with I/O anyway? We haven't done anything concerning I/O so far. Well, so far we've always made our random number generator manually by making it with some arbitrary integer. The problem is, if we do that in our real programs, they will always return the same random numbers, which is no good for us. That's why System.Random offers the getStdGen I/O action, which has a type of IO StdGen. When your program starts, it asks the system for a good random number generator and stores that in a so called global generator. getStdGen fetches you that global random generator when you bind it to something.

Here's a simple program that generates a random string.

import System.Random  
  
main = do  
    gen <- getStdGen  
    putStr $ take 20 (randomRs ('a','z') gen)  
$ runhaskell random_string.hs  
pybphhzzhuepknbykxhe  
$ runhaskell random_string.hs  
eiqgcxykivpudlsvvjpg  
$ runhaskell random_string.hs  
nzdceoconysdgcyqjruo  
$ runhaskell random_string.hs  
bakzhnnuzrkgvesqplrx  
Be careful though, just performing getStdGen twice will ask the system for the same global generator twice. If you do this:

import System.Random  
  
main = do  
    gen <- getStdGen  
    putStrLn $ take 20 (randomRs ('a','z') gen)  
    gen2 <- getStdGen  
    putStr $ take 20 (randomRs ('a','z') gen2)  
you will get the same string printed out twice! One way to get two different strings of length 20 is to set up an infinite stream and then take the first 20 characters and print them out in one line and then take the second set of 20 characters and print them out in the second line. For this, we can use the splitAt function from Data.List, which splits a list at some index and returns a tuple that has the first part as the first component and the second part as the second component.

import System.Random  
import Data.List  
  
main = do  
    gen <- getStdGen  
    let randomChars = randomRs ('a','z') gen  
        (first20, rest) = splitAt 20 randomChars  
        (second20, _) = splitAt 20 rest  
    putStrLn first20  
    putStr second20  
Another way is to use the newStdGen action, which splits our current random generator into two generators. It updates the global random generator with one of them and encapsulates the other as its result.

import System.Random  
  
main = do     
    gen <- getStdGen     
    putStrLn $ take 20 (randomRs ('a','z') gen)     
    gen' <- newStdGen  
    putStr $ take 20 (randomRs ('a','z') gen')     
Not only do we get a new random generator when we bind newStdGen to something, the global one gets updated as well, so if we do getStdGen again and bind it to something, we'll get a generator that's not the same as gen.

Here's a little program that will make the user guess which number it's thinking of.

import System.Random  
import Control.Monad(when)  
  
main = do  
    gen <- getStdGen  
    askForNumber gen  
  
askForNumber :: StdGen -> IO ()  
askForNumber gen = do  
    let (randNumber, newGen) = randomR (1,10) gen :: (Int, StdGen)  
    putStr "Which number in the range from 1 to 10 am I thinking of? "  
    numberString <- getLine  
    when (not $ null numberString) $ do  
        let number = read numberString  
        if randNumber == number   
            then putStrLn "You are correct!"  
            else putStrLn $ "Sorry, it was " ++ show randNumber  
        askForNumber newGen  
jack of diamonds
We make a function askForNumber, which takes a random number generator and returns an I/O action that will prompt the user for a number and tell him if he guessed it right. In that function, we first generate a random number and a new generator based on the generator that we got as a parameter and call them randNumber and newGen. Let's say that the number generated was 7. Then we tell the user to guess which number we're thinking of. We perform getLine and bind its result to numberString. When the user enters 7, numberString becomes "7". Next, we use when to check if the string the user entered is an empty string. If it is, an empty I/O action of return () is performed, which effectively ends the program. If it isn't, the action consisting of that do block right there gets performed. We use read on numberString to convert it to a number, so number is now 7.

Excuse me! If the user gives us some input here that read can't read (like "haha"), our program will crash with an ugly error message. If you don't want your program to crash on erronous input, use reads, which returns an empty list when it fails to read a string. When it succeeds, it returns a singleton list with a tuple that has our desired value as one component and a string with what it didn't consume as the other.
We check if the number that we entered is equal to the one generated randomly and give the user the appropriate message. And then we call askForNumber recursively, only this time with the new generator that we got, which gives us an I/O action that's just like the one we performed, only it depends on a different generator and we perform it.

main consists of just getting a random generator from the system and calling askForNumber with it to get the initial action.

Here's our program in action!

$ runhaskell guess_the_number.hs  
Which number in the range from 1 to 10 am I thinking of? 4  
Sorry, it was 3  
Which number in the range from 1 to 10 am I thinking of? 10  
You are correct!  
Which number in the range from 1 to 10 am I thinking of? 2  
Sorry, it was 4  
Which number in the range from 1 to 10 am I thinking of? 5  
Sorry, it was 10  
Which number in the range from 1 to 10 am I thinking of?  
Another way to make this same program is like this:

import System.Random  
import Control.Monad(when)  
  
main = do  
    gen <- getStdGen  
    let (randNumber, _) = randomR (1,10) gen :: (Int, StdGen)     
    putStr "Which number in the range from 1 to 10 am I thinking of? "  
    numberString <- getLine  
    when (not $ null numberString) $ do  
        let number = read numberString  
        if randNumber == number  
            then putStrLn "You are correct!"  
            else putStrLn $ "Sorry, it was " ++ show randNumber  
        newStdGen  
        main  
It's very similar to the previous version, only instead of making a function that takes a generator and then calls itself recursively with the new updated generator, we do all the work in main. After telling the user whether they were correct in their guess or not, we update the global generator and then call main again. Both approaches are valid but I like the first one more since it does less stuff in main and also provides us with a function that we can reuse easily.

Bytestrings
like normal string, only they byte ... what a pedestrian pun this is
Lists are a cool and useful data structure. So far, we've used them pretty much everywhere. There are a multitude of functions that operate on them and Haskell's laziness allows us to exchange the for and while loops of other languages for filtering and mapping over lists, because evaluation will only happen once it really needs to, so things like infinite lists (and even infinite lists of infinite lists!) are no problem for us. That's why lists can also be used to represent streams, either when reading from the standard input or when reading from files. We can just open a file and read it as a string, even though it will only be accessed when the need arises.

However, processing files as strings has one drawback: it tends to be slow. As you know, String is a type synonym for [Char]. Chars don't have a fixed size, because it takes several bytes to represent a character from, say, Unicode. Furthemore, lists are really lazy. If you have a list like [1,2,3,4], it will be evaluated only when completely necessary. So the whole list is sort of a promise of a list. Remember that [1,2,3,4] is syntactic sugar for 1:2:3:4:[]. When the first element of the list is forcibly evaluated (say by printing it), the rest of the list 2:3:4:[] is still just a promise of a list, and so on. So you can think of lists as promises that the next element will be delivered once it really has to and along with it, the promise of the element after it. It doesn't take a big mental leap to conclude that processing a simple list of numbers as a series of promises might not be the most efficient thing in the world.

That overhead doesn't bother us so much most of the time, but it turns out to be a liability when reading big files and manipulating them. That's why Haskell has bytestrings. Bytestrings are sort of like lists, only each element is one byte (or 8 bits) in size. The way they handle laziness is also different.

Bytestrings come in two flavors: strict and lazy ones. Strict bytestrings reside in Data.ByteString and they do away with the laziness completely. There are no promises involved; a strict bytestring represents a series of bytes in an array. You can't have things like infinite strict bytestrings. If you evaluate the first byte of a strict bytestring, you have to evaluate it whole. The upside is that there's less overhead because there are no thunks (the technical term for promise) involved. The downside is that they're likely to fill your memory up faster because they're read into memory at once.

The other variety of bytestrings resides in Data.ByteString.Lazy. They're lazy, but not quite as lazy as lists. Like we said before, there are as many thunks in a list as there are elements. That's what makes them kind of slow for some purposes. Lazy bytestrings take a different approach — they are stored in chunks (not to be confused with thunks!), each chunk has a size of 64K. So if you evaluate a byte in a lazy bytestring (by printing it or something), the first 64K will be evaluated. After that, it's just a promise for the rest of the chunks. Lazy bytestrings are kind of like lists of strict bytestrings with a size of 64K. When you process a file with lazy bytestrings, it will be read chunk by chunk. This is cool because it won't cause the memory usage to skyrocket and the 64K probably fits neatly into your CPU's L2 cache.

If you look through the documentation for Data.ByteString.Lazy, you'll see that it has a lot of functions that have the same names as the ones from Data.List, only the type signatures have ByteString instead of [a] and Word8 instead of a in them. The functions with the same names mostly act the same as the ones that work on lists. Because the names are the same, we're going to do a qualified import in a script and then load that script into GHCI to play with bytestrings.

import qualified Data.ByteString.Lazy as B  
import qualified Data.ByteString as S  
B has lazy bytestring types and functions, whereas S has strict ones. We'll mostly be using the lazy version.

The function pack has the type signature pack :: [Word8] -> ByteString. What that means is that it takes a list of bytes of type Word8 and returns a ByteString. You can think of it as taking a list, which is lazy, and making it less lazy, so that it's lazy only at 64K intervals.

What's the deal with that Word8 type? Well, it's like Int, only that it has a much smaller range, namely 0-255. It represents an 8-bit number. And just like Int, it's in the Num typeclass. For instance, we know that the value 5 is polymorphic in that it can act like any numeral type. Well, it can also take the type of Word8.

ghci> B.pack [99,97,110]  
Chunk "can" Empty  
ghci> B.pack [98..120]  
Chunk "bcdefghijklmnopqrstuvwx" Empty  
As you can see, you usually don't have to worry about the Word8 too much, because the type system can makes the numbers choose that type. If you try to use a big number, like 336 as a Word8, it will just wrap around to 80.

We packed only a handful of values into a ByteString, so they fit inside one chunk. The Empty is like the [] for lists.

unpack is the inverse function of pack. It takes a bytestring and turns it into a list of bytes.

fromChunks takes a list of strict bytestrings and converts it to a lazy bytestring. toChunks takes a lazy bytestring and converts it to a list of strict ones.

ghci> B.fromChunks [S.pack [40,41,42], S.pack [43,44,45], S.pack [46,47,48]]  
Chunk "()*" (Chunk "+,-" (Chunk "./0" Empty))  
This is good if you have a lot of small strict bytestrings and you want to process them efficiently without joining them into one big strict bytestring in memory first.

The bytestring version of : is called cons It takes a byte and a bytestring and puts the byte at the beginning. It's lazy though, so it will make a new chunk even if the first chunk in the bytestring isn't full. That's why it's better to use the strict version of cons, cons' if you're going to be inserting a lot of bytes at the beginning of a bytestring.

ghci> B.cons 85 $ B.pack [80,81,82,84]  
Chunk "U" (Chunk "PQRT" Empty)  
ghci> B.cons' 85 $ B.pack [80,81,82,84]  
Chunk "UPQRT" Empty  
ghci> foldr B.cons B.empty [50..60]  
Chunk "2" (Chunk "3" (Chunk "4" (Chunk "5" (Chunk "6" (Chunk "7" (Chunk "8" (Chunk "9" (Chunk ":" (Chunk ";" (Chunk "<"  
Empty))))))))))  
ghci> foldr B.cons' B.empty [50..60]  
Chunk "23456789:;<" Empty  
As you can see empty makes an empty bytestring. See the difference between cons and cons'? With the foldr, we started with an empty bytestring and then went over the list of numbers from the right, adding each number to the beginning of the bytestring. When we used cons, we ended up with one chunk for every byte, which kind of defeats the purpose.

Otherwise, the bytestring modules have a load of functions that are analogous to those in Data.List, including, but not limited to, head, tail, init, null, length, map, reverse, foldl, foldr, concat, takeWhile, filter, etc.

It also has functions that have the same name and behave the same as some functions found in System.IO, only Strings are replaced with ByteStrings. For instance, the readFile function in System.IO has a type of readFile :: FilePath -> IO String, while the readFile from the bytestring modules has a type of readFile :: FilePath -> IO ByteString. Watch out, if you're using strict bytestrings and you attempt to read a file, it will read it into memory at once! With lazy bytestrings, it will read it into neat chunks.

Let's make a simple program that takes two filenames as command-line arguments and copies the first file into the second file. Note that System.Directory already has a function called copyFile, but we're going to implement our own file copying function and program anyway.

import System.Environment  
import qualified Data.ByteString.Lazy as B  
  
main = do  
    (fileName1:fileName2:_) <- getArgs  
    copyFile fileName1 fileName2  
  
copyFile :: FilePath -> FilePath -> IO ()  
copyFile source dest = do  
    contents <- B.readFile source  
    B.writeFile dest contents  
We make our own function that takes two FilePaths (remember, FilePath is just a synonym for String) and returns an I/O action that will copy one file into another using bytestring. In the main function, we just get the arguments and call our function with them to get the I/O action, which is then performed.

$ runhaskell bytestringcopy.hs something.txt ../../something.txt  
Notice that a program that doesn't use bytestrings could look just like this, the only difference is that we used B.readFile and B.writeFile instead of readFile and writeFile. Many times, you can convert a program that uses normal strings to a program that uses bytestrings by just doing the necessary imports and then putting the qualified module names in front of some functions. Sometimes, you have to convert functions that you wrote to work on strings so that they work on bytestrings, but that's not hard.

Whenever you need better performance in a program that reads a lot of data into strings, give bytestrings a try, chances are you'll get some good performance boosts with very little effort on your part. I usually write programs by using normal strings and then convert them to use bytestrings if the performance is not satisfactory.

=== Виключення

image:images/timber.png[]
Всі мови мають процедури, функції та частини коду, що можуть схибити в деякий спосіб. Це просто життєвий факт. Різні мови мають різні шляхи обробки ціх збоїв. В C ми зазвичай використовуємо деяке незвичайне значення повернення (як `-1` або вказівник `null`), щоб вказати, що те, що повернула функція, не може розглядатись як звичайне значення. З іншого боку, Java та C# схиляються до використання виключень для обробки збоїв. Коли закидається виключення, контроль виконання перестрибує на деякий визначений вами код, що робить деяке очищення, і можливо пере-закидає виключення, так що інший код обробки помилок зможе потурбуватись про деякі інші речі.

Haskell має дуже гарну систему типів. Алгебраїчні типи даних дозволяють такі типи, як `Maybe` та `Either`, і ми можемо використовувати значення ціх типів для представлення результатів, що можуть бути, або ні. Повернення в C, скажімо, `-1` в разі збою, повністю є предметом домовленості. Це також має особливе значення для людини. Якщо ми не будемо обережні, ми можемо розглядати ці незвичайні значення як звичайні, і потім вони можуть наробити безлад і жах в нашому коді. Система типів Haskell дає нам деяку таку-необхідну безпеку в цьому аспекті. Функція `a -> Maybe b` ясно вказує, що вона може продукувати `b` огорнуте в `Just`, або що вона може повернути `Nothing`. Тип відмінний від простого `a -> b`, і якщо ми спробуємо використати ці дві функції одну замість іншої, копілятор буде скаржитись на нас.

Не зважаючи на присутність виразних типів, що підтримують збійні обчислення, Haskell все ще має підтримку для виключень, оскільки вони мають більше сенсу в I/O контекстах. Багато речей можуть пійти не так, коли ви маєте справу з зовнішнім світом, тому що він такий ненадійний. Наприклад, коли відкривається файл, ціла кіпа речей може пійти не так. Файл може бути заблокований, він може взагалі бути відсутній, або може бути відсутній взагалі весь жорстуий диск, або будь-що ще. Так що це гарно бути в змозі перестрибнути на деяку частину обробки помилок в вашому коді, коли такі помилки трапляються.

Гаразд, так що I/O код (тобто нечистий код) може закидати виключення. Це має сенс. То що щодо чистого коду? Гаразд, він також може закидати виключення. Подумайте про функції `div` та `head`. Вони мають типи `(Integral a) => a -> a -> a` та `[a] -> a`, відповідно. Ні `Maybe` або `Either` в їх типах повернення, і вони обоє можуть схибити! `div` вибухне вам в обличчя, коли ви спробуєте поділити на ноль, та `head` впадає в істеріку, коли ви передасте їй порожній список.

[source,scala]
----
ghci> 4 `div` 0  
*** Exception: divide by zero  
ghci> head []  
*** Exception: Prelude.head: empty list
----

image:images/police.png[]
Чистий код може закидати виключення, але вони можуть бути перехоплені в I/O частині нашого кода (коли ми всередині блока `do`, що іде в `main`). Це тому, що ви не знаєте коли (та взагалі чи) будь-що буде виконане в чистому коді, оскільки він лінивий, та не має гарно визначеного порядку виконання, тоді як I/O код має.

Раніше ми казали про те, що нам слідує проводити щонайменьше часу в  I/O частині нашої програми. Логіка нашої програми повинна здебільшого залишатись в наших чистих функціях, оскільки їх результати залежать лише від параметрів, з якими викликаються функції. Коли ми маємо справи з чистими функціями, ви маєте думати лише про те, що повертає функція, оскільки вона не може робити будь-що інше. Це робить ваше життя простіше. Навіть якщо і робити деяку логіку в I/O необхідно (як відкривати файли і подібні речі), повинно утримувати її на мінімумі. Чисті функції ліниві по замовчанню, що означає, що ми не знаємо, коли вони будуть обчислені, і що це насправді не повинно мати значення. Однак як тільки чисті функції починають закидати виключення, стає важливим, коли вони обчислюються. Ось чому ми лише можемо перехоплювати виключення, закинуті в чистому коді, в I/O частині нашого кода. І це погано, оскільки ми бажаємо утримувати I/O частину малою, наскільки це можливо. Однак якщо ми не перехоплюємо їх в I/O частині нашого коду, наша програма падає. Яке рішення? Не змішуйте виключення і чистий код. Отримуйте перевагу від потужної системи типів Haskell, і використовуйте типи як `Either` та `Maybe` для представлення результатів, що можуть схибити.

Ось чому поки що ми тільки подивимось, як використовувати I/O виключення. I/O виключення є виключення, що виникають коли щось іде не так при взаємодії з зовнішнім світом в I/O дії, що є частиною `main`. Наприклад, ми можемо спробувати відкрити файл, і потім з'ясовується, що файл був видалений або ще щось. Погляньте на цю програму, яка відкриває файл, чиє ім'я надається в аргументі командного рядка, і каже, скільки рядків має цей файл.

[source,scala]
----
import System.Environment  
import System.IO  
  
main = do (fileName:_) <- getArgs  
          contents <- readFile fileName  
          putStrLn $ "The file has " ++ show (length (lines contents)) ++ " lines!"  
----

Дуже проста програма. Ми виконуємо I/O дію `getArgs`, і прикріплюємо перший рядок в списку, що вона видає, до `fileName`. Потім ми викликаємо `contents` файла з вмістом цього ім'я. Нарешті ми застосовуємо рядки цього `contents`, щоб отримати список рядків, і потім ми отримуємо довжину списка щоб надати її до `show`, щоб отримати рядкове представлення цього числа. Це робить як очікується, але що трапиться, коли ми дамо йому ім'я файла, що не існує?

[source,bash]
----
$ runhaskell linecount.hs i_dont_exist.txt  
linecount.hs: i_dont_exist.txt: openFile: does not exist (No such file or directory)  
----

Ага, ми отримали помилку від GHC, яке каже нам, що файл не існує. Наша програма падає. Що, коли ми бажаємо надрукувати краще повідомлення якщо файл не існує? Один спосіб зробити це є перевірити, чи файл існує, перед спробою відкрити його, використовуючи функцію `doesFileExist` з `System.Directory`.

[source,scala]
----
import System.Environment  
import System.IO  
import System.Directory  
  
main = do (fileName:_) <- getArgs  
          fileExists <- doesFileExist fileName  
          if fileExists  
              then do contents <- readFile fileName  
                      putStrLn $ "The file has " ++ show (length (lines contents)) ++ " lines!"  
              else do putStrLn "The file doesn't exist!"  
----

Ми зробили `fileExists <- doesFileExist fileName`, оскільки `doesFileExist` має тип `doesFileExist :: FilePath -> IO Bool`, що означає, що вона повертає I/O дію, що має як результат логічне значення, яке каже нам чи файл існує. Ми не можемо просто використовувати `doesFileExist` в `if` виразі напряму.

Іншим рішенням тут може бути використання виключень. Це чудово припустиме використовувати їх в цьому контексті. Неіснуючий файл є виключенням, що походить від I/O, так що перехоплення його в I/O є гарним і пристойним.

Щоб впоратись з цім через використання виключень ми збираємось скористатись перевагами від функції `catch` з `System.IO.Error`. Її тип є `catch :: IO a -> (IOError -> IO a) -> IO a`. Вона приймає два параметри. Перший є I/O дією. Наприклад це може бути I/O дією, що намагається виконати файл. Другий є так званим обробником. Якщо перша I/O дія, парадана до `catch`, закидає I/O виключення, це виключення передається в обробник, який вирішує що робити далі. Так що фінальний результат є I/O дія, що буде діяти або як перший параметр, або буде робити те що каже обробник, якщо перша I/O дія закидає виключення.

image:images/puppy.png[]
Якщо ви знайомі з `try-catch` блоками в мовах як Java або Python, функція `catch` подібна до них. Перший параметр є річ, що треба спробувати, як щось таке в блоці `try` в інших, імперативних мовах. Другий параметр є обробником, що приймає виключення, як більшість  `catch` блоків приймають виключення, що ви потім можете перевіряти, щоб побачити, що трапилось. Обробник викликається, якщо виключення було закинуте.

Обробник бере значення типу `IOError`, що є значенням, яке вказує, що трапилось I/O виключення. Воно також несе інформацію відносно типу виключення, що було закинуте. Як цей тип реалізований залежить від реалізації самої мови, що означає, що ми не можемо досліджувати значення типу `IOError` через співпадіння шаблону, так само як ми не можемо порівнювати шаблони до значень типу `IO something`. Ми можемо використовувати декілька корисних предикатів, щоб отримувати корисну інформацію щодо значень типу `IOError`, які ми вивчино через мить.

Так що давайте застосуємо нового друга `catch`!

[source,scala]
----
import System.Environment  
import System.IO  
import System.IO.Error  
  
main = toTry `catch` handler  
              
toTry :: IO ()  
toTry = do (fileName:_) <- getArgs  
           contents <- readFile fileName  
           putStrLn $ "The file has " ++ show (length (lines contents)) ++ " lines!"  
  
handler :: IOError -> IO ()  
handler e = putStrLn "Whoops, had some trouble!"  
----

Для початку ви бачите, що ми поклали зворотні апострофи кругом нього, так що ми можемо використовувати його як інфіксну функцію, бо він приймає два параметри. Його використання як інфіксної функції робить його більш читабельним. Так що `toTry ``catch`` handler` є те саме,що і `catch toTry handler`, що гарно пасує до його типу. `toTry` є I/O дія, яку ми намагаємось використати, і `handler` є функція, що приймає `IOError`, та повертає дію, яка виконується в випадку виключення.

Давайте надамо цьому руху:

[source,bash]
----
$ runhaskell count_lines.hs i_exist.txt  
The file has 3 lines!
$ runhaskell count_lines.hs i_dont_exist.txt  
Whoops, had some trouble!  
----

В обробнику ми не перевіряли, який тип `IOError` ми отримали. Ми просто кажемо `"Whoops, had some trouble!"` для любого типу помилок. Просте перехоплення всіх типів виключень в обробнику є поганою практикою в Haskell, так само як в більшості інших мов. Що коли трапляться деякі інші виключення, які ми не бажаємо перехоплювати, як переривання програми або щось інше? Ось чому ми бажаємо зробити ту саму річ, яку ми звичайно робимо також в інших мовах: ми перевіримо, який саме тип виключення ми отримали. Якщо це різновид виключення, який ми бажаємо перехопити, ми робимо наші речі. Якщо ні - ми знову перезапускаємо виключення. Давайте модифікуємо нашу програму для перехоплення тільки виключень, викликаних відсутністю файла.

[source,scala]
----
import System.Environment  
import System.IO  
import System.IO.Error  
  
main = toTry `catch` handler  
              
toTry :: IO ()  
toTry = do (fileName:_) <- getArgs  
           contents <- readFile fileName  
           putStrLn $ "The file has " ++ show (length (lines contents)) ++ " lines!"  
  
handler :: IOError -> IO ()  
handler e  
    | isDoesNotExistError e = putStrLn "The file doesn't exist!"  
    | otherwise = ioError e  
----

Все залишається тим самим, крім обробника, який ми модифікували для перехоплення тільки певної групи I/O виключень. Тут ми використали дві нові функції з `System.IO.Error` — `isDoesNotExistError` та `ioError`. `isDoesNotExistError` є предикатом на `IOErrors`, що означає, що це функція, що приймає `IOError` і повертає `True` або `False`, що означає що вона має тип `isDoesNotExistError :: IOError -> Bool`. Ми використовуємо його на виключенні, що передано в наш обробник, щоб побачити, чи це помилка спричинена неіснуванням файла. Тут ми використали синтаксис охоронців, але також можна застосувати  `if else`. Якщо це не спричинено неіснуванням файла, ми пере-закидаєм виключення, що було передане в обробник за допомогою функції `ioError`. Вона має тип `ioError :: IOException -> IO a`, так що вона приймає `IOError` і продукує I/O дію, що буде закидати її. I/O має тип `IO a`, оскільки вона насправді ніколи не видає результат, так що вона може діяти як `IO anything`.

Так що якщо виключення, закинуте в I/O дії `toTry`, що ми склеїли разом в блок `do`, не спричинене неіснуванням файла, `toTry ``catch`` handler` буде перехоплювати, та потім пере-закидати його. Досить круто, так?

Існують декілька предикатів, що діють на `IOError`, і якщо охоронець не обчислюється до `True`, обчислення провалюється до наступного охоронця. Предикати, що діють на `IOError`, такі:

[source,scala]
----
isAlreadyExistsError
isDoesNotExistError
isAlreadyInUseError
isFullError
isEOFError
isIllegalOperation
isPermissionError
isUserError
----

Більшість з цього є досить самоописовим. `isUserError` обислюється до `True`, коли ми використовуємо функцію `userError` для створення виключення, що використовується для створення виключень в нашому коді, і оснащення його рядком. Наприклад, ви можете зробити `ioError $ userError "remote computer unplugged!"`, хоча бажано щоб ви використовували типи як `Either` та `Maybe` для представлення можливого збою, замість закидання виключень самому за допомогою `userError`.

Так що ви можете мати обробник, що виглядає дещо подібно до наступного:

[source,scala]
----
handler :: IOError -> IO ()  
handler e  
    | isDoesNotExistError e = putStrLn "The file doesn't exist!"  
    | isFullError e = freeSomeSpace  
    | isIllegalOperation e = notifyCops  
    | otherwise = ioError e  
----

де `notifyCops` та `freeSomeSpace` є деякі визначені вами I/O дії. Переконайтесь, що перезакинули виключення, якщо воно не співпало з жодною з ваших критерій, інакше ви спричините мовчазний крах вашого застосування в деяких випадках, де це не мало б статись.

`System.IO.Error` також експортує функції, що дозволяють опитувати наші виключення щодо деяких атрибутів, як що за вказівник файлу, який спричинив помилку, або яке ім'я файлу. Вони починаються на `ioe`, і ви можете побачити їх повний список в документації. Скажімо ми бажаємо надрукувати ім'я файлу, що спричинив нашу помилку. Ми не можемо надрукувати `fileName`, яке ми отримали від `getArgs`, оскільки в обробник надійшло тільки `IOError`, і обробник не знає більше нічого іншого. Функція залежить тільки від параметрів, з якими вона викликана. Ось чому ми можемо використати фуункцію `ioeGetFileName`, що має тип `ioeGetFileName :: IOError -> Maybe FilePath`. Вона приймає `IOError` як параметр і можливо повертає `FilePath` (що є тільки синонімом типу для `String`, пам'ятаємо, так що це різновид тієї самої речі). Те що вона в основному робить, це виділяє шлях файлу з `IOError`, якщо зможе. Давайте модифікуємо нашу програму для роздрукування нашого шляху файла, відповідального за виникле виключення.

[source,scala]
----
import System.Environment     
import System.IO     
import System.IO.Error     
    
main = toTry `catch` handler     
                 
toTry :: IO ()     
toTry = do (fileName:_) <- getArgs     
           contents <- readFile fileName     
           putStrLn $ "The file has " ++ show (length (lines contents)) ++ " lines!"     
    
handler :: IOError -> IO ()     
handler e     
    | isDoesNotExistError e =   
        case ioeGetFileName e of Just path -> putStrLn $ "Whoops! File does not exist at: " ++ path  
                                 Nothing -> putStrLn "Whoops! File does not exist at unknown location!"  
    | otherwise = ioError e     
----

В охоронці, коли `isDoesNotExistError` є `True`, ми використали  `case` вираз для виклику `ioeGetFileName` з `e`, та потім порівняли шаблон щодо значення `Maybe`, яке він повертає. Застосування виразів `case` загально використовується коли ви бажаєте порівняти шаблон з дечим, не привносячи нову функцію.

Ви не маєте використовувати один обробник для перехоплення виключень у всій вашій частині I/O. Ви можете просто охопити певну частину вашого I/O коду в `catch`, або ви можете охопити деякі з ни в `catch`, та використовувати різні обробники для них, десь так:

[source,scala]
----
main = do toTry `catch` handler1  
          thenTryThis `catch` handler2  
          launchRockets  
----

Тут `toTry` використовує `handler1` як обробник, та `thenTryThis` використовує `handler2`. `launchRockets` не є параметром для `catch`,так що якщо воно може закинути виключення, це має занапастити нашу програму, хіба що `launchRockets` використовує перехоплення внутрішньо для обробки своїх власних виключень. Звичайно, `toTry`, `thenTryThis` та `launchRockets` є I/O дії що мають бути склеєні разом з використанням синтаксису `do`, і гіпотетично визначені деінде в іншому місці. Це дещо подібно до блоків `try-catch` в інших мовах, де ви можете оточити всю вашу програму в єдиний `try-catch`, або ви можете використати більш поміркований підхід, та використовувати різні блоки в різних частинах вашого коду, щоб контролювати, який тип обробки помилок відбувається.

Тепер ви знаєте, як мати справи з I/O виключеннями! Закидання виключень з чистого коду та справи з ними тут не були розглянуті, здебільшого через те, що, як ми казали, Haskell пропонує значно кращий спосіб вказувати на помилки, ніж звертатись до I/O для їх відловлення. Навіть при склеюванні разом I/O дій що можуть схибити, я схиляюсь мати їх тип дещо на кшталт `IO (Either a b)`, що означає, що вони є нормальними I/O діями, але результат, що вони видають після виконання, матиме тип `Either a b`, що означає або `Left a`, або `Right b`.