include::header.adoc[]

== Ввод та вивод

image:images/dognap.png[]
Ми вже казали, що Haskell є чисто функціональною мовою. В той час, коли в імперативних мовах ви зазвичай отримуєте речі надаючи копм'ютеру серії кроків для виконання, функціональне програмування більше щодо визначення того, чим є речі. В Haskell функція не може змінити деякий стан, як змінити вміст змінної (коли функція змінює стан ми кажемо, що функція має побічні ефекти). Єдина річ, яку може робити функція в Haskell, це дати нам деякий результат на основі параметрів, які ми надаємо їй. Якщо функція викликається два рази з тими самими параметрами, вона має повертати той самий результат. Хоча це може виглядати дещо обмежувальним, якщо ви прийшли з імперативного світу, ми бачили, що насправді це круто. В імперативній мові ви не маєте гарантії, що проста функція, що має лише пожонглювати деякими числами, не спалить вашу хату, не викраде вашого пса і не пошкрябає вашу машину помідором, при маніпуляції ціма числами. Наприклад, коли ви шукаєте в двійниковому дереві, ми не вставляли елемент в дерево, модифікуючи дерево на місці. Наша функція для вставки в двійникове дерево насправді повертає нове дерево, бо воно не може змінити старе дерево.

Хоча функції, що не можуть змінювати стан, є гарним, бо дозволяє нам промислювати наші програми, з цім є одна проблема. Якщо функція не може зробити дещо в світі, як вона зможе повідомити нам, що вона нарахувала? Щоб сказати нам, що обчислено, вона має змінити стан вихідного пристрою (звичайно стан екрану), що потім виділяє фотони, які подорожують до нашого мозку, та змінюють стан наших думок, чоловіче.

Не втрачайте надію, не все втрачено. З'ясовується, що насправді Haskell має дійсно розумну систему для справ з функціями, що мають побічні ефекти, яка мило відокремлює частину програми, що є чистою, та частину що є нечистою і робить всю брудну роботу, як розмови з клавіатурою та екраном. Маючи ці дві частини розділеними ми все ще можемо промислювати нашу чисту програму і отримувати переваги від всіх речей, що надає чистота, як лінивість, надійність та модульність, при цьому ефективно комунікуючи з зовнішним світом.

=== Hello, world!

image:images/helloworld.png[]
До цього часу ми завжди завантажували наші функції в GHCI для їх тестування та забавок з ними. Ми також дослідили функції стандартної бібліотеки, проміж справ. Але тепер, після восьми або десь так глав, ми нарешті збираємось написати нашу першу справжню програму Haskell! Вау! І будьте впевнені, що ми збираємось зробити стару добру штуку "hello, world".

Хей! Для цілей цієї глави я буду вважати, що ми використовуємо  unix-подібне середовище для вивчення Haskell. Якщо ви в Windows, я б порадив завантажити Cygwin, що є Linux-подібне оточення для Windows, тобто те що нам треба.

І так, для початку, забийте наступне в вашому улюбленому текстовому редакторі:

[source,haskell]
----
main = putStrLn "hello, world"  
----

Ми тільки що визначили ім'я `main`, і в ньому ми викликаємо функцію з назвою `putStrLn` з параметром `"hello, world"`. Виглядає як робота млина, але ні, ми побачимо це через мить. Збережіть це в файлі `helloworld.hs`.

Але зараз ми збираємось зробити дещо, чого не робили досі. Насправді ми збираємось скомпілювати нашу програму! Я в захопленні! Відкрийте ваш термінал і перейдіть в каталог, де розміщений `helloworld.hs`, та виконайте таке:

[source,bash]
----
$ ghc --make helloworld  
[1 of 1] Compiling Main             ( helloworld.hs, helloworld.o )  
Linking helloworld ...  
----

Okay! With any luck, you got something like this and now you can run your program by doing ./helloworld.

[source,bash]
----
$ ./helloworld  
hello, world  
----

І ось де ми є, наша перша скомпільована програма, що друкує дещо на терміналі. Неймовірно нудно!

Давайте перевіримо, що ми написали. Спочатку давайте подивимось на тип функції `putStrLn`.

[source,haskell]
----
ghci> :t putStrLn  
putStrLn :: String -> IO ()  
ghci> :t putStrLn "hello, world"  
putStrLn "hello, world" :: IO ()  
----

Ми можемо прочитати тип `putStrLn` таким чином: `putStrLn` бере рядок та повертає I/O дію, що має тип результата `()` (тобто порожній тапл, також відомий як юніт). I/O дія є дещо, що коли виконується буде нести дію з побічним ефектом (це зазвичай або читання з входу або друкування дечого на екрані), і буде також містити деякий різновид значення повернення в собі. Друкування рядка на термінал насправді не має жодного осмисленого типу повернення, так що використовується фіктивне значення `()`.

Порожній типл є значенням `()`, і він має також тип `()`.

То коли буде виконана I/O дія? Гаразд, ось де з'являється `main`. I/O дія буде виконана коли ми дамо їй ім'я `main`, і потім виконаємо програму.

Маючи всю вашу програму тільки в вигляді однієї I/O дії виглядає дещо обмежувальним. Ось чому ми можемо використовувати синтаксис `do` для зклеювання разом декількох I/O дій в одну. Погляньте на наступний приклад:

[source,haskell]
----
main = do  
    putStrLn "Hello, what's your name?"  
    name <- getLine  
    putStrLn ("Hey " ++ name ++ ", you rock!")  
----

А, цікаво, новий синтаксис! І це читається майже так само як імперативна програма. Якщо ви скомпілюєте це і спробуєте в дії, це буде поводитись так само, як ми цього очікуємо. Зауважте, що ми тільки но сказали `do` і потім поклали серію кроків, як би ми зробили в імперативній програмі. Кожний з ціх кроків є I/O дією. Збираючи їх докупи за допомогою синтаксису `do`, ми зліпили їх в одну I/O дію. Дія, яку ми отримали, має тип `IO ()`, оскільки це тип останньої I/O дії всередині.

Через це `main` завжди має сигнатуру типу `main ::IO something`, де `something` є деякий конкретний тип. За домовленістю ми звичайно не вказуємо декларацію типу для `main`.

Цікава річ, з якою ми досі не зустрічались, це третій рядок, що вказує `name <- getLine`. Виглядає що воно читає рядок з входу, та зберігає його в змінній на ім'я `name`. Чи так насправді? Гаразд, давайте перевіримо тип `getLine`.

[source,haskell]
----
ghci> :t getLine  
getLine :: IO String  
----

image:images/luggage.png[]
Так, це є. `getLine` є I/O дією, що містить тип результату `String`. Це має сенс, оскільки це буде очікувати користувача, докі він введе дещо в терміналі, і потім це дещо буде представлене як рядок. То що відбувається в `name <- getLine`? Ви можете читати цей шматок коду таким чином: виконати I/O дію `getLine`, та потім прив'язати значення результату до `name`. `getLine` має тип `IO String`, так що `name` буде мати тип `String`. Ви можете думати про I/O дію як бокс на маленьких ніжках, що виходитиме в реальний світ і робитиме там дещо (як малювати графіті на стінах), або приносити назад деякі дані. Коли ці дані були підтягрутя до вас, єдиним способом відкрити бокс і отримати дані всередині є використання конструкції `<-`. І коли ми беремо дані з I/O дії, ми можемо дістати їх лише коли ми всередині іншої I/O. Ось як діє Haskell для гарного поділу кода на чисту і нечисту частину. `getLine` в деякому сенсі є нечистим, бо його значення результату не буде гарантовано тим самим, якщо його виконати двічі. Ось чому він дещо заплямований конструктором типу IO, і ми можемо отримати ці дані лише від I/O коду. І оскільки I/O код також заплямований, любі обчислення, що залежать від заплямованих I/O даних, будуть мати заплямований результат.

Коли я кажу заплямований, я не маю на увазі це в такий спосіб, що ми ніколи не зможемо використовувати результат в I/O дії, будь-коли в чистому коді. Ні, ми тимчасово роз-плямовуємо дані в I/O дії, коли ми прикріплюємо її до `name`. Коли ми робимо `name <- getLine`, `name` є просто звичайний рядок, оскільки він представляє те, що знаходиться в боксі. Ми можемо мати дійсно складну функцію, що скажімо бере ваше ім'я (звичайний рядок) як параметр, і каже вам всю вашу вдачу і майбутнє всього вашого життя, базуючись на вашому імені. Ми можемо зробити це:

[source,haskell]
----
main = do  
    putStrLn "Hello, what's your name?"  
    name <- getLine  
    putStrLn $ "Read this carefully, because this is your future: " ++ tellFortune name  
----

і `tellFortune` (або жодна функція, куди вона передає `name`) не має знати нічого про I/O, це просто нормальна функція `String -> String`!

Подивіться на цей шматок коду, чи він валідний?

[source,haskell]
----
nameTag = "Hello, my name is " ++ getLine  
----

Якщо ви відповіли ні - пійдіть з'їжте періжок. Якщо ви сказали так - випийте ковшик розпеченої лави. Я пошуткував, не робіть цього! Причина, з якої це не робить, в тому, що `++` потребує обоє свої параметри як спискаи того самого типу. Лівий параметр має тип `String` (або `[Char]` якщо бажаєте), тоді як `getLine` має тип `IO String`. Ви не можете приєднати рядок та I/O дію. Спочатку ми маємо отримати результат від I/O дії, щоб отримати тип `String`, і це єдиний спосіб зробити це в тому, щоб сказати щось як `name <- getLine` всередині іншої I/O дії. Якщо ми бажаємо мати справу з нечистими даними, ми маємо робити це в нечистому оточенні. Так що прокляття нечистості розповсюджується навкруги як бич нечесті, так що в наших кращих інтересах утримувати I/O частини коду такими малими, як це можливо.

Кожна I/O дія, що виконується, має заточений в собі результат. Ось чому наш попередній приклад програми може бути записаний таким чином:

[source,haskell]
----
main = do  
    foo <- putStrLn "Hello, what's your name?"  
    name <- getLine  
    putStrLn ("Hey " ++ name ++ ", you rock!")  
----

Однак `foo` буде мати лише значення `()`, так що робити таке буде лише  різновидом балачок. Зауважте, що ми не прив'язали останній `putStrLn` до будь-чого. Це через те, що в блоці `do` остання дія не може бути прив'язана до імені, як це було з першими двома. Ми побачимо чому це так трохи пізніше, коли ми поринемо в світ монад. Зараз ви можете думати про це в спосіб, що блок `do` автоматично виділяє значення з останньої дії, і прикріпляє його до свого власного результату.

За винятком цього останнього рядка, кожний рядок, що не прикріплений, може бути записаний як прикріплення. Так що `putStrLn "BLAH"` може бути записане як `_ <- putStrLn "BLAH"`. Але це не має користі, так що ми не будемо використовувати `<-` для I/O дій, що не мають корисних результатів, як `putStrLn something`.

Початківці іноді думають, що роблячи

[source,haskell]
----
name = getLine  
----

буде читати з входу, та потім прикріпляти значення до `name`. Гаразд, так не буде, все що тут робиться це надання дії `getLine` іншого імені, а саме `name`. Пам'ятайте, щоб отримати значення від I/O дії, ми маємо виконати її в середині іншої I/O дії, через прикріплення її до імені за допомогою `<-`.

I/O дії будуть виконані лише тоді, якщо їм надано ім'я `main`, або коли вони всередині більшої I/O дії, що компонується за допомогою блоку `do`. Ми також можемо використати блок `do` для склеювання разом декількох I/O дій, і потім ми можемо використовувати цю I/O дію в іншому `do` блоці, і так далі. З іншого боку, всі вони будуть виконані тільки якщо вони з часом дійдуть до `main`.

О, гаразд, існує ще один випадок, коли будуть виконані I/O дії. Коли ми набираємо I/O дію в GHCI та натискаємо `return`, це також буде виконане.

[source,haskell]
----
ghci> putStrLn "HEEY"  
HEEY  
----

Хоч коли ми вводимо число або викликаємо функцію в GHCI і натискаємо `return`, це буде обчислене (здебільшого як це нам і треба), і потім викликає `show` на цьому, і потім це друкує рядок на терміналі, неявно викликаючи `putStrLn`.

Пам'ятаєте прикріплення `let`? Якщо ні, оновіть вашу пам'ять щодо них в цьому розділі. Вони мають бути в формі `let` прикріплень в виразах, де прикріалення є іменами що будуть надані виразам, та вираз є вираз, що буде обчислений коли хтось бачить його. Ми також бачили його в осяжностях списків, де частина `in` непотрібна. Гаразд, ви можете використовувати їх в `do` блоках, майже так само, як ми використовуємо їх в осяжностях списків. Перевірте це:

[source,haskell]
----
import Data.Char  
  
main = do  
    putStrLn "What's your first name?"  
    firstName <- getLine  
    putStrLn "What's your last name?"  
    lastName <- getLine  
    let bigFirstName = map toUpper firstName  
        bigLastName = map toUpper lastName  
    putStrLn $ "hey " ++ bigFirstName ++ " " ++ bigLastName ++ ", how are you?"  
----

Бачите, як підрівняні I/O дії в блоці `do`? Також зауважте, як `let` вирівняне разом з I/O діями і іменами `let`, вирівняними один з одним? Це гарна практика, бо відступи важливі в Haskell. Тепер: ми зробили `toUpper firstName`, що перетворює якесь `"John"` в значно крутіший рядок як `"JOHN"`. Ми прикріплюємо цей збільшений рядок до `name`, та потім використовуємо його в рядку нижче, коли друкуємо на термінал.

Ви можете поцікавитись, коли використавувати `<-`, та коли використовувати прикріплення `let`? Гаразд, пам'ятайте, що `<-` є (поки що) для виконання I/O дій та прикріплення їх результатів до імен. Однак `map toUpper firstName` не є I/O дією. Це чистий вираз в Haskell. Так що використовуйте `<-`, коли ви бажаєте прикріпити результати I/O дій до імен, і ви можете використовувати `let` для прикріалень чистих виразів до імен. Так що коли ми робимо дещо як `let firstName = getLine`, ми отримаємо лише нове ім'я для I/O дії `getLine`, і ми все ще маємо запустити її через `<-`, щоб вона виконалась.

Тепер ми збираємось зробити програму, що постійно читає рядок і друкує той самий рядок зі словами в зворотньому порядку. Виконання програми буде припинено, коли ми введемо порожній рядок. Ось програма:

[source,haskell]
----
main = do   
    line <- getLine  
    if null line  
        then return ()  
        else do  
            putStrLn $ reverseWords line  
            main  
  
reverseWords :: String -> String  
reverseWords = unwords . map reverse . words  
----

Щоб отримати відчуття того, що вона робить, ви можете виконати її перед тим, як ми пройдемось по коду.

Protip: Щоб виконати програму ви можете або скомпілювати її, та потім виконати отримий виконавчий файл, роблячи `ghc --make helloworld` та потім `./helloworld`, або ви можете використовувати команду `runhaskell` ось так: `runhaskell helloworld.hs` і ваша програма буде виконана на льоту.

Спочатку давайте поглянемо на функцію `reverseWords`. Це тільки звичайна функція, що приймає рядок як `"hey there man"`, та потім викликає `words` для продукування списку слів як `["hey","there","man"]`. Потім ми `map reverse` на списку, отримуючи `["yeh","ereht","nam"]`, і потім ми знову покладаємо все це в один рядок, використовуючи `unwords`, і фінальний результат буде `"yeh ereht nam"`. Дивітья, як ми використовували композицію функцій. Без композиції функцій ми б мали написати щось як `reverseWords st = unwords (map reverse (words st))`.

Що відносно `main`? Спершу ми отримуємо рядок з термінала, виконуючи виклик `getLine` що повертається в `line`. І тепер ми маємо условний вираз. Пам'ятайте, що в Haskell кожний `if` повинен мати відповідний `else`, оскільки кожний вираз повинен мати деякий різновид значення. Ми робимо `if`, так що коли умова `true` (в нашому випадку введений нами рядок порожній), ми виконуємо одну I/O дію, та коли ні - буде виконана інша I/O дія для `else`. Ось чому в блоці `I/O do`, `if` повинні мати форму `if умова then I/O дія else I/O дія`.

Давайте спочатку поглянемо на те, що відбувається в частині `else`. Оскільки ми повинні мати саме одну I/O дію після `else`, ми використовуємо блок `do` для склеювання разом двох I/O дій в одну. Ви також можете записати цю частину таким чином:

[source,haskell]
----
else (do  
    putStrLn $ reverseWords line  
    main)  
----

Це робить більш наочним, що блок `do` має розгядатись як одна I/O дія, але це огидніше. В любому разі в блоці `do` ми викликаємо  `reverseWords` на рядку, що ми отрумуємо від `getLine`, та потім друкуємо це на терміналі. Після цього ми просто виконуємо `main`. Вона виключається рекурсивно і це гаразд, оскільки `main` сама є I/O дією. Так що в деякому сенсі ми повертаємось на початок програми.

Тепер, що трапляється коли `null line` стає `true`? Це те, що в данному випадку виконується після `then`. Якщо ми подивимось, ми побачимо що виконується `then return ()`. Якщо ви візьмете імперативні мови, як C, Java або Python, ви можливо думаєте, що знаєте що робить цей `return`, так що є шанси, що ви перестрибнули цей доволі довгий параграф. Гаразд, ось як вам таке: `return` в Haskell насправді нічого подібного до `return` в більшості мов! Воно має те саме ім'я, що бентежить багато людей, але в реальності це досить інше. В імперативних мовах `return` звичайно закінчує виконання метода або процедури, і повідомляє про це деяким разновидом значення до викликаючої сторони. В Haskell (точніше в I/O діях), це виробляє I/O дію з чистого значення. Якщо ви подумаєте про аналогію бокса з попередніх розділів, це приймає значення і огортає його в бокс. Отримана I/O дія насправді нічого не робить, це просто це значення, оточене як його результат. Так що в контексті I/O `return "haha"` буде мати тип `IO String`. Який сенс в простому перетворенні чистого значення в I/O дію, що нічого не робить? Навіщо заплямовувати нашу програму в IO більше, ніж це мало би бути? Гаразд, нам треба деяка I/O дія, що треба видати в випадку порожнього вхідного рядка. Ось чому ми просто створили фіктивну I/O дію, дію що нічого не робить, написавши `return ()`.

Використання `return` не спричиняє закінчення блоку `I/O do` в плані виконання, або будь чого подібного. Наприклад, ця програма буде досить щасливо продовжуватись весь час до останнього рядка:

[source,haskell]
----
main = do  
    return ()  
    return "HAHAHA"  
    line <- getLine  
    return "BLAH BLAH BLAH"  
    return 4  
    putStrLn line  
----

Всі ці `return` роблять те, що вони створюють I/O дії, що насправді нічого не роблять, крім інкапсуляції результату, і цей результат відкидається геть, оскільки він не прив'язаний до імені. Ми можемо використовувати `return` в комбінації з `<-` для прив'язки речей до імен.

[source,haskell]
----
main = do  
    a <- return "hell"  
    b <- return "yeah!"  
    putStrLn $ a ++ " " ++ b  
----

Як ви бачите, `return` є різновидом протилежності до `<-`. Тоді як  `return` бере значення і огортає його в бокс, `<-` бере бокс (та виконує його), та отримує з нього значення, прикріплюючи його до імені. Але робити це є дещо надлишковим, особливо коли ви можете використовувати `let` прикріпленнях в `do` блоках до імен, десь так:

[source,haskell]
----
main = do  
    let a = "hell"  
        b = "yeah"  
    putStrLn $ a ++ " " ++ b  
----

Коли маємо справу з блоками `I/O do`, ми в основному використовуємо `return` оба тому що нам треба створити I/O дію, що не робить нічого, або якщо ми не бажаємо, щоб I/O дія, створена з `do` блока, мала значення результата в своїй останній дії, але ми бажаємо, щоб він мав інше значення результата, так що ми використовуємо `return` для створення I/O дії, що завжди має заточеним наш бажаний результат, і ми покладаємо його в кінці.

Блок `do` також може мати лише одну I/O дію. В цьому випадку це те саме, що просто записати I/O дію. Деякі люди можуть обрати запис `then do return ()` в цьому випадку, оскільки `else` також має `do`.

Оскільки ми просуваємось до файлів, давайте поглянемо на деякі функції, що будуть корисні при справах з I/O.

`putStr` здебільшого як `putStrLn` в тому, що він бере рядок як параметр і повертає I/O дію, що буде друкувати цей рядок на терміналі, тільки `putStr` не перестрибує на новий рядок після друкування рядка, тоді як `putStrLn` робить це.

[source,haskell]
----
main = do   putStr "Hey, "  
            putStr "I'm "  
            putStrLn "Andy!"   
$ runhaskell putstr_test.hs  
Hey, I'm Andy!  
----

Її сигнатура типу є `putStr :: String -> IO () `, так що результат заточений в результуючій I/O дії є юніт. Дурне значення, так що немає сенсу прикріплювати його.

`putChar` бере символ та повертає I/O дію, що буде роздруковувати його на термінал.

[source,haskell]
----
main = do   putChar 't'  
            putChar 'e'  
            putChar 'h'  
$ runhaskell putchar_test.hs  
teh  
----

`putStr` насправді визначена рекурсивно за допомогою `putChar`. Гранична умова `putStr` є порожній рядок, так що якщо ми друкуємо порожній рядок, просто повертаємо I/O дію, що нічого не робить, використовуючи `return ()`. Якщо він не порожній, тоді друкуємо перший символ рядка, роблячи `putChar`, і потім друкуємо всі інші з використанням `putStr`.

[source,haskell]
----
putStr :: String -> IO ()  
putStr [] = return ()  
putStr (x:xs) = do  
    putChar x  
    putStr xs  
----

Дивіться, як ми можемо використовувати рекурсію в I/O, так само як ми можемо робити це в чистому коді. Так само як в чистому коді, ми визначаємо граничний випадок, і потім думаємо, чим насправді є результат. Це дія, що спочатку виводить перший символ, і потім виводить залишок рядка.

`print` бере значення любого типу, що є примірником `Show` (що означає, що ми знаємо як представляти його як рядок), викликає `show` з цім значенням для перетворення на рядок, і потім виводить цей рядок на термінал. В основному це просто `putStrLn . show`. Це спочатку виконує `show` на значенні, і потім подає його до `putStrLn`, що повертає I/O дію, що буде роздруковувати наше значення.

[source,haskell]
----
main = do   print True  
            print 2  
            print "haha"  
            print 3.2  
            print [3,4,3]  
$ runhaskell print_test.hs  
True  
2  
"haha"  
3.2  
[3,4,3]  
----

Як ви можете бачити, це дуже зручна функція. Згадайте, як ми казали про те, як I/O дії виконуються тільки коли вони підпадають до `main`, або коли ми намагаємось обчислити їх в підказці GHCI. Коли ми друкуємо значення (як `3` або `[1,2,3]`), та натискаємо клавішу `return`, GHCI насправді використовує `print` на цьому значенні для відображення його на терміналі!

[source,haskell]
----
ghci> 3  
3  
ghci> print 3  
3  
ghci> map (++"!") ["hey","ho","woo"]  
["hey!","ho!","woo!"]  
ghci> print (map (++"!") ["hey","ho","woo"])  
["hey!","ho!","woo!"]  
----

Коли ми бажаємо роздрукувати рядки, зазвичай ми використовуємо `putStrLn`, оскільки ми не бажаємо лапки навкруги, але для друку наших значень інших типів на термінал здебільшого використовується `print`.

`getChar` є I/O дією, що читає символ зі входу. Таким чином її сигнатура типу є `getChar ::IO Char`, оскільки результат, що міститься в I/O дії є `Char`. Зауважте, що через буферізацію читання символів насправді не буде відбуватись, доки користувач не начавить на клавішу вводу.

[source,haskell]
----
main = do     
    c <- getChar  
    if c /= ' '  
        then do  
            putChar c  
            main  
        else return ()  
----

Ця програма виглядає так, що вона повинна читати символ, та перевіряти чи це проміжок. Якщо це так, виконання припиняється, та якщо ні - друкуємо його на терміналі, і потім робимо те саме спочатку і так далі. Гаразд, це робить щось подібне, тільки не в спосіб як ви можете це очікувати. Перевірте це:

[source,haskell]
----
$ runhaskell getchar_test.hs  
hello sir  
hello  
----

Другий рядок є ввод. Ми вводимо `hello sir` і потім натискаємо ввод. Через буферізацію виконання програми буде починатись тільки після того, як ми натиснемо `return`, і не після кожного введеного символу. Але коли ми натиснемо ввод, це діє на тому, що було введено до цього. Спробуйте погратись з цією програмою щоб отримати відчуття як вона робить!

Функцію `when` можна знайти в `Control.Monad` (щоб отримати до неї доступ зробіть `import Control.Monad`). Це цікаво, бо в блоці `do` це виглядає як твердження керування виконанням, але насправді це звичайна функція. Вона приймає логічне значення та I/O дію. Якщо це логічне значення є `True`, це повертає ту саму I/O дію, що ми надали туди. Однак якщо це `False`, вона повертає `return ()`, дію, так що  I/O дія нічого не робить. Ось як ми можемо переписати попередній шмат коду, де ми демонструємо `getChar` через використання `when`:

[source,haskell]
----
import Control.Monad   
  
main = do  
    c <- getChar  
    when (c /= ' ') $ do  
        putChar c  
        main  
----

Так що, як ви можете бачити, це корисно для інкапсуляції шаблона `if something then do some I/O action else return ()`.

`sequence` бере список I/O дій і повертає I/O дії, що будуть виконувати ці дії, одна за одною. Результат, що міститься в цій I/O дії. буде списком результатів всіх I/O дій, що виконуються. Його сигнатура типу є `sequence ::[IO a] -> IO [a]`. Роблячи це:

[source,haskell]
----
main = do  
    a <- getLine  
    b <- getLine  
    c <- getLine  
    print [a,b,c]  
----

є те саме, що і наступне:

[source,haskell]
----
main = do  
    rs <- sequence [getLine, getLine, getLine]  
    print rs  
----

Так що `sequence [getLine, getLine, getLine]` створює I/O дію, що буде виконувати `getLine` три рази. Якщо ви прикріпите цю дію до імені, результатом є список з усіх результатів, так що в нашому випадку це список з трьох речей, які користувач вводить на підказку консолі.

Загальний шаблон з `sequence` це коли ми відображуємо функції як  `print` або `putStrLn` на списки. Виконання `map print [1,2,3,4]` не створить I/O дію. Це створить сипсок I/O дій, оскільки це як написати `[print 1, print 2, print 3, print 4]`. Якщо ми бажаємо трансформувати цей список I/O дій в одну I/O дію, ми маємо створити послідовність.

[source,haskell]
----
ghci> sequence (map print [1,2,3,4,5])  
1  
2  
3  
4  
5  
[(),(),(),(),()]  
----

Що це за `[(),(),(),(),()]` в самому кінці? Гаразд, коли ми обчислюємо I/O дію в GHCI, вона виконується і потім друкується його результат, якщо результат не `()`, в якому випадку нічого не друкується. Ось чому обчислення `putStrLn "hehe"` в GHCI просто друкує `hehe` (оскільки результат що міститься в результаті `putStrLn "hehe"` в `()`). Але коли ми робимо `getLine` в GHCI, результат цієї I/O дії друкується, оскільки `getLine` має тип `IO String`.

Оскільки відображення функції, що повертає I/O дію до списку, та потім створення послідовності по цьому, є таким загальним, що були введені функції `mapM` та `mapM_`. `mapM` бере функцію та список, відображує функцію на списку, та потім створює послідовність. `mapM_` робить те саме, тільки пізніше він закидає результат. Ми звичайно використовуємо `mapM_`, коли нам байдуже, який результат має наша послідовність I/O дій.

[source,haskell]
----
ghci> mapM print [1,2,3]  
1  
2  
3  
[(),(),()]  
ghci> mapM_ print [1,2,3]  
1  
2  
3  
----

`forever` бере I/O дію і повертає I/O дію, що просто повторює отриману I/O дію постійно. Вона розташована в `Control.Monad`. Ця невелика програма буде безкінечно запитувати користувача щодо деякого вводу, і віддавати його назад, ВЕЛИКИМИ ЛІТЕРАМИ:

[source,haskell]
----
import Control.Monad  
import Data.Char  
  
main = forever $ do  
    putStr "Give me some input: "  
    l <- getLine  
    putStrLn $ map toUpper l  
----

`forM` (розташована в `Control.Monad`) як `mapM`, тільки з переміненими параметрами. Перший параметр є списком, та другий є функція, по якій треба відображувати цей список, що потім перетворюється на послідовність. Чому це корисне? Гаразд, з деяким креативним застосуванням лямбди та `do` нотації ми можемо робити такі речі:

[source,haskell]
----
import Control.Monad  
  
main = do   
    colors <- forM [1,2,3,4] (\a -> do  
        putStrLn $ "Which color do you associate with the number " ++ show a ++ "?"  
        color <- getLine  
        return color)  
    putStrLn "The colors that you associate with 1, 2, 3 and 4 are: "  
    mapM putStrLn colors  
----

`(\a -> do ... )` є функцією, що бере число і повертає I/O дію. Ми маємо оточити її в дужки, інакше лямбда подумає, що останні дві I/O дії належать до неї. Зауважте, що ми робимо `return color` всередині `do` блока. Ми робимо так, щоб I/O дія, яку визначає `do` блок, мала б результат кольору, що міститься в ній. Ми насправді не маємо робити це, оскільки `getLine` вже має те, що міститься в ній. Роблячи `color <- getLine` та потім `return color` є проста розпаковка результату від `getLine`, та потім знову його розпаковка, так що це як просто зробити `getLine`. `forM` (викликана з її двома параметрами) продукує I/O дію, результат якої ми прикріплюємо до `colors`. `colors` є лише звичайний список, що містить рядки. В кінці ми друкуємо всі ці кольори, роблячи `mapM putStrLn colors`.

Ви можете думати про `forM` в такому сенсі: створити I/O для кожного елементу в цьому списку. Що кожна I/O дія буде робити залежить від елемента, що була використана для створення дії. Нарешті виконаємо ці дії та прикріпимо результат до чогось. Ми не маємо прикріплювати будь-що, так що ми можемо також відкинути це геть.

[source,haskell]
----
$ runhaskell form_test.hs  
Which color do you associate with the number 1?  
white  
Which color do you associate with the number 2?  
blue  
Which color do you associate with the number 3?  
red  
Which color do you associate with the number 4?  
orange  
The colors that you associate with 1, 2, 3 and 4 are:  
white  
blue  
red  
orange  
----

Насправді ми можемо зробити це без `forM`, але з `forM` це більш читабельне. Звичайно ми пишемо `forM` коли ми бажаємо зробити `map` та `sequence` деяких дій, що ми визначили тут на місці за допомогою `do` нотації. В тому ж ключі ми можемо замінити останній рядок на `forM colors putStrLn`.

В цьому розділі ми навчились основам вводу та виводу. Ми також з'ясували що таке I/O дії, як вони дозволяють нам вводити та виводити рядки, та коли вони насправді виконуються. Для повтору: I/O дії є значення, здебільшого як любі інші значення в Haskell. Ми можемо передавати їх як параметри до функцій, та функції можуть повертати I/O дії як результати. Що особливе щодо них, це якщо вони підпадають в функцію `main` (або є результат в рядку GHCI), вони виконуються. І ось тоді вони починають писати дещо на вашому екрані, або грають Yakety Sax в ваших гучномовцях. Кожна I/O дія також може інкапсулювати результат з тим, що ви бажаєте отримати від зовнішнього світу.

Не думайте про функцію як `putStrLn` як про функцію, що бере рядок і друкує його на екрані. Думайте про це, як про функцію, що бере рядок та повертає I/O дію. Ця I/O дія буде, коли виконана, друкувати прекрасні вірші на вашому терміналі.

=== Файли та потоки

image:images/streams.png[]
`getChar` є I/O дія, що читає поодинокий символ з терміналу. `getLine` є I/O дія, що читає рядок з терміналу. Вони обоє досить прямолінійні, і більшість мов програмування мають деякі функції або твердження, що відповідають до них. Але тепер давайте познайомимось з `getContents`. `getContents` є I/O дія, що читає все зі стандартного вводу, доки не зустріне символ кінця файлу. Її тип `getContents ::IO String`. Що круте щодо `getContents`, це те що вона робить ліниве I/O. Коли ми робимо `foo <- getContents`, вона не читає все і одразу, зберігає в пам'яті і потім прикріплює до `foo`. Ні, вона лінива! Вона каже: "Так так, я прочитаю вхідні дані з терміналу пізніше як ми дійдемо до цього, коли нам це дійсно знадобиться!".

`getContents` є дійсно корисним, коли ми перенаправляємо вихід однієї програми на вхід нашої програми. В випадку, якщо ви не знаєте, як робить перенаправлення в юнікс системах, ось швидке пояснення. Давайте створимо текстовий файл, що містить наступне маленьке хайку:

----
I'm a lil' teapot  
What's with that airplane food, huh?  
It's so small, tasteless   
----

Так, хайку поганеньке, що з ним таке? Якщо хтось знає гарні туторіали по хайку - дайте мені знати.

Тепер згадайте маленьку програму, яку ми писали коли вводили функцію `forever`. Якщо запитати користувача ввести рядок, він повертається ВЕЛИКИМИ ЛІТЕРАМИ, і потім все починається спочатку, без кінця. Тільки ви не маєте прогортати весь час назад, ось те саме ще раз:

[source,haskell]
----
import Control.Monad  
import Data.Char  
  
main = forever $ do  
    putStr "Give me some input: "  
    l <- getLine  
    putStrLn $ map toUpper l  
----

Ми зберіжемо цю програму як `capslocker.hs` або якось інакше, і скомпілюємо її. І потім ми збираємось використати юнікс конвеєр, щоб скормити наш текстовий файл напряму в нашу маленьку програму. Ми збираємось використати допомогу програми GNU `cat`, що роздруковує файл, переданий їй як аргумент. Перевір це!

[source,bash]
----
$ ghc --make capslocker   
[1 of 1] Compiling Main             ( capslocker.hs, capslocker.o )  
Linking capslocker ...  
$ cat haiku.txt  
I'm a lil' teapot  
What's with that airplane food, huh?  
It's so small, tasteless  
$ cat haiku.txt | ./capslocker  
I'M A LIL' TEAPOT  
WHAT'S WITH THAT AIRPLANE FOOD, HUH?  
IT'S SO SMALL, TASTELESS  
capslocker <stdin>: hGetLine: end of file  
----

Як ви можете бачити, перенаправлення виходу однієї програми (в нашому випадку це була `cat`) на вхід іншої (`capslocker`) робиться символом `|`. Те що ми зробили доволі еквівалентне до простого виконання `capslocker`, друкування нашого хайку на терміналі і потім ввод символа кінця файла (це звичайно робиться натисканням `Ctrl-D`). Це як виконати `cat haiku.txt` і сказати: “Зачекай, не друкуй це на термінал, замість цього скажи це `capslocker`!”.

Те, що ми в основному робимо за допомогою `forever`, це прийняти вхід і перетворити його в деякий вихід. Ось чому ми можемо використати `getContents`, щоб зробити нашу програму навіть коротше та краще:

[source,haskell]
----
import Data.Char  
  
main = do  
    contents <- getContents  
    putStr (map toUpper contents)  
----

Ми виконуємо I/O дію `getContents`, і називаємо рядок, який вона продукує, `contents`. Потім ми робимо `map toUpper` по цьому рядку і друкуємо це на термінал. Майте на увазі, що оскільки рядки в основному списки, що є лінивими, і `getContents` є лінивим I/O, вона не буде читати весь вміст одночасно, і зберігати його в пам'яті перед роздруковки збільшенної версії. Скоріше вона буде роздруковувати збільшену версію по мірі читання, оскільки вона буде читати лише один рядок з входу, коли це буде дійсно треба.

[source,bash]
----
$ cat haiku.txt | ./capslocker  
I'M A LIL' TEAPOT  
WHAT'S WITH THAT AIRPLANE FOOD, HUH?  
IT'S SO SMALL, TASTELESS  
----

Круто, це робить. Що коли ми просто виконаємо `capslocker` і спробуємо набрати рядки самотужки?

[source,haskell]
----
$ ./capslocker  
hey ho  
HEY HO  
lets go  
LETS GO  
----

Ми виходимо з цього натискаючи `Ctrl-D`. Доволі мило! Як ви можете бачити, це друкує наш збільшений вхід, рядок за рядком. Коли результат `getContents` прикріплений до `contents`, він не представлений в пам'яті як справжній рядок, але більше як проміс, що з часом спродукує рядок. Коли ми робимо `map toUpper` на `contents`, це також проміс відобразити цю функцію по колись створеному  `contents`. І нарешті коли трапляється `putStr`, він каже попередньому промісу: "Хей, мені треба збільшений рядок!". Це все ще не має рядків, так що він каже до `contents`: "Хей, як щодо дійсно отримати рядок з терміналу?". І вже тоді `getContents` насправді читає з терміналу, і повертає рядок до коду, що запитував його, що спродукувати дещо цікаве. Цей код потім відображує `toUpper` по цьому рядку та видає це до `putStr`, що друкує його. І потім `putStr` каже: "Хей, мені треба наступний рядок, ворушись!", і це повторюється доки не буде більше вводу, що втілюється в вигляді символу кінця файла.

Давайте створимо програму, що бере деякий ввод, і друкує тільки ті рядки, що коротше десяти символів. Дослідимо:

[source,haskell]
----
main = do  
    contents <- getContents  
    putStr (shortLinesOnly contents)  
  
shortLinesOnly :: String -> String  
shortLinesOnly input =   
    let allLines = lines input  
        shortLines = filter (\line -> length line < 10) allLines  
        result = unlines shortLines  
    in  result  
----

Ми створили нашу I/O частину нашої програми такою короткою, як це можливо. Оскільки наша програма передбачає прийняття деякого вводу і друк деякого виводу на основі цього вводу, ми можемо реалізувати її через читання вмісту вводу, виконання функції на ньому, та потім друк того, що повертає функція.

Функція `shortLinesOnly` робить саме так: вона приймає рядок, як `"short\nlooooooooooooooong\nshort again"`. Цей рядок має три рядка, два з них короткі, і середня довга. Вона виконує на цьому рядку функцію `lines`, що конвертує її на `["short", "looooooooooooooong", "short again"]`, що потім прикріплюється до імені `allLines`. Цей список рядків потім фільтрується, так що в списку залишаються лише рядки коротші десяти символів, продукуючи `["short", "short again"]`. І нарешті `unlines` поєднує список в єдиний рядок, обмежений  `newline`, даючи `"short\nshort again"`. Давайте дамо цьому хід.

----
i'm short  
so am i  
i am a loooooooooong line!!!  
yeah i'm long so what hahahaha!!!!!!  
short line  
loooooooooooooooooooooooooooong  
short  
$ ghc --make shortlinesonly  
[1 of 1] Compiling Main             ( shortlinesonly.hs, shortlinesonly.o )  
Linking shortlinesonly ...  
$ cat shortlines.txt | ./shortlinesonly  
i'm short  
so am i  
short  
----

Ми перенаправляємо вміст `shortlines.txt` на вихід `shortlinesonly`, так що на виході ми отримуємо лише короткі рядки.

Цей шаблон отримання деякого рядка на вході, трансформація його за допомогою функції, та потім вивід його, є настільки загальним, що існує функція, що робить це навіть простішим, з назвою `interact`. `interact` бере функцію типу `String -> String` в якості параметра, та повертає I/O дію, що буде сприймати деякий вхід, виконувати цю функцію на ньому, та потім друкувати результат функції. Давайте модифікуємо нашу програму для її використання.

[source,haskell]
----
main = interact shortLinesOnly  
  
shortLinesOnly :: String -> String  
shortLinesOnly input =   
    let allLines = lines input  
        shortLines = filter (\line -> length line < 10) allLines  
        result = unlines shortLines  
    in  result  
----

Тільки щоб показати, як багато можливо досягти в такому стислому коді (хоча це буде менш читабельне), та щоб продемонструвати здатності ношої функції до композиції, ми збираємось переробити її ще більше.

[source,haskell]
----
main = interact $ unlines . filter ((<10) . length) . lines  
----

Вау, ми насправді скоротили все до одного рядка, що насправді круто!

`interact` може бути використаний для створення програм, до яких пересилаються деякі дані, та потім накопичення результатів, або вона може бути використана для створення програм, що поводиться як отримує рядок від користувача, видаючи деякий результат на основі цього рядка, потім бере інший рядок і так далі. Насправді немає різниці між двома, все залежить тільки від того як користувач збирається використовувати їх.

Давайте створимо програму що постійно читає рядок та потім каже нам, чи рядок є паліндромом або ні. Ми можемо просто використовувати `getLine` для читання рядка, каже користувачу чи це паліндром, і потім виконує `main` ще раз. Але буде простіше, якщо ми використаємо  `interact`. При використанні `interact`, думайте про те, що вам треба трансформувати деякий вхід в бажаний вихід. В нашому випадку ми маємо замінити кожний рядок на вході або на `"palindrome"` або на `"not a palindrome"`. Так що ми пишемо функцію, що трансформує дещо як `"elephant\nABCBA\nwhatever"` в `"not a palindrome\npalindrome\nnot a palindrome"`. Давайте зробимо це!

[source,haskell]
----
respondPalindromes contents = unlines (map (\xs -> if isPalindrome xs then "palindrome" else "not a palindrome") (lines contents))  
    where   isPalindrome xs = xs == reverse xs  
----

Давайте запишемо це без крапок.

[source,haskell]
----
respondPalindromes = unlines . map (\xs -> if isPalindrome xs then "palindrome" else "not a palindrome") . lines  
    where   isPalindrome xs = xs == reverse xs  
----

Досить прямолінійно. Спершу це перетворює дещо накшталт `"elephant\nABCBA\nwhatever"` на `["elephant", "ABCBA", "whatever"]`, та потім відображує цю лямбду по цьому, даючи `["not a palindrome", "palindrome", "not a palindrome"]`, та потім використовує поєднання в єдиний рядок, розділений `newline`. Тепер ми можемо робити

[source,haskell]
----
main = interact respondPalindromes  
----

Давайте протестуємо це:

[source,bash]
----
$ runhaskell palindromes.hs  
hehe  
not a palindrome  
ABCBA  
palindrome  
cookie  
not a palindrome  
----

Навіть якщо ми створили програму, що трансформує один великий рядок в інший, це робить так, якби ми створили програму, що робить це рядок за рядком. Це тому що Haskell лінивий,і він бажає друкувати перший рядок результуючого рядка, але він не може, бо він все ще не має першого рядка на вході. Так що так швидко, як ми отримаємо перший рядок на вході, він друкує перший рядок на виході. Ми отримаємо результат від програми, коли надамо символ кінця рядка.

Ми також можемо використати цю програму через переправлення їй файлу. Скажімо, ми маємо такий файл:

----
dogaroo  
radar  
rotor  
madam  
----

та ми зберігаємо його як `words.txt`. Ось що ми отримаємо, направивши його в програму:

[source,bash]
----
$ cat words.txt | runhaskell palindromes.hs  
not a palindrome  
palindrome  
palindrome  
palindrome  
----

І знову ми отримали той самий вихід, так якби ми виконали нашу програму та надали слова один за одним в стандартний вхід. Ми просто не бачимо вводу до `palindromes.hs`, оскільки вхід надходить з файлу, а не від того що ми набираємо слова.

Так що тепер ви можливо бачите, як робить ліниве I/O, та як ми можемо використовувати його з вигодою. Ви можете просто думати в термінах того, як має виглядати вихід для деякого нашого вводу, і писати функцію для виконання цієї трансформації. В лінивому I/O нічого не береться з входу, доки це не буде абсолютно потрібне, оскільки те, що ми бажаємо друкувати прямо зараз залежить від цього вводу.

Досі ми робили з I/O, друкуючи речі на терміналі та читаючи з нього. Але як щодо читання та запису до файлів? Гаразд, в деякому сенсі ми вже мали робити це. Один шлях думати про читання з терміналу є уявити, що це як читання з (дещо особливого) файла. Те саме вірне і для запису на терміналЮ це так само як писати в файл. Ми можемо називати ці два файли `stdout` та `stdin`, маючи на увазі стандартний вивід та вхід, відповідно. Маючи це на увазі ми бачимо, що запис та читання з файлів максимально подібне до писання в стандартний вивід і читання зі стандартного вводу.

Ми почнемо з насправді простої програми, що відкриває файл з назвою `girlfriend.txt`, що містить текст хіта Avril Lavigne `Girlfriend`, і просто друкує його на термінал. Ось `girlfriend.txt`:

----
Hey! Hey! You! You!   
I don't like your girlfriend!   
No way! No way!   
I think you need a new one!  
----

І ось наша програма:

[source,haskell]
----
import System.IO  
  
main = do  
    handle <- openFile "girlfriend.txt" ReadMode  
    contents <- hGetContents handle  
    putStr contents  
    hClose handle  
----

Виконуючи це ми отримуємо очікуваний результат:

[source,bash]
----
$ runhaskell girlfriend.hs  
Hey! Hey! You! You!  
I don't like your girlfriend!  
No way! No way!  
I think you need a new one!  
----

Давайте пройдемо рядок за рядком. Перший рядок лише заклики, що привертають нашу увагу. В другому рядку Avril каже нам, що їй не подобається її поточний романтичний партнер. Третій рядок служить для наголосу цього твердження, а четвертий рядок каже, що нам треба шукати нову дівчину.

Тепер пройдемо програму рядок з рядком! Наша програма є декілька I/O дій, склеєних разом за допомогою блоку `do`. В першому рядку блоку `do` ми помічаємо нову функцію на ім'я `openFile`. Ось її сигнатура типу: `openFile ::FilePath -> IOMode -> IO Handle`. ЯКщо ми прочитаємо це вголос, це означає: `openFile` бере шлях до файлу та `IOMode`, і повертає I/O дію, що буде відкривати файл, і буде мати асоційований хендлер інкапсульований як результат.

`FilePath` є просто синонім типу для `String`, визначений просто як:

[source,haskell]
----
type FilePath = String  
----

`IOMode` є визначений таким чином тип:

[source,haskell]
----
data IOMode = ReadMode | WriteMode | AppendMode | ReadWriteMode  
----

image:images/file.png[]
Так само як наш тип, що представляє сім можливих значень для днів тижня, цей тип перелічує представлення того, що ми бажаємо робити з нашим відкритим файлом. Дуже просто. Просто зауважте, що цей тип `IOMode`, а не `IO Mode`. `IO Mode` має бути типом I/O дії, що має значення деякого типу `Mode` в якості результату, але `IOMode` є простий перелік.

Нарешті це повертає I/O дію, що повертає вказаний файл у вказаному режимі. Якщо ми прикріпимо дію до будь-чого, ми отримаємо `Handle`. Значення типу `Handle` представляє де є наш файл. Ми будемо використовувати цей хендлер, так що ми знатимемо з якого файлу треба читати. Це було б дурним читати файл, але не прикріпити це читання до хендлера, бо ми не були б в змозі робити будь-що з файлом. Так що в нашому випадку ми прикріпили хендлер до `handle`.

В наступному рядку ми бачимо функцію з назвою `hGetContents`. Вона приймає `Handle`, так що вона знає, з якого файла треба отримати вміст, та повертає `IO String` — I/O дію, що в якості результата зберігає вміст файлу. Ця функція доволі подібна до `getContents`. Єдина відмінність в тому, що `getContents` буде автоматично читати зі стандартного входу (тобто з терміналу), тоді як `hGetContents` бере хендлер файла, що каже з якого файла має відбуватись читання. У всіх інших відношеннях вони роблять однаково. І так само як `getContents`, `hGetContents` не буде читати файл за один раз та зберігати його в пам'яті, але буде читати його по мірі потреби. Це насправді круто, оскільки ми можемо розглядати `contents` як повний вміст файла, але він насправді не є завантаженим в пам'ять. Так що якщо це дійсно величезний файл, виконання `hGetContents` не буде поїдати нашу пам'ять, але вона лише буде читати з файла тільки те, що потрібно, і коли це потрібно.

Зауважте різницю між хендлером, використаним для ідентифікації файла і вмістом цього файла, прикріплені в нашій програмі до `handle` та `contents`. `handle` є лише дещо, по чому ми знаємо, що є нашим файлом. Якщо ви уявляєте вашу файлову систему як насправді велику книгу, і кожний файл є глава цієї книги, `handle` є закладкою, що показує, де ви зараз читаєте (або) главу, тоді як `contents` є саме главою.

За допомогою `putStr contents` ми просто друкуємо вміст на стандартний вихід, і потім ми робимо `hClose`, що бере `handle` і повертає I/O дію, що зачиняє файл. Ви маєте самі зачиняти файл після відкриття його через `openFile`!

Інший спосіб зробити те саме, що ми тільки що зробили, це використання функцію `withFile`, що має сигнатуру типу `withFile ::FilePath -> IOMode -> (Handle -> IO a) -> IO a`. Вона приймає шлях до файлу, `IOMode`, і потім вона бере функцію, яка приймає хендлер і повертає деяку I/O дію. Те що вона повертає є I/O дія, що буде відкривати файл, робити те що ви бажаєте зробити, і потім зачиняє його. Результат, інкапсульований в фінальній I/O дії, є той самий, що і результат I/O дії від функції, яку ми надаємо. Це може виглядати як дещо ускладнене, але це насправді просто, особливо з лямбдами, і ось як виглядає наш попередній, переписаний для використання `withFile`:

[source,haskell]
----
import System.IO     
    
main = do     
    withFile "girlfriend.txt" ReadMode (\handle -> do  
        contents <- hGetContents handle     
        putStr contents)  
----

Як ви можете бачити, це дуже подібно до попереднього шматка коду. `(\handle -> ... )` є функцією, що бере `handle` та повертає I/O дію, і це звичайно виконується саме так, за допомогою лямбди. Причина того, що тут очікується функція, що повертає I/O дію, замість просто сприйняття I/O дії до виконання, та наступне закриття файлу, є в тому, що I/O дія, яку ми передаємо, не мала б знати, з яким файлом треба оперувати. Таким чином `withFile` відкриває файл, і потім передає `handle` до функції, яку ми надали. Вона отримує назад I/O дію від цієї функції, і потім створює I/O дію, що така сама, але зачиняє файл після себе. Ось як ми можемо створити свою власну функцію `withFile`:

[source,haskell]
----
withFile' :: FilePath -> IOMode -> (Handle -> IO a) -> IO a  
withFile' path mode f = do  
    handle <- openFile path mode   
    result <- f handle  
    hClose handle  
    return result  
butter toast
----

image:images/edd.png[]
Ми знаємо, що результатом буде I/O дія, так що ми можемо розпочати з `do`. Спочатку ми відкриваємо файл і отримуємо від нього `handle`. Потім ми застосовуємо `handle` до нашої функції, що отримати назад I/O дію, що робить всю нашу роботу. Ми прикріплюємо цю дію до `result`, зачиняємо `handle`, і потім повертаємо `result`. Через повернення результату, огорнутому в I/O дію, що ми отримали від `f`, ми зробили так, що наша I/O дія інкапсулює той самий результат, що ми отримали від `f handle`. Так що коли `f handle` повертає дію, що буде читати число рядків зі стандартного входу, і писати їх до файлу, та мати інкапсульованим результат числа рядків для читання, якби ми використовували цю `withFile'`, результуюча I/O дія також мала б своїм результатом число прочитаних рядків.

Так само, як ми маємо `hGetContents`, що робить як `getContents` для специфічного файлу, також є `hGetLine`, `hPutStr`, `hPutStrLn`, `hGetChar`, etc. Вони роблять як їх колеги без `h`, тільки вони беруть хендлно як параметр, та оперують на специфічному файлі замість стандартного вводу та стандартного виходу. Приклад: `putStrLn` є функція, що приймає рядок та повертає I/O дію, що буде записувати цей рядок на терміналі та новий рядок після цього. `hPutStrLn` приймає хендлер та рядок, і повертає I/O дію, що буде писати писати рядок в файл, асоційований з хендлером. В тому ж дусі `hGetLine` бере хендлер та повертає I/O дію, що читає рядок з файла.

Завантаження файлів і потім трактування їх вмісту як рядків є настільки загальним, що ми маємо ці три чудові невеличкі функції, щоб зробити нашу роботу ще простішою:

`readFile` має сигнатуру типу `readFile ::FilePath -> IO String`. Пам'ятайте, `FilePath` це просто інша назва для `String`. `readFile` бере шлях до файлу і повертає I/O дію, що буде читати файл (ліниво, звичайно), та прикріплює його вміст до дечого як рядок. Це зазвичай більш зручно, ніж робити `openFile` та прикріплення його до хендлера, і потім робити `hGetContents`. Ось як ми можемо записати наш попередній приклад з `readFile`:

[source,haskell]
----
import System.IO  
  
main = do  
    contents <- readFile "girlfriend.txt"  
    putStr contents  
----

Оскільки ми не отримали хендлера, з яким ми можемо ідентифікувати наш файл, ми не можемо зачинити його вручну, так що Haskell робить це за нас, коли ми використовуємо `readFile`.

`writeFile` має тип `writeFile ::FilePath -> String -> IO ()`. Вона бере шлях до файлу і рядок, що треба записати до цього файлу, і повертає I/O дію, що буде виконувати запис. Якщо такий файл вже існує він буде усічений до нульової довжини, перед тим як буде перезаписаний. Ось як перетворити `girlfriend.txt` на `CAPSLOCKED` версію, і записати її до `girlfriendcaps.txt`:

[source,haskell]
----
import System.IO     
import Data.Char  
    
main = do     
    contents <- readFile "girlfriend.txt"     
    writeFile "girlfriendcaps.txt" (map toUpper contents)  
$ runhaskell girlfriendtocaps.hs  
$ cat girlfriendcaps.txt  
HEY! HEY! YOU! YOU!  
I DON'T LIKE YOUR GIRLFRIEND!  
NO WAY! NO WAY!  
I THINK YOU NEED A NEW ONE!  
----

`appendFile` має сигнатуру типу таку саму як `writeFile`, тільки `appendFile` не відсікає файл до нульової довжини, якщо він вже існує, але додає дещо до нього.

Скажімо, ми маємо файл `todo.txt` що має по одному завданню на рядок, що нам треба робити. Тепер давайте створимо програму, що бере рядок зі стандартного вводу і додає його до нашого списку `to-do`.

[source,haskell]
----
import System.IO     
    
main = do     
    todoItem <- getLine  
    appendFile "todo.txt" (todoItem ++ "\n")  
$ runhaskell appendtodo.hs  
Iron the dishes  
$ runhaskell appendtodo.hs  
Dust the dog  
$ runhaskell appendtodo.hs  
Take salad out of the oven  
$ cat todo.txt  
Iron the dishes  
Dust the dog  
Take salad out of the oven  
----

Нам треба додати `"\n"` в кінець кожного рядка, оскільки `getLine` не видає нам символ нового рядка в кінці.

О, ще одна річ. Ми казали як виконання `contents <- hGetContents handle` не призводить до читання всього файлу за один раз, та розміщення його в пам'яті. Це I/O ліниве, так що виконання цього:

[source,haskell]
----
main = do   
    withFile "something.txt" ReadMode (\handle -> do  
        contents <- hGetContents handle  
        putStr contents)  
----

те саме що під'єднати трубку від файла до виходу. Так само як ви можете думати про списки як про потоки, так само ви можете думати про файли як про потоки. Це буде читати один рядок за раз і друкувати його на термінал по мірі надходження. Так що ви можете запитати, якою довгою є ця труба? Як часто буде відбуватись доступ до диску? Гаразд, для текстових файлів буферизація по замовчанню зазвичай обмежується рядком. Це означає, що найменьша частина файлу, що буде прочитана за раз, є рядок. Ось чому в цьому випадку воно дійсно читає рядок, друкує вивід, читає наступний рядок, друкує його, і так далі. Для двійникових файлів звичайна буферизація зазвичай блочна. Це означає, що вона буде читати файл частина за частиною. Розмір цього блока є саме такий, який вважає за найкраще ваша операційна система.

Ви можете контролювати як саме відбувається буферизація через використання функції `hSetBuffering`. Вона приймає хендлер та `BufferMode`, і повертає I/O дію, що встановлює буферизацію. `BufferMode` є простим типом даних переліку і можливими значеннями є: `NoBuffering`, `LineBuffering` або `BlockBuffering (Maybe Int)`. `Maybe Int` для завдання того, наскільки великим має бути фрагмент, в байтах. Якщо це `Nothing`, тоді розмір буфера визначить операційна система. `NoBuffering` означає, що буде читатись один символ за раз. `NoBuffering` зазвичай програє як режим буферизації, оскільки він має дуже багато доступу до диску.

Ось наш попередній фрагмент коду, тільки він не читає файл рядок за рядком, але замість цього читає цілий файл шматками по 2048 байт.

[source,haskell]
----
main = do   
    withFile "something.txt" ReadMode (\handle -> do  
        hSetBuffering handle $ BlockBuffering (Just 2048)  
        contents <- hGetContents handle  
        putStr contents)  
----

Читання файлів в більших шматках може допомогти, якщо ви бажаєте мінімізувати доступ до диску, або коли наш файл є насправді повільним мережевим ресурсом.

Ми також можемо використовувати `hFlush`, що є функцією, що приймає хендлер та повертає I/O дію, що буде скидати буфер для файла, асоційованого з хендлером. Коли ми робимо буферізацію рядків, буфер скидається після кожного рядка. Коли ми робимо блочну буферизацію, це відбувається після читання фрегменту. Він також скидається після закриття хендлера. Це означає, що коли ми досягли символ нового рядка, механізм читання (або запису) повідомляє що данні скинчились. Але коли ми використовуємо `hFlush` це змушує повідомити що данні для читання скінчились. Після скидання дані доступні для інших програм, що виконуються в той самий час.

Думайте про читання блочно буферизованого файлу таким чином: ваш туалетний бачок налаштований таким чином, щоб зливатись після набору одного галону води. Так що ви починаєте запускати воду, і коли мітка галону досягнута, вода автоматично зливається, і дані в воді, що ви назбирали, може бути прочитані. Але ви можете змити туалет вручну також, натиснувши кнопку на бачку. Це змиє туалет і вся вода (дані) можуть бути прочитані. В випадку якщо ви не помітили, змивання туалету вручну є метафорою для `hFlush`. Це не дуже гарна аналогія по стандартам програмних аналогій, але я бажав використати об'єкт з реального світу, що може бути злитий для гумору.

Ми вже створювали програму для додавання нового елементу до нашого `to-do` списку в `todo.txt`, тепер давайте створимо програму для видалення елементу. Я просто вставлю код і потім ми пройдемо по програмі разом, так що ви побачите що вона насправді проста. Ми будемо використовувати нові функції з `System.Directory` і одну нову функцію з `System.IO`, але вони всі будуть пояснені.

Так чи інакше, ось програма для видалення елемента з `todo.txt`:

[source,haskell]
----
import System.IO  
import System.Directory  
import Data.List  
  
main = do        
    handle <- openFile "todo.txt" ReadMode  
    (tempName, tempHandle) <- openTempFile "." "temp"  
    contents <- hGetContents handle  
    let todoTasks = lines contents     
        numberedTasks = zipWith (\n line -> show n ++ " - " ++ line) [0..] todoTasks     
    putStrLn "These are your TO-DO items:"  
    putStr $ unlines numberedTasks  
    putStrLn "Which one do you want to delete?"     
    numberString <- getLine     
    let number = read numberString     
        newTodoItems = delete (todoTasks !! number) todoTasks     
    hPutStr tempHandle $ unlines newTodoItems  
    hClose handle  
    hClose tempHandle  
    removeFile "todo.txt"  
    renameFile tempName "todo.txt"  
----

Для початку ми просто відкриваємо `todo.txt` в режимі читання, і прикріплюємо його хендлер до `handle`.

Далі ми використовуємо функцію, яку ми досі не зустрічали, і що походить з `System.IO` — `openTempFile`. Її ім'я досить пояснює само себе. Вона бере шлях до тимчасового каталогу і шаблон імені для файлу, і відериває тимчасовий файл. Ми використали `"."` для тимчасової директорії, оскільки `.` означає поточну директорію майже на кожній OS. Ми використали `"temp"` як ім'я шаблону для тимчасового файлу, що означає, що тимчасовий файл буде названий `temp`, плюс деякі випадкові символи. Він повертає I/O дію, що створює тимчасовий файл і результат цієї I/O дії буде пара значень: ім'я тимчасового файлу і хендлер. Ми можемо просто відкрити звичайний файл з назвою `todo2.txt`, або щось біля того, але краща практика використовувати `openTempFile`, так що ви знаєте, що ви можливо нічого не переписуєте.

Причина, з якої ми не використовуємо `getCurrentDirectory` для отримання поточної директорії, та потім передачі її до `openTempFile`, але замість просто використання `"."` до `openTempFile` це тому, що `.` посилається на поточну директорію на unix-подібних системах та Windows.

Наступним кроком ми прикріплюємо вміст `todo.txt` до `contents`. Потім розбиваємо цей рядок на список рядків, кожний рядок в одному рядку. Так що `todoTasks` депер щось подібне до `["Iron the dishes", "Dust the dog", "Take salad out of the oven"]`. Ми зціплюємо з числами від `0` і далі цей список з функцією, що приймає число, як `3`, та рядок, як `"hey"`, і повертає `"3 - hey"`, так що `numberedTasks` буде `["0 - Iron the dishes", "1 - Dust the dog"` .... Ми поєднуємо цей список рядків в один рядок розділений новими рядками за допомогою `unlines`, і друкуємо цей рядок на термінал. Зауважте, що замість робити це ми можемо також зробити `mapM putStrLn numberedTasks`.

Ми питаємо користувача який елемент він бажає видалити, і очікуємо доки він введе число. Скажімо він бажає видалити номер `1`, що є `Dust the dog`, так що він натискає `1`. `numberString` тепер `"1"`, і оскільки ми бажаємо число а не рядок, ми запускаємо `read` на цьому числі для отримання `1`, і прикріплюємо його до `number`.

Згадайте функції `delete` та `!!` з `Data.List`. `!!` повертає елемент зі списку з деяким індексом, і `delete` видаляє перше входження елементу в списку, і повертає новий список без цього входження. `(todoTasks !! number)` (`number` тепер `1`) повертає `"Dust the dog"`. Ми прикріплюємо `todoTasks` без першого входження `"Dust the dog"` до `newTodoItems`, і потім поєднуємо це в один рядок за допомогою `unlines` перед записом його в тимчасовий файл, який ми відкрили. Старий файл тепер незмінний, і тимчасовий файл містить всі рядки що містив старий, крім того що ми видалили.

Після цього ми зачиняємо обоє, оригінальний та тимчасовий файли, та потім ми видаляємо оригінальній за допомогою `removeFile`, що, як ви можете бачити, бере шлях до файлу та видаляє його. Після видалення старого `todo.txt`, ми використовуємо `renameFile` для переіменування тимчасового файлу на `todo.txt`. Будьте обережні, `removeFile` та `renameFile` (що обоє, до речі, в `System.Directory`), приймають шляхі в якості параметрів, не хендлери.

І це все! Ми можемо зробити це навіть за меньше рядків, але ми будемо дуже обережні не переписати жодних існуючих файлів, і чемно попросимо операційну систему сказати нам, де можна розташувати наш тимчасовий файл. Надамо цьому руху!

[source,haskell]
----
$ runhaskell deletetodo.hs  
These are your TO-DO items:  
0 - Iron the dishes  
1 - Dust the dog  
2 - Take salad out of the oven  
Which one do you want to delete?  
1  
  
$ cat todo.txt  
Iron the dishes  
Take salad out of the oven  
  
$ runhaskell deletetodo.hs  
These are your TO-DO items:  
0 - Iron the dishes  
1 - Take salad out of the oven  
Which one do you want to delete?  
0  
  
$ cat todo.txt  
Take salad out of the oven  
----

=== Аргументи командного рядка

image:images/arguments.png[]
Мати справу з аргументами командного рядка майже необхідність, якщо ви бажаєте створити скрипт або застосування, що робить в терміналі. На щастя стандартна бібліотека Haskell має гарний спосіб отримання аргументів командного рядка в програмі.

В попередньому розділі ми створили одну програму для додавання елемента `to-do` до нашого `to-do`, і одну програму для видалення елемента. Існує дві проблеми з обраним нами підходом. Перше в тому що ми жорстко закодували ім'я нашого файла `to-do` в нашому коді. Ми просто вирішили, що файл буде називатись `todo.txt`, і що користувач ніколи не матиме потреби керувати декількома списками `to-do`.

Один шлях вирішити це є завжди запитувати користувача, який файл він бажає використовувати для списку `to-do`. Ми використовували цей підхід, коли хотіли знати, який елемент користувач бажає видалити. Це робить, але це не так добре, оскільки це вимагає від користувача запустити програму, дочекатись доки програма запитає дещо, і потім сказати це програмі. Це називається інтерактивною програмою, і складна частина з інтерактивними програмами командного рядка така — що коли ми бажаємо автоматизувати роботу цієї програми, як це робиться в пакетних скриптах? Складніше скласти пакетний скрипт, що взаємодіє з програмою, ніж скрипт, що просто викликає одну програму або декілька з них.

Ось чому іноді краще щоб користувач сказав програмі що він хоче під час запуску програми, замість щоб програма запитувала користувача під час роботи. І найкращий шлях користувачеві сказати програмі, що він бажає що вона робила, це через аргументи командного рядка!

Модуль `System.Environment` також має круті I/O дії. Одна це `getArgs`, що має тип `getArgs ::IO [String]`, і є I/O дією, що буде отримувати аргументи, з якими програма була запущена, і має свій результат в вигляді списку з ціма аргументами. `getProgName` має тип `getProgName ::IO String`, і є I/O дією, що містить ім'я програми.

Ось невелика програма, що демонструє як роблять ці дві функції:

[source,haskell]
----
import System.Environment   
import Data.List  
  
main = do  
   args <- getArgs  
   progName <- getProgName  
   putStrLn "The arguments are:"  
   mapM putStrLn args  
   putStrLn "The program name is:"  
   putStrLn progName  
----

Ми прикріпили `getArgs` та `progName` до `args` та `progName`. Ми кажемо `The arguments are:`, і потім для кожного аргумента в `args` ми робимо `putStrLn`. Нарешті ми також друкуємо ім'я нашої програми. Давайте завершимо цей `arg-test`.

[source,bash]
----
$ ./arg-test first second w00t "multi word arg"  
The arguments are:  
first  
second  
w00t  
multi word arg  
The program name is:  
arg-test  
----

Гарно. Обзброєні цім знанням ви можете створити деякі круті застосування командного рядка. Фактично давайте пійдемо далі і самі створимо таке. В попередньому розділі ми створили окрему прогруму для додавання завдань і окрему програму для для їх видалення. Тепер ми збираємось поєднати це в одну програму, і що вона робитиме від аргументів командного рядка. Ми також збираємось створити її так, що ми зможемо оперувати з разними файлами , не тільки з `todo.txt`.

Ми назвемо її просто `todo` і вона зможе робити (хаха!) три різні речі:

* Переглядати завдання
* Додавати завдання
* Видаляти завдання

Ми не збираємось обтяжувати себе можливим поганим вводом дуже сильно і прямо зараз.

Наша програма буде зроблена таким чином, що якщо ми бажаємо додати завдання `Find the magic sword of power` до файлу `todo.txt`, нам треба набрати `todo add todo.txt "Find the magic sword of power"` в нашому терміналі. Щоб подивитись завдання, нам треба зробити тільки `todo view todo.txt`, і щоб видалити завдання з індексом 2 ми робимо `todo remove todo.txt 2`.

Ми почнемо зі створення асоціативного списку диспечеризації. Це буде простий асоціативний список, що матиме аргументи командного рядка в якості ключів, і функції як відповідні значення. Всі ці функції будуть типу `[String] -> IO ()`. Вони будуть приймати список аргументів як параметр, і повертають I/O дію, що робить перегляд, додавання, видалення, тощо.

[source,haskell]
----
import System.Environment   
import System.Directory  
import System.IO  
import Data.List  
  
dispatch :: [(String, [String] -> IO ())]  
dispatch =  [ ("add", add)  
            , ("view", view)  
            , ("remove", remove)  
            ]  
----

Ми все де маємо визначити `main`, `add`, `view` та `remove`, так що почнемо `main`:

[source,haskell]
----
main = do  
    (command:args) <- getArgs  
    let (Just action) = lookup command dispatch  
    action args  
----

Спочатку ми отримуємо аргументи, і прикріплюємо їх до `(command:args)`. Якщо ви пам'ятаєте ваше співпадіння шаблонів, це означає, що перший аргумент буде прив'язаний до `command` та решта аргументів буде прив'язана до `args`. Якщо ми викличемо нашу програму як `todo add todo.txt "Spank the monkey"`, команда буде `"add"`, і `args` буде `["todo.xt", "Spank the monkey"]`.

В наступному рядку ми шукаємо нашу команду в списку диспечеризації. Оскільки `"add"` вказує на `add`, ми отримуємо `Just add` в якості результату. Ми використовуємо порівняння шаблонів для виділення наших функцій з `Maybe`. Що станеться, якщо нашої `command` немає в списку диспечеризації? Гаразд, тоді пошук буде повертати `Nothing`, але ми казали, що не будемо дуже сильно заморочувати себе на збої, так що наше порівняння дасть збій, і наша програма втратить придатність.

Нарешті ми викликаємо функцію нашої дії з рештою списку аргументів. Це повертатиме I/O дію, що або додає елемент, або відображує список елементів, або видаляє елемент. І оскільки ця дія є частиною блоку `main do`, вона буде виконана. Якщо ми прослідимо наш приклад далі, і наша функція дії є `add`, вона буде викликана з `args` (тобто `["todo.txt", "Spank the monkey"]`), і повертає I/O дію, що додає `Spank the monkey` до `todo.txt`.

Чудово! Все що нам залишилось, це реалізувати `add`, `view` та `remove`. Давайте почнемо з `add`:

[source,haskell]
----
add :: [String] -> IO ()  
add [fileName, todoItem] = appendFile fileName (todoItem ++ "\n")  
----

Якщо ми викличемо нашу програму як `todo add todo.txt "Spank the monkey"`, `"add"` буде прикріплене до `command` в першому співпадінні шаблонів в блоці `main`, тоді як `["todo.txt", "Spank the monkey"]` буде передане до функції, що ми отримали зі списку диспачеризації. Так що, оскільки ми не маємо справу з поганим вводом прямо зараз, ми просто робимо співпадіння шаблону по списку з ціма двома елементами, і повертаємо I/O дію, що додає цей рядок до кінця файлу, разом з символом нового рядка.

Далі давайте реалізуємо функціональність перегляду списку. Якщо ми бажаємо переглянути елементи в файлі, ми робимо `todo view todo.txt`. Так що в першому порівнянні шаблонів `command` буде `"view"`, і `args` буде `["todo.txt"]`.

[source,haskell]
----
view :: [String] -> IO ()  
view [fileName] = do  
    contents <- readFile fileName  
    let todoTasks = lines contents  
        numberedTasks = zipWith (\n line -> show n ++ " - " ++ line) [0..] todoTasks  
    putStr $ unlines numberedTasks  
----

Ми вже робили майже таку саму річ в програмі, що тільки видаляла завдання, де ми відображували завдання, так що користувач міг обрати одне для видаляння. Але тут ми просто відображуємо завдання.

І, нарешті, ми збираємось реалізувати `remove`. Вона буде дуже подібна до програми, що тільки видаляє завдання, так що якщо ви не розумієте, як тут робить видалення елементів, перевірте пояснення для тієї програми. Головна різниця в тому, що ми не жорстко кодуємо `todo.txt`, але отримуємо його як аргумент. Ми також не видаємо підказку користувачеві для введення номеру завдання для видалення, ми отримуємо його як аргумент.

[source,haskell]
----
remove :: [String] -> IO ()  
remove [fileName, numberString] = do  
    handle <- openFile fileName ReadMode  
    (tempName, tempHandle) <- openTempFile "." "temp"  
    contents <- hGetContents handle  
    let number = read numberString  
        todoTasks = lines contents  
        newTodoItems = delete (todoTasks !! number) todoTasks  
    hPutStr tempHandle $ unlines newTodoItems  
    hClose handle  
    hClose tempHandle  
    removeFile fileName  
    renameFile tempName fileName  
----

Ми відкрили файл на основі `fileName` і відкрили тимчасовий файл, видалили рядок з індексом, який користувач бажає видалити, записали це в тимчасовий файл, видалили оригінальний файл і переіменували тимчасовий файл назад в `fileName`.

Ось вся програма разом, у всій своїй славі!

[source,haskell]
----
import System.Environment   
import System.Directory  
import System.IO  
import Data.List  
  
dispatch :: [(String, [String] -> IO ())]  
dispatch =  [ ("add", add)  
            , ("view", view)  
            , ("remove", remove)  
            ]  
   
main = do  
    (command:args) <- getArgs  
    let (Just action) = lookup command dispatch  
    action args  
  
add :: [String] -> IO ()  
add [fileName, todoItem] = appendFile fileName (todoItem ++ "\n")  
  
view :: [String] -> IO ()  
view [fileName] = do  
    contents <- readFile fileName  
    let todoTasks = lines contents  
        numberedTasks = zipWith (\n line -> show n ++ " - " ++ line) [0..] todoTasks  
    putStr $ unlines numberedTasks  
  
remove :: [String] -> IO ()  
remove [fileName, numberString] = do  
    handle <- openFile fileName ReadMode  
    (tempName, tempHandle) <- openTempFile "." "temp"  
    contents <- hGetContents handle  
    let number = read numberString  
        todoTasks = lines contents  
        newTodoItems = delete (todoTasks !! number) todoTasks  
    hPutStr tempHandle $ unlines newTodoItems  
    hClose handle  
    hClose tempHandle  
    removeFile fileName  
    renameFile tempName fileName  
----

image:images/salad.png[]
Щоб підсумувати наше рішення: ми створили асоціативну диспечеризацію, що відображує команди на функції, які приймають деякі аргументи командного рядка, та повертають I/O дію. Ми дивимось, що таке `command`, і на цій основі отримуємо функцію с асоціативного списку. Ми викликаємо цю функцію з залишком аргументів командного рядка, щоб отримати назад I/O дію, що буде робити відповідні речі, і потім просто виконуємо цю дію!

В інших мовах ми б могли реалізувати це за допомогою великого твердження `switch case` або ще якось, але використання функцій вищого порядку дозволяє нам лише сказати списку диспечеризації дати нам відповідну функцію, і потім сказати функції дати нам I/O дію для деяких аргументів командного рядка.

Давайте спробуємо наше застосування!

[source,bash]
----
$ ./todo view todo.txt  
0 - Iron the dishes  
1 - Dust the dog  
2 - Take salad out of the oven  
  
$ ./todo add todo.txt "Pick up children from drycleaners"  
  
$ ./todo view todo.txt  
0 - Iron the dishes  
1 - Dust the dog  
2 - Take salad out of the oven  
3 - Pick up children from drycleaners  
  
$ ./todo remove todo.txt 2  
  
$ ./todo view todo.txt  
0 - Iron the dishes  
1 - Dust the dog  
2 - Pick up children from drycleaners  
----

Інша крута річ щодо цього в тому, що легко можна додати додаткову функціональність. Просто дадайте елемент в асоциативний список диспечеризації, Просто додайте елемент в асоціативний список і реалізуйте відповідну функцію - і ви вже щасливі! В якості завдання ви можете спробувати реалізувати функцію `bump`, що буде брати файл і номер завдання, і повертати I/O дію, що буде проштовхувати завдання нагору списку to-do.

Ви можете зробити, щоб ця програма падала більш витончено в випадку поганого вводу (наприклад, якщо хтось виконає `todo UP YOURS HAHAHAHA`), створивши I/O дію, яка тільки повідомляє, що була помилка (скажімо, `errorExit ::IO ()`), і потім перевіряти на потенційно помилковий ввод. Якщо ввод помилковий - виконуємо I/O дію, що повідомляє про подію. Інший шлях є використання виключень, з чим ми скоро зустрінемось.

=== Випадковість

image:images/random.png[]
Багато разів під час програмування вам треба отримати деякі випадкові дані. Можливо ви створюєте гру, де треба кинути на вдачу, або вам треба згенерувати деякі тестові дані для перевірки вашої програми. В програмуванні є багато використань випадкових даних. Гаразд, насправді псевдо-випадкових, бо всі ми знаємо, що єдине справжнє джерело випадковості є мавпа на моно-колесі, з сиром в одній руці і задом в іншій. В цьому розділі ми поглянемо як змусити Haskell генерувати на-вигляд випадкові дані.

В більшості інших мов програмування ви маєте функції, що повертають вам деяке випадкове число. Кожного разу, коли ви викликаєте цю функцію, ви отримуєте (маємо надію) інше випадкове число. Як щодо Haskell? Гарад, пам'ятаємо, Haskell є чисто функціональною мовою. Що означає, що він має посилкову прозорість. Що, В СВОЮ ЧЕРГУ, означає, що функція, двічі обчислена з тими самими параметрами, має двічі продукувати той самий результат. Це дійсно круто, оскільки це дозволяє нам по-іншому продумувати програми, і це дозволяє нам відкладувати виконання до того часу, коли це насправді потрібно. Якщо я викликаю функцію, я можу бути певний, що вона не буде робити жодні веселі речі перед надання мені результатів. Все що важливе, це її результат. Однак це робить отримання випадкових чисел трохи заплутаним. Наприклад, я маю таку функцію:

[source,haskell]
----
randomNumber :: (Num a) => a  
randomNumber = 4  
----

Вона не дуже корисна, як функція випадкових чисел, бо вона завжди буде повертати 4. Навіть якщо я вважатиму що це 4 повністю випадкове, бо я використав кості щоб визначити його.

Як інші мови виробляють на вигляд випадкові числа? Гаразд, вони беруть різну інформацію з вашого комп'ютера, як поточний час, як сильно та де ви пересували мишу, та який різновид шумів ви створювали за комп'ютером. І базуючись на цьому видають число, що виглядає досить випадковим. Комбінація ціх факторів (їх випадковість) можливо є різною в кожний окремий момент часу, так що ви отримуєте інше випадкове число.

Ага. То в Haskell ми можемо отримати випадкове число, коли ми створимо функцію, що приймає свій параметр, що є випадковістю, і на основі цього повертає деяке число (або інший тип даних).

Входить модуль `System.Random`. Він має всі функціЇ, що задовільняють наші потреби в випадковості. Давайте просто зануримось в одну з функцій, які він експортує, а саме `random`. Ось її тип: `random ::(RandomGen g, Random a) => g -> (a, g)`. Отакої! В цьому типі з'являються деякі нові типокласи! Типоклас `RandomGen` існує для типів, що можуть діяти як джерело випадковості. Типоклас `Random` існує для речей, що можуть приймати випадкові значення. Логічне значення може приймати випадкові значення, а саме `True` або `False`. Число також може приймати безліч різних випадкових значень. Чи може функція приймати випадкове значення? Я так не думаю, напеве ні! Якщо ми спробуємо перекласти декларацію `random` на людську мову, ми отримаємое: вона приймає випадковий генервтор (що є нашим джерелом випадковості), та повертає випадкове значення і новий випадковий генератор. Чому вона також повертає новий генератор разом з випадковим значенням? Гараз, через мить ми це побачимо.

Щоб використати випадкову функцію, ми маємо взятись за один із цих випадкових генераторів. Модуль `System.Random` експортує крутий тип, `StdGen`, що є примірником типокласу `RandomGen`. Ми можемо або створити `StdGen` вручну, або ми можемо сказати системі дати нам готовий, що базується на різних типах випадкових речей.

Щоб створити випадковий генератор, використовуйте функцію `mkStdGen`. Він має тип `mkStdGen ::Int -> StdGen`. Вона приймає ціле, і базуючись на цьому видає випадковий генератор. Гаразд, давайте спробуємо використати `random` та `mkStdGen` в тандемі, для отримання (навряд чи випадкового) числа.

[source,haskell]
----
ghci> random (mkStdGen 100)  
<interactive>:1:0:  
    Ambiguous type variable `a' in the constraint:  
      `Random a' arising from a use of `random' at <interactive>:1:0-20  
    Probable fix: add a type signature that fixes these type variable(s)  
----

Що таке? Ага, вірно, випадкова функція може повертати значення любого типу, що є частиною типокласу `Random`, так що ми маємо інформувати Haskell, який різновид типу ми бажаємо. Також не забуваємо, що він повертає випадкове значення, в парі з випадковим генератором.

[source,haskell]
----
ghci> random (mkStdGen 100) ::(Int, StdGen)  
(-1352021624,651872571 1655838864)  
----

Нарешті! Число, що виглядає випадковим! Перший компонент тапла є наше число, а ось другий є текстуальне представлення нашого нового випадковогоо генератора. Що трапиться,якщо ми знову викличемо `random` з тим самим випадковим генератором?

[source,haskell]
----
ghci> random (mkStdGen 100) ::(Int, StdGen)  
(-1352021624,651872571 1655838864)  
----

Звичайно. Той самий результат для тих самих параметрів. Так що давайте спробуємо надати інший випадковий генератор як параметр.

[source,haskell]
----
ghci> random (mkStdGen 949494) ::(Int, StdGen)  
(539963926,466647808 1655838864)  
----

Все гаразд, круто, чудово, інше число. Ми можемо використати анотацію типу, щоб отримати інші типи від функції.

[source,haskell]
----
ghci> random (mkStdGen 949488) ::(Float, StdGen)  
(0.8938442,1597344447 1655838864)  
ghci> random (mkStdGen 949488) ::(Bool, StdGen)  
(False,1485632275 40692)  
ghci> random (mkStdGen 949488) ::(Integer, StdGen)  
(1691547873,1597344447 1655838864)  
----

Давайте створимо функцію, що симулює кидання монети три рази поспіль. Якщо `random` не повертав би новий генератор разом зі значенням, ми б мали зробити щоб ця функція приймала три випадкові генератори як параметри, і потім повертала результат кидання для кожного з них. Але це звучить погано, бо якщо генератор може створити значення типу `Int` (який може взяти на себе навантаження різних значень), вона має бути в змозі створити три кидка монети (що може приймати рівно вісім комбінацій). Тому ось де `random` зі своїм поверненням випадкового генератора разом зі значенням стає в нагоді.

Ми представимо монету як просте `Bool`. `True` є орел, `False` решка.

[source,haskell]
----
threeCoins ::StdGen -> (Bool, Bool, Bool)  
threeCoins gen =   
    let (firstCoin, newGen) = random gen  
        (secondCoin, newGen') = random newGen  
        (thirdCoin, newGen'') = random newGen'  
    in  (firstCoin, secondCoin, thirdCoin)  
----

Ми викликаємо `random` з генератором, що ми отримали як параметр, щоб отримати монету та новий генератор. Потім ми викликаємо його знову, тільки цього разу з новим генератором, щоб отримати другу монету. Ми робимо те саме для отримання тертьої монети. Коли б ми викликали його з тим самим генератором кожного разу, всі монети мали б те саме значення, і ми б могли отримати лише `(False, False, False)` або `(True, True, True)` в якості результата.

[source,haskell]
----
ghci> threeCoins (mkStdGen 21)  
(True,True,True)  
ghci> threeCoins (mkStdGen 22)  
(True,False,True)  
ghci> threeCoins (mkStdGen 943)  
(True,False,True)  
ghci> threeCoins (mkStdGen 944)  
(True,True,True)  
----

Зауважте, що нам не треба робити `random gen ::(Bool, StdGen)`. Це тому, що ми вже вказали, що ми бажаємо логічні в декларації типу функції. Ось чому Haskell може вивести, що ми бажаємо отримати логічне в цьому випадку.

То що, коли ми бажаємо кинути чотири монети? Або п'ять? Гаразд, існує функція з назвою `randoms`, що приймає генератор, та повертає безкінечну послідовність значень на основі цього генератора.

[source,haskell]
----
ghci> take 5 $ randoms (mkStdGen 11) ::[Int]  
[-1807975507,545074951,-1015194702,-1622477312,-502893664]  
ghci> take 5 $ randoms (mkStdGen 11) ::[Bool]  
[True,True,True,True,False]  
ghci> take 5 $ randoms (mkStdGen 11) ::[Float]  
[7.904789e-2,0.62691015,0.26363158,0.12223756,0.38291094]  
----

Чому `randoms` не повертає новий генератор разом зі списком? Ми можемо реалізувати функцію `randoms` дуже просто, ось так:

[source,haskell]
----
randoms' ::(RandomGen g, Random a) => g -> [a]  
randoms' gen = let (value, newGen) = random gen in value:randoms' newGen  
----

Рекурсивне визначення. Ми отримуємо випадкове значення і новий генератор від поточного генератора, і потім створюємо список, що має значення в якості голови, і випадкові числа на основі нового генератора в якості хвоста. Оскільки ми повинні бути в змозі генерувати безкінечну кількість чисел, ми не повертаємо новий генератор назад.

Ми можемо створити функцію, що генерує скінчений потік чисел і новий генератор таким чином:

[source,haskell]
----
finiteRandoms :: (RandomGen g, Random a, Num n) => n -> g -> ([a], g)  
finiteRandoms 0 gen = ([], gen)  
finiteRandoms n gen =   
    let (value, newGen) = random gen  
        (restOfList, finalGen) = finiteRandoms (n-1) newGen  
    in  (value:restOfList, finalGen)  
----

Знову це рекурсивне визначення. Ми кажемо, що якщо ми бажаємо 0 чисел, ми просто повертаємо порожній список і генератор, що був нам наданий. Для любого іншого числа випадкових значень ми спочатку отримуємо одно випадкове значення і новий генератор. Це буде голова. Потім ми кажемо, що хвіст буде `n - 1` чисел, згенерованих новим генератором. Потім ми повертаємо голову і залишок списку поєднані, і фінальний генератор, що ми маємо від отримання `n - 1` випадкових чисел.

Що, коли ми бажаємо випадкове значення в деякому різновиді диапазону? Всі випадкові цілі до ціх пір були надмірно великі або малі. Що, коли ми бажаємо кинути кості? Гаразд, для ціх цілей ми використовуємо `randomR`. Вона має тип `randomR ::(RandomGen g, Random a) ::(a, a) -> g -> (a, g)`, що означає, що це різновид `random`, тільки вона приймає в якості першого параметру пару значень, що встановлюють нижню та верхню межу, та фінальне вироблене значення буде в ціх межах.

[source,haskell]
----
ghci> randomR (1,6) (mkStdGen 359353)  
(6,1494289578 40692)  
ghci> randomR (1,6) (mkStdGen 35935335)  
(3,1250031057 40692)  
----

Також існує `randomRs`, що продукує потік випадкових значень в визначених межах. Перевірте це:

[source,haskell]
----
ghci> take 10 $ randomRs ('a','z') (mkStdGen 3) :: [Char]  
"ndkxbvmomg"  
----

Гарно, виглядає як супер секретний пароль або щось таке.

Можливо ви питаєте себе, що цей розділ взагалі має назагал з I/O? Досі ми не зробили нічого, що має відношення до I/O. Гаразд, досі ми завжди створювали наші генератори випадкових чисел вручну, створюючи їх за допомогою довільних цілих. Пробелма в тому, що якщо ми робитимемо це в справжніх програмах, вони будуть завжди повертати ті самі випадкові числа, що буде недобре для нас. Ось чому `System.Random` пропонує I/O дію `getStdGen`, що має тип `IO StdGen`. Коли ваша програма починає роботу, вона питає систему щодо гарного генератора випадкових чисел, і зберігає його в так званому глобальному генераторі. `getStdGen` отримує цей глобальний випадковий генератор, коли ви прикріплюєте його до будь-чого.

Ось проста програма, що генерує випадковий рядок.

[source,haskell]
----
import System.Random  
  
main = do  
    gen <- getStdGen  
    putStr $ take 20 (randomRs ('a','z') gen)  
$ runhaskell random_string.hs  
pybphhzzhuepknbykxhe  
$ runhaskell random_string.hs  
eiqgcxykivpudlsvvjpg  
$ runhaskell random_string.hs  
nzdceoconysdgcyqjruo  
$ runhaskell random_string.hs  
bakzhnnuzrkgvesqplrx  
----

Однак будьте обережні, просте виконання `getStdGen` двічі буде запитувати систему про той самий глобальний генератор два рази. Якщо ви зробите таке:

[source,haskell]
----
import System.Random  
  
main = do  
    gen <- getStdGen  
    putStrLn $ take 20 (randomRs ('a','z') gen)  
    gen2 <- getStdGen  
    putStr $ take 20 (randomRs ('a','z') gen2)  
----

ви отрмаєте той самий рядок, надрукований двічі! Один спосіб отримати два різні рядка довжиною 20 символів є встановити безкінечний потік, і потім взяти перші 20 символів, та роздрукувати їх як один рядок, потім взяти інший набір з 20 символів та роздрукувати їх як другий рядок. Для цього ми можемо використати функцію `splitAt` з `Data.List`, що розділяє список по деякому індексу і повертає тапл, що має першу частину як перший компонент, і другу частину як другий компонент.

[source,haskell]
----
import System.Random  
import Data.List  
  
main = do  
    gen <- getStdGen  
    let randomChars = randomRs ('a','z') gen  
        (first20, rest) = splitAt 20 randomChars  
        (second20, _) = splitAt 20 rest  
    putStrLn first20  
    putStr second20  
----

Інший шлях є використовувати дію `newStdGen`, що розділяє наш поточний випадковий генератор на два генератори. Вона оновлює глобальний випадковий генератор за допомогою одного з них, і інкапсулює інший як свій результат.

[source,haskell]
----
import System.Random  
  
main = do     
    gen <- getStdGen     
    putStrLn $ take 20 (randomRs ('a','z') gen)     
    gen' <- newStdGen  
    putStr $ take 20 (randomRs ('a','z') gen')     
----

Ми не тільки отримуємо новий випадковий генератор, коли ми прикріплюємо `newStdGen` до будь-чого, глобальний також оновлюється, так що якщо ми робимо `getStdGen`, і прикріпляємо його до будь-чого, ми отримаємо генератор, що не є той самий що `gen`.

Ось невеличка програма, що змушує користувача здогадуватись, яке число вона загадала4.

[source,haskell]
----
import System.Random  
import Control.Monad(when)  
  
main = do  
    gen <- getStdGen  
    askForNumber gen  
  
askForNumber :: StdGen -> IO ()  
askForNumber gen = do  
    let (randNumber, newGen) = randomR (1,10) gen :: (Int, StdGen)  
    putStr "Which number in the range from 1 to 10 am I thinking of? "  
    numberString <- getLine  
    when (not $ null numberString) $ do  
        let number = read numberString  
        if randNumber == number   
            then putStrLn "You are correct!"  
            else putStrLn $ "Sorry, it was " ++ show randNumber  
        askForNumber newGen  
----

image:images/jackofdiamonds.png[]
Ми ствоорюємо функцію `askForNumber`, що приймає генератор випадкових чисел і повертає  I/O дію, що буде запитувати користувача ввести число, та потім каже, чи була його здогадка вірною. І цій функції ми спочатку генеруємо випадкове число і новий генератор на основі генератора, що ми отримали як параметр, і викликаємо `randNumber` та `newGen`. Давайте скажімо, що зенероване число є `7`. Потім ми кажемо користувачеві ввести число зі своєю здогадкою. Ми виконуємо `getLine`, та прикріплюємо результат до `numberString`. Тоді, якщо користувач вводить `7`, `numberString` стає `"7"`. Далі ми використовуєте  `when` для перевірки, чи рядок, що ввів користувач, є порожнім рядком. Якщо це так, виконується порожня I/O дія `return ()`, що ефективно завершує програму. Якщо це не так, виконується дія, що складається з блоу `do`. Ми використовуємо `read` на `numberString` для перетворення його на число, так що `number` тепер `7`.

Пробачте мені! Якщо користувач надає нам деякий ввод, що `read` на може прочитати (як `"haha"`), наша програма буде падати з незграбним повідомленням про помилку. Якщо ви не бажаєте, щоб ваша програма падала по помилковому вводу, використовуйте `reads`, що повертає порожній список, коли вона схибить в читанні рядка. Коли вона успішна, вона повертає список синглтон з таплом, що має наше бажане значення в якості одного компоненту, і рядок з тим що ми не спожили в якості іншого.

Ми перевіряємо, чи число що ми ввели дорівнює до того, що згенероване випадково, і видає відповідне повідомлення. І потім ми викликаємо `askForNumber` рекурсивно, тільки на цей раз з новим генератором, що ми отримали, що дає нам I/O дію, що просто така сама, яку ми виконали, тільки вона залежить від іншого генератора, і ми виконуємо її.

`main` складається з простого отримання випадкового генератора від системи, і виклика `askForNumber` з ним для отримання початкової дії.

Ось наша програма в дії!

[source,bash]
----
$ runhaskell guess_the_number.hs  
Which number in the range from 1 to 10 am I thinking of? 4  
Sorry, it was 3  
Which number in the range from 1 to 10 am I thinking of? 10  
You are correct!  
Which number in the range from 1 to 10 am I thinking of? 2  
Sorry, it was 4  
Which number in the range from 1 to 10 am I thinking of? 5  
Sorry, it was 10  
Which number in the range from 1 to 10 am I thinking of?  
----

Інший спосіб зробити цю програму наступний:

[source,haskell]
----
import System.Random  
import Control.Monad(when)  
  
main = do  
    gen <- getStdGen  
    let (randNumber, _) = randomR (1,10) gen :: (Int, StdGen)     
    putStr "Which number in the range from 1 to 10 am I thinking of? "  
    numberString <- getLine  
    when (not $ null numberString) $ do  
        let number = read numberString  
        if randNumber == number  
            then putStrLn "You are correct!"  
            else putStrLn $ "Sorry, it was " ++ show randNumber  
        newStdGen  
        main  
----

Це дуже подібно до попередньої версії, тільки замість створення функції, що приймає генератор, і потім викликає себе рекурсивно з новим оновленим генератором, ми робимо всю роботу в `main`. Після повідомлення користувача, чи був він правий чи ні, ми оновлюємо глобальний генератор, і потім знову викликаємо `main`. Обоє підхода гідні, але перший подобається мені більше, бо він робить меньше справ в `main`, і також провадить нам функцію, яку ми можемо легко використовувати ще раз.

=== Рядки байтів

image:images/chainchomp.png[]
Списки круті і корисні структури даних. Досі ми використовували їх майже весь час. Існують різноманітні функції, що оперують з ними, і лінивість Haskell дозволяє нам замінити цикли `for` та `while` на фільтрування та відображення для списків, оскільки обчислення буде відбуватись тільки коли це дійсно знадобиться, так що такі речі, як безкінечні списки (і навіть безкінечні списки безкінечних списків!) не є для нас проблемою. Ось чому списки також можуть бути використані для представлення потоків, або при читанні з зі стандартного входу, або при читанні з файлів. Ми можемо просто відкрити файл, і читати його як рядок, навіть якщо доступ до нього буде здійснено за виникнення потреби.

Однак обробка файлів як рядків має один недолік: він має схильність бути повільним. Як ви знаєте, `String` є синонімом типу для `[Char]`. `Char` не має фіксованого розміру, бо він має декілька байтів щоб представляти символи зі, скажімо, Unicode. Більше того, списки дійсно ліниві. Якщо ви маєте список, як `[1,2,3,4]`, він буде обчислений тільки за кончої потреби. Так що весь список є щось як обіцянка списку. Пам'ятайте, що цей `[1,2,3,4]` є синтаксичним цукром для `1:2:3:4:[]`. Коли перший елемент списку буде вимушений обчислитись (скажімо, для свого друку), залишок списку, `2:3:4:[]`, все ще залишається обіцянкою списку, і так далі. Так що ви можете думати про списки як про обіцянки, що наступний елемент буде доставлений, як тільки це дійсно має відбутись, і разом з цім обіцянка щодо елементу після нього. Не треба робити великі розумові зусилля,щоб дійти висновку, що обробка простого списку чисел як послідовності обіцянок не є сама ефективна річ в світі.

Це навантаження не дуже нас турбує більшісь часу, але це виявляється відповідальним при читанні великих файлів та їх обробці. Ось чому Haskell має байтові рядки. Байтові рядки є дещо як списки, тільки кожний елемент має один байт (або 8 біт) розміром. Спосіб, як вони обробляють лінивість, також відрізняється.

Байтові рядки мають два різновиди: суворі та ліниві. Суворі байтові рядки знаходятся в  `Data.ByteString`, і вони зовсім відходять від лінивості. Не задіяні жодні обіцянки; суворий байтовий рядок представляє послідовність байтів в массиві. Ви не можете мати таких речей, як безкінечні суворі байтові рядки. Якщо ви обчислите перший байт в суворому байтовому рядку, ви маєте обчислити його як ціле. Гарною стороною є те, що це меньш обтяжливо, бо немає задіяних грон (технічний термін для обіцянок). Недоліком є те, що це займе вашу пам'ять швидше, бо ці рядку читаються в пам'ять одночасно.

Інший різновид байтових рядків знаходиться в `Data.ByteString.Lazy`. Вони ліниві, але не такі ліниві, як списки. Як ми казали раніше, в списку стільки грон, скільки елементів. Це те, що робить їх повільними для наших цілей. Ліниві байтові рядки обирають інший підхід — вони зберігаються в шматках (не плутати з гронами!), кожний шматок має розмір 64K. Так що коли ви обчислюєте байт в лінивому байтовому рядку (друкуючи його, або ще щось), будуть обчислень перші 64K. Після цього це просто обіцянка для залишку шматків. Ліниві байтові рядки є різновид списків суворих байтових рядків розміром 64K. Коли ми обробляємо файл за допомогою лінивих байтових рядків, він буде читатись рядок за рядком. Це круто, бо це не призводить до стрімкого злету використання пам'яті, і 64K можливо гарно розміститься в вашому L2 кеші.

Якщо ви продивитесь документацію по `Data.ByteString.Lazy`, ви побачите, що він має багато функцій, що мають ті самі назви, що і такі самі з `Data.List`, тільки сингатури типу мають `ByteString` замість `[a]`, і `Word8` замість `a` в них. Функції з тими самими назвами здебільшого діють так само, як ті, що роблять зі списками. Оскільки імена ті самі, ми збираємось зробити кваліфікований імпорт в скрипті, і потім завантажити цей скрипт в GHCI, щоб погратись з байтовими рядками.

[source,haskell]
----
import qualified Data.ByteString.Lazy as B  
import qualified Data.ByteString as S  
----

`B` має ліниві типи і функції для байтових рядків, тоді як `S` має суворі. Ми в основному будемо використовувати ліниві версії.

Функція `pack` має сигнатуру типу `pack ::[Word8] -> ByteString`. Це означає те, що вона бере список байтів типу `Word8` і повертає `ByteString`. Ви можете думати про це як взяти список, що є лінивим, і зробити його менш лінивим, так що він лінивий тільки з інтервалами 64K.

Що за справи з цім типом `Word8`? Гаразд, це як `Int`, тільки він має значно меньший диапазон, а саме `0-255`. Він представляє 8-бітне число. І так само як `Int`, він в типокласі `Num`. Наприклад, ми знаємо, що значення `5` є поліморфним в тому, що може діяти як любий числовий тип. Гаразд, воно також може прийняти тип `Word8`.

[source,haskell]
----
ghci> B.pack [99,97,110]  
Chunk "can" Empty  
ghci> B.pack [98..120]  
Chunk "bcdefghijklmnopqrstuvwx" Empty  
----

Як ви можете бачити, ви звичайно не повинні турбуватись про `Word8` дуже сильно, оскількли система типів може дати числам обирати свій тип. Якщо ви спробуєте використати велике число, як `336` як `Word8`, воно буде огорнене по колу до `80`.

Ми запакували тільки декілька значень до `ByteString`, так що вони в одному шматку.  `Empty` таке як `[]` для списків.

`unpack` є зворотня функція до `pack`. Вона приймає байтовий рядок і обертає його на список байтів.

fromChunks takes a list of strict bytestrings and converts it to a lazy bytestring. toChunks takes a lazy bytestring and converts it to a list of strict ones.

[source,haskell]
----
ghci> B.fromChunks [S.pack [40,41,42], S.pack [43,44,45], S.pack [46,47,48]]  
Chunk "()*" (Chunk "+,-" (Chunk "./0" Empty))  
----

Це гарно, якщо ви маєте багато малих суворих байтових рядків, і ви бажаєте обробляти їх ефективно, без поєднання їх перед цім в один великий суворий байтовий рядок.

Байтова версія `:` має назву `cons`. Вона бере байт і байтовий рядок, і ставить байт на початок. Вона тим не менш лінива, так що вона створюватиме новий шматок, навіть якщо перший шматок в байтовому рядку не повний. Ось чому краще використовувати сувору версію  `cons`, `cons'`, якщо ви збираєтесь вставляти багато байтів на початок рядка.

[source,haskell]
----
ghci> B.cons 85 $ B.pack [80,81,82,84]  
Chunk "U" (Chunk "PQRT" Empty)  
ghci> B.cons' 85 $ B.pack [80,81,82,84]  
Chunk "UPQRT" Empty  
ghci> foldr B.cons B.empty [50..60]  
Chunk "2" (Chunk "3" (Chunk "4" (Chunk "5" (Chunk "6" (Chunk "7" (Chunk "8" (Chunk "9" (Chunk ":" (Chunk ";" (Chunk "<"  
Empty))))))))))  
ghci> foldr B.cons' B.empty [50..60]  
Chunk "23456789:;<" Empty  
----

Як ви можете бачити, `empty` створює порожній байтовий рядок. Бачите різницю між `cons` та `cons'`? За допомогою `foldr`, ми почали з порожнього байтового рядка, і потім пройшись по списку чисел справа, додаючи кожне число до початку байтового рядка. Коли ми використовуємо `cons`, ми скінчили одним шматком для кожного байту, що дещо перемагає мету.

В іншому модулі байтових рядків мають кіпу функцій, що аналогічні до таких в `Data.List`, включаючи але не обмежуючись до: `head`, `tail`, `init`, `null`, `length`, `map`, `reverse`, `foldl`, `foldr`, `concat`, `takeWhile`, `filter`, тощо.

Він також має функції, що мають ті самі імена і поводяться так само, як деякі функції, які можна знайти в `System.IO`, тільки `String` замінені на `ByteStrings`. Наприлад, функція `readFile` в `System.IO` має тип `readFile ::FilePath -> IO String`, коли `readFile` з модулів байтових рядків мають тип `readFile ::FilePath -> IO ByteString`. Начувайтесь, якщо ви використовуєте суворі байтові рядки, і ви намагаєтесь прочитати файл, він прочитає файл в пам'ять за один раз! З лінивими байтовами рядками читання буде відбуватись по милих шматках.

Давайте створимо невелику програму, що приймає два імені файлів як аргументи командного рядка, і копіює перший файл в другий файл. Зауважте, що `System.Directory` вже має функцію з назвою `copyFile`, але ми все одно збираємось реалізувати власну функцію і програму копіювання файлів.

[source,haskell]
----
import System.Environment  
import qualified Data.ByteString.Lazy as B  
  
main = do  
    (fileName1:fileName2:_) <- getArgs  
    copyFile fileName1 fileName2  
  
copyFile ::FilePath -> FilePath -> IO ()  
copyFile source dest = do  
    contents <- B.readFile source  
    B.writeFile dest contents  
----

Ми створюємо нашу власну функцію, що приймає два `FilePath` (пам'ятайте, що `FilePath` є просто синонім до `String`), і повертає I/O дію, що буде копіювати один файл в інший з використанням байтових рядків. В функції `main` ми просто отримуємо аргументи і викликаємо нашу функцію з ними, щоб отримати I/O дію, яка потім виконується.

----
$ runhaskell bytestringcopy.hs something.txt ../../something.txt  
----

Зауважте, що програма, що не використовує байтові рядки, може виглядати майже так само, з тією різницею, що ми використали `B.readFile` та `B.writeFile` замість `readFile` та `writeFile`. В багатьох випадках ви можете перетворити програму, що використовує звичайні рядки, на програму, що використовує байтові рядки, тільки зробивши потрібні імпорти, і потім поклавши кваліфіковані імена модулів перед деякими функціями. Іноді ви маєте конвертувати функції, що ви пишете для роботи з рядками, так, щоб вони робили з байтовими рядками, але це не складно.

Кожного разу, коли вам потрібна краща продуктивність в програмі, що читає багато даних в рядки, дайте спробувати байтовим рядкам - є шанси що вони прискорять справи з дуже невеликими зусиллями з вашого боку. Звичайно я пишу програми, використовуючи звичайні рядки, і потім конвертую їх для використання байтових рядків, якщо продуктивність є незадовільною.

=== Виключення

image:images/timber.png[]
Всі мови мають процедури, функції та частини коду, що можуть схибити в деякий спосіб. Це просто життєвий факт. Різні мови мають різні шляхи обробки ціх збоїв. В C ми зазвичай використовуємо деяке незвичайне значення повернення (як `-1` або вказівник `null`), щоб вказати, що те, що повернула функція, не може розглядатись як звичайне значення. З іншого боку, Java та C# схиляються до використання виключень для обробки збоїв. Коли закидається виключення, контроль виконання перестрибує на деякий визначений вами код, що робить деяке очищення, і можливо пере-закидає виключення, так що інший код обробки помилок зможе потурбуватись про деякі інші речі.

Haskell має дуже гарну систему типів. Алгебраїчні типи даних дозволяють такі типи, як `Maybe` та `Either`, і ми можемо використовувати значення ціх типів для представлення результатів, що можуть або бути, або ні. Повернення в C, скажімо, `-1` в разі збою, повністю є предметом домовленості. Це також має особливе значення для людини. Якщо ми не будемо обережні, ми можемо розглядати ці незвичайні значення як звичайні, і потім вони можуть наробити безлад і жах в нашому коді. Система типів Haskell дає нам деяку таку-необхідну безпеку в цьому аспекті. Функція `a -> Maybe b` ясно вказує, що вона може продукувати `b` огорнуте в `Just`, або що вона може повернути `Nothing`. Це тип, відмінний від простого `a -> b`, і якщо ми спробуємо використати ці дві функції одну замість іншої, копілятор буде скаржитись на нас.

Не зважаючи на присутність виразних типів, що підтримують збійні обчислення, Haskell все ще має підтримку для виключень, оскільки вони мають більше сенсу в I/O контекстах. Багато речей можуть пійти не так, коли ви маєте справу з зовнішнім світом, тому що він такий ненадійний. Наприклад, коли відкривається файл, ціла кіпа речей може пійти не так. Файл може бути заблокований, він може взагалі бути відсутній, або може бути відсутній взагалі весь жорстуий диск, або будь-що ще. Так що це гарно бути в змозі перестрибнути на деяку частину обробки помилок в вашому коді, коли такі помилки трапляються.

Гаразд, так що I/O код (тобто нечистий код) може закидати виключення. Це має сенс. То що щодо чистого коду? Гаразд, він також може закидати виключення. Подумайте про функції `div` та `head`. Вони мають типи `(Integral a) => a -> a -> a` та `[a] -> a`, відповідно. Ні `Maybe` або `Either` в їх типах повернення, і вони обоє можуть схибити! `div` вибухне вам в обличчя, коли ви спробуєте поділити на ноль, та `head` впадає в істеріку, коли ви передасте їй порожній список.

[source,haskell]
----
ghci> 4 `div` 0  
*** Exception: divide by zero  
ghci> head []  
*** Exception: Prelude.head: empty list
----

image:images/police.png[]
Чистий код може закидати виключення, але вони можуть бути перехоплені в I/O частині нашого кода (коли ми всередині блока `do`, що іде в `main`). Це тому, що ви не знаєте коли (та взагалі чи) будь-що буде виконане в чистому коді, оскільки він лінивий, та не має гарно визначеного порядку виконання, тоді як I/O код має.

Раніше ми казали про те, що нам слідує проводити щонайменьше часу в  I/O частині нашої програми. Логіка нашої програми повинна здебільшого залишатись в наших чистих функціях, оскільки їх результати залежать лише від параметрів, з якими викликаються функції. Коли ми маємо справи з чистими функціями, ви маєте думати лише про те, що повертає функція, оскільки вона не може робити будь-що інше. Це робить ваше життя простіше. Навіть якщо і робити деяку логіку в I/O необхідно (як відкривати файли і подібні речі), повинно утримувати її на мінімумі. Чисті функції ліниві по замовчанню, що означає, що ми не знаємо, коли вони будуть обчислені, і що це насправді не повинно мати значення. Однак як тільки чисті функції починають закидати виключення, стає важливим, коли вони обчислюються. Ось чому ми лише можемо перехоплювати виключення, закинуті в чистому коді, в I/O частині нашого кода. І це погано, оскільки ми бажаємо утримувати I/O частину малою, наскільки це можливо. Однак якщо ми не перехоплюємо їх в I/O частині нашого коду, наша програма падає. Яке рішення? Не змішуйте виключення і чистий код. Отримуйте перевагу від потужної системи типів Haskell, і використовуйте типи як `Either` та `Maybe` для представлення результатів, що можуть схибити.

Ось чому поки що ми тільки подивимось, як використовувати I/O виключення. I/O виключення є виключення, що виникають коли щось іде не так при взаємодії з зовнішнім світом в I/O дії, що є частиною `main`. Наприклад, ми можемо спробувати відкрити файл, і потім з'ясовується, що файл був видалений або ще щось. Погляньте на цю програму, яка відкриває файл, чиє ім'я надається в аргументі командного рядка, і каже, скільки рядків має цей файл.

[source,haskell]
----
import System.Environment  
import System.IO  
  
main = do (fileName:_) <- getArgs  
          contents <- readFile fileName  
          putStrLn $ "The file has " ++ show (length (lines contents)) ++ " lines!"  
----

Дуже проста програма. Ми виконуємо I/O дію `getArgs`, і прикріплюємо перший рядок в списку, що вона видає, до `fileName`. Потім ми викликаємо `contents` файла з вмістом цього ім'я. Нарешті ми застосовуємо рядки цього `contents`, щоб отримати список рядків, і потім ми отримуємо довжину списка щоб надати її до `show`, щоб отримати рядкове представлення цього числа. Це робить як очікується, але що трапиться, коли ми дамо йому ім'я файла, що не існує?

[source,bash]
----
$ runhaskell linecount.hs i_dont_exist.txt  
linecount.hs: i_dont_exist.txt: openFile: does not exist (No such file or directory)  
----

Ага, ми отримали помилку від GHC, яке каже нам, що файл не існує. Наша програма падає. Що, коли ми бажаємо надрукувати краще повідомлення якщо файл не існує? Один спосіб зробити це є перевірити, чи файл існує, перед спробою відкрити його, використовуючи функцію `doesFileExist` з `System.Directory`.

[source,haskell]
----
import System.Environment  
import System.IO  
import System.Directory  
  
main = do (fileName:_) <- getArgs  
          fileExists <- doesFileExist fileName  
          if fileExists  
              then do contents <- readFile fileName  
                      putStrLn $ "The file has " ++ show (length (lines contents)) ++ " lines!"  
              else do putStrLn "The file doesn't exist!"  
----

Ми зробили `fileExists <- doesFileExist fileName`, оскільки `doesFileExist` має тип `doesFileExist :: FilePath -> IO Bool`, що означає, що вона повертає I/O дію, що має як результат логічне значення, яке каже нам чи файл існує. Ми не можемо просто використовувати `doesFileExist` в `if` виразі напряму.

Іншим рішенням тут може бути використання виключень. Це чудово припустиме використовувати їх в цьому контексті. Неіснуючий файл є виключенням, що походить від I/O, так що перехоплення його в I/O є гарним і пристойним.

Щоб впоратись з цім через використання виключень ми збираємось скористатись перевагами від функції `catch` з `System.IO.Error`. Її тип є `catch :: IO a -> (IOError -> IO a) -> IO a`. Вона приймає два параметри. Перший є I/O дією. Наприклад це може бути I/O дією, що намагається виконати файл. Другий є так званим обробником. Якщо перша I/O дія, парадана до `catch`, закидає I/O виключення, це виключення передається в обробник, який вирішує що робити далі. Так що фінальний результат є I/O дія, що буде діяти або як перший параметр, або буде робити те що каже обробник, якщо перша I/O дія закидає виключення.

image:images/puppy.png[]
Якщо ви знайомі з `try-catch` блоками в мовах як Java або Python, функція `catch` подібна до них. Перший параметр є річ, що треба спробувати, як щось таке в блоці `try` в інших, імперативних мовах. Другий параметр є обробником, що приймає виключення, як більшість  `catch` блоків приймають виключення, що ви потім можете перевіряти, щоб побачити, що трапилось. Обробник викликається, якщо виключення було закинуте.

Обробник бере значення типу `IOError`, що є значенням, яке вказує, що трапилось I/O виключення. Воно також несе інформацію відносно типу виключення, що було закинуте. Як цей тип реалізований залежить від реалізації самої мови, що означає, що ми не можемо досліджувати значення типу `IOError` через співпадіння шаблону, так само як ми не можемо порівнювати шаблони до значень типу `IO something`. Ми можемо використовувати декілька корисних предикатів, щоб отримувати корисну інформацію щодо значень типу `IOError`, які ми вивчино через мить.

Так що давайте застосуємо нового друга `catch`!

[source,haskell]
----
import System.Environment  
import System.IO  
import System.IO.Error  
  
main = toTry `catch` handler  
              
toTry :: IO ()  
toTry = do (fileName:_) <- getArgs  
           contents <- readFile fileName  
           putStrLn $ "The file has " ++ show (length (lines contents)) ++ " lines!"  
  
handler :: IOError -> IO ()  
handler e = putStrLn "Whoops, had some trouble!"  
----

Для початку ви бачите, що ми поклали зворотні апострофи кругом нього, так що ми можемо використовувати його як інфіксну функцію, бо він приймає два параметри. Його використання як інфіксної функції робить його більш читабельним. Так що `toTry ``catch`` handler` є те саме,що і `catch toTry handler`, що гарно пасує до його типу. `toTry` є I/O дія, яку ми намагаємось використати, і `handler` є функція, що приймає `IOError`, та повертає дію, яка виконується в випадку виключення.

Давайте надамо цьому руху:

[source,bash]
----
$ runhaskell count_lines.hs i_exist.txt  
The file has 3 lines!
$ runhaskell count_lines.hs i_dont_exist.txt  
Whoops, had some trouble!  
----

В обробнику ми не перевіряли, який тип `IOError` ми отримали. Ми просто кажемо `"Whoops, had some trouble!"` для любого типу помилок. Просте перехоплення всіх типів виключень в обробнику є поганою практикою в Haskell, так само як в більшості інших мов. Що коли трапляться деякі інші виключення, які ми не бажаємо перехоплювати, як переривання програми або щось інше? Ось чому ми бажаємо зробити ту саму річ, яку ми звичайно робимо також в інших мовах: ми перевіримо, який саме тип виключення ми отримали. Якщо це різновид виключення, який ми бажаємо перехопити, ми робимо наші речі. Якщо ні - ми знову перезапускаємо виключення. Давайте модифікуємо нашу програму для перехоплення тільки виключень, викликаних відсутністю файла.

[source,haskell]
----
import System.Environment  
import System.IO  
import System.IO.Error  
  
main = toTry `catch` handler  
              
toTry :: IO ()  
toTry = do (fileName:_) <- getArgs  
           contents <- readFile fileName  
           putStrLn $ "The file has " ++ show (length (lines contents)) ++ " lines!"  
  
handler :: IOError -> IO ()  
handler e  
    | isDoesNotExistError e = putStrLn "The file doesn't exist!"  
    | otherwise = ioError e  
----

Все залишається тим самим, крім обробника, який ми модифікували для перехоплення тільки певної групи I/O виключень. Тут ми використали дві нові функції з `System.IO.Error` — `isDoesNotExistError` та `ioError`. `isDoesNotExistError` є предикатом на `IOErrors`, що означає, що це функція, що приймає `IOError` і повертає `True` або `False`, що означає що вона має тип `isDoesNotExistError :: IOError -> Bool`. Ми використовуємо його на виключенні, що передано в наш обробник, щоб побачити, чи це помилка спричинена неіснуванням файла. Тут ми використали синтаксис охоронців, але також можна застосувати  `if else`. Якщо це не спричинено неіснуванням файла, ми пере-закидаєм виключення, що було передане в обробник за допомогою функції `ioError`. Вона має тип `ioError :: IOException -> IO a`, так що вона приймає `IOError` і продукує I/O дію, що буде закидати її. I/O має тип `IO a`, оскільки вона насправді ніколи не видає результат, так що вона може діяти як `IO anything`.

Так що якщо виключення, закинуте в I/O дії `toTry`, що ми склеїли разом в блок `do`, не спричинене неіснуванням файла, `toTry ``catch`` handler` буде перехоплювати, та потім пере-закидати його. Досить круто, так?

Існують декілька предикатів, що діють на `IOError`, і якщо охоронець не обчислюється до `True`, обчислення провалюється до наступного охоронця. Предикати, що діють на `IOError`, такі:

[source,haskell]
----
isAlreadyExistsError
isDoesNotExistError
isAlreadyInUseError
isFullError
isEOFError
isIllegalOperation
isPermissionError
isUserError
----

Більшість з цього є досить самоописовим. `isUserError` обислюється до `True`, коли ми використовуємо функцію `userError` для створення виключення, що використовується для створення виключень в нашому коді, і оснащення його рядком. Наприклад, ви можете зробити `ioError $ userError "remote computer unplugged!"`, хоча бажано щоб ви використовували типи як `Either` та `Maybe` для представлення можливого збою, замість закидання виключень самому за допомогою `userError`.

Так що ви можете мати обробник, що виглядає дещо подібно до наступного:

[source,haskell]
----
handler :: IOError -> IO ()  
handler e  
    | isDoesNotExistError e = putStrLn "The file doesn't exist!"  
    | isFullError e = freeSomeSpace  
    | isIllegalOperation e = notifyCops  
    | otherwise = ioError e  
----

де `notifyCops` та `freeSomeSpace` є деякі визначені вами I/O дії. Переконайтесь, що перезакинули виключення, якщо воно не співпало з жодною з ваших критерій, інакше ви спричините мовчазний крах вашого застосування в деяких випадках, де це не мало б статись.

`System.IO.Error` також експортує функції, що дозволяють опитувати наші виключення щодо деяких атрибутів, як що за вказівник файлу, який спричинив помилку, або яке ім'я файлу. Вони починаються на `ioe`, і ви можете побачити їх повний список в документації. Скажімо ми бажаємо надрукувати ім'я файлу, що спричинив нашу помилку. Ми не можемо надрукувати `fileName`, яке ми отримали від `getArgs`, оскільки в обробник надійшло тільки `IOError`, і обробник не знає більше нічого іншого. Функція залежить тільки від параметрів, з якими вона викликана. Ось чому ми можемо використати фуункцію `ioeGetFileName`, що має тип `ioeGetFileName :: IOError -> Maybe FilePath`. Вона приймає `IOError` як параметр і можливо повертає `FilePath` (що є тільки синонімом типу для `String`, пам'ятаємо, так що це різновид тієї самої речі). Те що вона в основному робить, це виділяє шлях файлу з `IOError`, якщо зможе. Давайте модифікуємо нашу програму для роздрукування нашого шляху файла, відповідального за виникле виключення.

[source,haskell]
----
import System.Environment     
import System.IO     
import System.IO.Error     
    
main = toTry `catch` handler     
                 
toTry :: IO ()     
toTry = do (fileName:_) <- getArgs     
           contents <- readFile fileName     
           putStrLn $ "The file has " ++ show (length (lines contents)) ++ " lines!"     
    
handler :: IOError -> IO ()     
handler e     
    | isDoesNotExistError e =   
        case ioeGetFileName e of Just path -> putStrLn $ "Whoops! File does not exist at: " ++ path  
                                 Nothing -> putStrLn "Whoops! File does not exist at unknown location!"  
    | otherwise = ioError e     
----

В охоронці, коли `isDoesNotExistError` є `True`, ми використали  `case` вираз для виклику `ioeGetFileName` з `e`, та потім порівняли шаблон щодо значення `Maybe`, яке він повертає. Застосування виразів `case` загально використовується коли ви бажаєте порівняти шаблон з дечим, не привносячи нову функцію.

Ви не маєте використовувати один обробник для перехоплення виключень у всій вашій частині I/O. Ви можете просто охопити певну частину вашого I/O коду в `catch`, або ви можете охопити деякі з ни в `catch`, та використовувати різні обробники для них, десь так:

[source,haskell]
----
main = do toTry `catch` handler1  
          thenTryThis `catch` handler2  
          launchRockets  
----

Тут `toTry` використовує `handler1` як обробник, та `thenTryThis` використовує `handler2`. `launchRockets` не є параметром для `catch`,так що якщо воно може закинути виключення, це має занапастити нашу програму, хіба що `launchRockets` використовує перехоплення внутрішньо для обробки своїх власних виключень. Звичайно, `toTry`, `thenTryThis` та `launchRockets` є I/O дії що мають бути склеєні разом з використанням синтаксису `do`, і гіпотетично визначені деінде в іншому місці. Це дещо подібно до блоків `try-catch` в інших мовах, де ви можете оточити всю вашу програму в єдиний `try-catch`, або ви можете використати більш поміркований підхід, та використовувати різні блоки в різних частинах вашого коду, щоб контролювати, який тип обробки помилок відбувається.

Тепер ви знаєте, як мати справи з I/O виключеннями! Закидання виключень з чистого коду та справи з ними тут не були розглянуті, здебільшого через те, що, як ми казали, Haskell пропонує значно кращий спосіб вказувати на помилки, ніж звертатись до I/O для їх відловлення. Навіть при склеюванні разом I/O дій що можуть схибити, я схиляюсь мати їх тип дещо на кшталт `IO (Either a b)`, що означає, що вони є нормальними I/O діями, але результат, що вони видають після виконання, матиме тип `Either a b`, що означає або `Left a`, або `Right b`.